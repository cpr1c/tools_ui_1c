&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	КопироватьДанныеФормы(Параметры.Объект, Объект);

	Обработка = РеквизитФормыВЗначение("Объект");

	Заголовок = Параметры.Заголовок + " (таблица значений)";

	Таблица = Обработка.СтрокаВЗначение(Параметры.Значение.Значение);

	Обработка.СоздатьРеквизитыТаблицыПоКолонкам(ЭтаФорма, "ТаблицаЗначений", "ТаблицаЗначенийСоответствиеКолонок",
		"ТаблицаЗначенийКолонкиКонтейнера", Таблица.Колонки, Истина);

	Обработка.ТаблицаВРеквизитыФормы(Таблица, ТаблицаЗначений, ТаблицаЗначенийКолонкиКонтейнера);

	фТолькоПросмотр = Ложь;
	Если Параметры.Свойство("ТолькоПросмотр", фТолькоПросмотр) И фТолькоПросмотр = Истина Тогда
		Элементы.ТаблицаЗначений.ТолькоПросмотр = Истина;
		Элементы.КнопкаОК.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПолучитьВозвращаемуюТаблицу()
	Обработка = РеквизитФормыВЗначение("Объект");
	тзВозвращаемаяТаблица = Обработка.ТаблицаИзРеквизитовФормы(ТаблицаЗначений, ТаблицаЗначенийКолонкиКонтейнера);
	Возврат Обработка.Контейнер_СохранитьЗначение(тзВозвращаемаяТаблица);
КонецФункции

&НаСервере
Процедура ИнициализироватьКонтейнерыСтрокиПоТипам(чСтрока, ТаблицаЗначенийКолонкиКонтейнера)
	РеквизитФормыВЗначение("Объект").ИнициализироватьКонтейнерыСтрокиПоТипам(ТаблицаЗначений.НайтиПоИдентификатору(
		чСтрока), ТаблицаЗначенийКолонкиКонтейнера);
КонецПроцедуры

&НаКлиенте
Процедура ПолеТаблицыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Перем Контейнер;

	стрИмяКолонки = ТаблицаЗначенийСоответствиеКолонок[Элемент.Имя];
	стрИмяКолонкиКонтейнера = стрИмяКолонки + "_31415926Контейнер";

	Если ТаблицаЗначенийКолонкиКонтейнера.Свойство(стрИмяКолонки) Тогда

		СтрокаТаблицы = ТаблицаЗначений.НайтиПоИдентификатору(Элементы.ТаблицаЗначений.ТекущаяСтрока);
		Контейнер = СтрокаТаблицы[стрИмяКолонкиКонтейнера];

		Если Не ЗначениеЗаполнено(Контейнер) Тогда
			ИнициализироватьКонтейнерыСтрокиПоТипам(Элементы.ТаблицаЗначений.ТекущаяСтрока,
				ТаблицаЗначенийКолонкиКонтейнера);
			Контейнер = СтрокаТаблицы[стрИмяКолонкиКонтейнера];
		КонецЕсли;

		Если ЗначениеЗаполнено(Контейнер.Тип) Тогда

			Если Контейнер.Тип = "Тип" Тогда
				СтандартнаяОбработка = Ложь;
				ПараметрыОповещения = Новый Структура("Таблица, Строка, Поле", "ТаблицаЗначений",
					Элементы.ТаблицаЗначений.ТекущаяСтрока, стрИмяКолонки);
				ОписаниеОповещенияОЗакрытииОткрываемойФормы = Новый ОписаниеОповещения("ОкончаниеРедактированияСтроки",
					ЭтаФорма, ПараметрыОповещения);
				ПараметрыОткрытия = Новый Структура("Объект, ТипЗначения", Объект, Контейнер);
				ОткрытьФорму("Обработка.УИ_КонсольЗапросов.Форма.РедактированиеТипа", ПараметрыОткрытия, ЭтаФорма,
					Истина, , , ОписаниеОповещенияОЗакрытииОткрываемойФормы,
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			ИначеЕсли Контейнер.Тип = "МоментВремени" Тогда
				СтандартнаяОбработка = Ложь;
				ПараметрыОповещения = Новый Структура("Таблица, Строка, Поле", "ТаблицаЗначений",
					Элементы.ТаблицаЗначений.ТекущаяСтрока, стрИмяКолонки);
				ОписаниеОповещенияОЗакрытииОткрываемойФормы = Новый ОписаниеОповещения("ОкончаниеРедактированияСтроки",
					ЭтаФорма, ПараметрыОповещения);
				ПараметрыОткрытия = Новый Структура("Объект, Значение", Объект, Контейнер);
				ОткрытьФорму("Обработка.УИ_КонсольЗапросов.Форма.РедактированиеГраницыМомента", ПараметрыОткрытия,
					ЭтаФорма, Ложь, , , ОписаниеОповещенияОЗакрытииОткрываемойФормы,
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеРедактированияСтроки(РезультатЗакрытия, ДополнительныеПараметры) Экспорт

	Если РезультатЗакрытия <> Неопределено Тогда

		чИдентификаторСтроки = ДополнительныеПараметры.Строка;
		Значение = Неопределено;
		Если РезультатЗакрытия.Свойство("Значение", Значение) Тогда
			ТаблицаЗначений[чИдентификаторСтроки][ДополнительныеПараметры.Поле + "_31415926Контейнер"] = Значение;
			ТаблицаЗначений[чИдентификаторСтроки][ДополнительныеПараметры.Поле] = Значение.Представление;
		Иначе
			ТаблицаЗначений[чИдентификаторСтроки][ДополнительныеПараметры.Поле
				+ "_31415926Контейнер"] = РезультатЗакрытия.ОписаниеКонтейнера;
			ТаблицаЗначений[чИдентификаторСтроки][ДополнительныеПараметры.Поле] = РезультатЗакрытия.ОписаниеКонтейнера.Представление;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КомандаОчистить(Команда)
	ТаблицаЗначений.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	ВозвращаемоеЗначение = Новый Структура("Значение", ПолучитьВозвращаемуюТаблицу());
	Закрыть(ВозвращаемоеЗначение);
КонецПроцедуры