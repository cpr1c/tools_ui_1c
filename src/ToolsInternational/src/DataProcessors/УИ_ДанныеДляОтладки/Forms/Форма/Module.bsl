&НаКлиенте
Procedure ВыполнитьОтладку(Команда)
	ТекДанные=Элементы.ТаблицаСохраненныхНастроек.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	UT_CommonClient.RunDebugConsoleByDebugDataSettingsKey(ТекДанные.КлючНастроек);

EndProcedure

&НаКлиенте
Procedure ОбновитьТаблицу(Команда)
	ОбновитьТаблицуНаСервере();
EndProcedure

&НаСервере
Procedure ОбновитьТаблицуНаСервере()
	ТаблицаСохраненныхНастроек.Очистить();

	СтруктураПоиска=Новый Структура;
	СтруктураПоиска.Вставить("КлючОбъекта", КлючОбъектаНастроек);

	Выборка=ХранилищеСистемныхНастроек.Выбрать(СтруктураПоиска);

	Пока Выборка.Следующий() Цикл
		НоваяСтрока=ТаблицаСохраненныхНастроек.Добавить();
		НоваяСтрока.КлючНастроек=Выборка.КлючНастроек;
		НоваяСтрока.Пользователь=Выборка.Пользователь;

		МассивКлючаНастроек=СтрРазделить(НоваяСтрока.КлючНастроек, "/");

		НоваяСтрока.Автор=МассивКлючаНастроек[1];
		НоваяСтрока.ТипОбъектаОтладки=МассивКлючаНастроек[0];
		Попытка
			НоваяСтрока.ДатаСоздания=Дата(МассивКлючаНастроек[2]);
		Исключение
			НоваяСтрока.ДатаСоздания="";
		КонецПопытки;

	КонецЦикла;

	ТаблицаСохраненныхНастроек.Сортировать("ДатаСоздания УБЫВ");
EndProcedure

&НаКлиенте
Procedure Удалить(Команда)

	ВыделенныеСтроки = Элементы.ТаблицаСохраненныхНастроек.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	УдалитьВыделенныеСтроки(ВыделенныеСтроки);

EndProcedure

&НаСервере
Procedure УдалитьВыделенныеСтроки(Знач ВыделенныеСтроки)

	Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		УдалитьНаСервере(ВыделеннаяСтрока);
	КонецЦикла; 	
	ОбновитьТаблицуНаСервере();

EndProcedure // УдалитьВыделенныеСтроки()
 
&НаСервере
Процедура УдалитьНаСервере(ТекущаяСтрока)

	СтрокаТЧ = ТаблицаСохраненныхНастроек.НайтиПоИдентификатору(ТекущаяСтрока);

	UT_Common.ХранилищеСистемныхНастроекУдалить(КлючОбъектаНастроек, СтрокаТЧ.КлючНастроек,
		СтрокаТЧ.Пользователь);

КонецПроцедуры

&НаСервере
Procedure ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	КлючОбъектаНастроек=UT_CommonClientServer.DebuggingDataObjectDataKeyInSettingsStorage();

	ОбновитьТаблицуНаСервере();
EndProcedure