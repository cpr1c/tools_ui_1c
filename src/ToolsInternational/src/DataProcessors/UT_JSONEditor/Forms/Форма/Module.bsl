&AtClient
Перем ЗакрытиеФормыПодтверждено;
#Region СобытияФормы

&AtServer
Procedure ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьАдресБиблиотекиНаСервере();

	If Параметры.Свойство("СтрокаJSON") Then
		РедактируемаяСтрока=Параметры.СтрокаJSON;
		РежимРедактирования=True;
	EndIf;

	If Параметры.Свойство("РежимПросмотра") Then
		РежимРедактирования=Not Параметры.РежимПросмотра;
	EndIf;
	Items.ФормаЗавершитьРедактирование.Видимость=РежимРедактирования;

	UT_Common.ToolFormOnCreateAtServer(ЭтотОбъект, Отказ, СтандартнаяОбработка);

EndProcedure

&AtClient
Procedure ПриОткрытии(Отказ)
	UT_CommonClient.AttachFileSystemExtensionWithPossibleInstallation(
		New NotifyDescription("ПриОткрытииЗавершение", ЭтотОбъект));
EndProcedure
&AtClient
Procedure ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	If Not ЗакрытиеФормыПодтверждено Then
		Отказ = True;
		Return;
	EndIf;

	НачатьУдалениеФайлов(New NotifyDescription("ПередЗакрытиемЗавершениеУдаленияФайлов", ЭтаФорма), КаталогСохраненияБибилиотеки);
EndProcedure

&AtClient
Procedure ПередЗакрытиемЗавершениеУдаленияФайлов(ДополнительныеПараметры) Экспорт
	
	

EndProcedure

#EndRegion

#Region СобытияЭлементовФормы

&AtClient
Procedure ПолеРедактораДокументСформирован(Элемент)
	If ЗначениеЗаполнено(РедактируемаяСтрока) Then
		ПодключитьОбработчикОжидания("ОбработчикОжиданияУстановитьРедактируемуюСтрокуВРедакторДерево", 0.5, True);
	EndIf;
EndProcedure

&AtClient
Procedure ПолеРедактораСтрокаДокументСформирован(Элемент)
	If ЗначениеЗаполнено(РедактируемаяСтрока) Then
		ПодключитьОбработчикОжидания("ОбработчикОжиданияУстановитьРедактируемуюСтрокуВРедакторСтроки", 0.1, True);
	EndIf;
EndProcedure

&AtClient
Procedure СкопироватьИзСтрокиВДерево(Команда)
	СтрокаJSON=СтрокаJSONИзПоляРедактора(Items.ПолеРедактораСтрока);
	СтрокаДерева=СтрокаJSONИзПоляРедактора(Items.ПолеРедактораДерево);

	УстановитьJSONВHTML(Items.ПолеРедактораДерево, СтрокаJSON);
	If Not ЗначениеЗаполнено(СтрокаДерева) Then
		РазвернутьСтрокиДереваJSON(Items.ПолеРедактораДерево);
	EndIf;
EndProcedure

&AtClient
Procedure СкопироватьИзДереваВСтроку(Команда)
	СтрокаJSON=СтрокаJSONИзПоляРедактора(Items.ПолеРедактораДерево);
	УстановитьJSONВHTML(Items.ПолеРедактораСтрока, СтрокаJSON);
EndProcedure

&AtClient
Procedure ЗавершитьРедактирование(Команда)
	ЗакрытиеФормыПодтверждено=True;
	Закрыть(СтрокаJSONИзПоляРедактора(Items.ПолеРедактораСтрока));
EndProcedure

//@skip-warning
&AtClient
Procedure Подключаемый_ВыполнитьОбщуюКомандуИнструментов(Команда) 
	UT_CommonClient.Attachable_ExecuteToolsCommonCommand(ЭтотОбъект, Команда);
EndProcedure



#EndRegion

#Region СлужебныеПроцедурыИФункции

&AtClient
Procedure ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	СтруктураФайловыхПеременных=UT_CommonClient.SessionFileVariablesStructure();
	КаталогСохраненияБибилиотеки=СтруктураФайловыхПеременных.TempFilesDirectory + "tools_ui_1c"
		+ ПолучитьРазделительПути() + Формат(UT_CommonClientServer.Version(), "ЧГ=0;") + ПолучитьРазделительПути() + "jsoneditor";
	ФайлРедактора=New Файл(КаталогСохраненияБибилиотеки);
	ФайлРедактора.НачатьПроверкуСуществования(New NotifyDescription("ПриОткрытииЗавершениеПроверкиСуществованияБиблиотеки", ЭтаФорма));

EndProcedure

&AtClient
Procedure ПриОткрытииЗавершениеПроверкиСуществованияБиблиотеки(Существует, ДополнительныеПараметры1) Экспорт
	
	If Существует Then
		НачатьУдалениеФайлов(New NotifyDescription("ПриОткрытииЗавершениеПроверкиСуществованияБиблиотекиЗавершениеУдаленияФайлов", ЭтаФорма,,"ПриОткрытииЗавершениеПроверкиСуществованияБиблиотекиЗавершениеОшибкаУдаленияФайлов", ЭтотОбъект), КаталогСохраненияБибилиотеки);
	Else
		ПриОткрытииЗавершениеПроверкиСуществованияБиблиотекиФрагмент();
	EndIf;
	
EndProcedure

&AtClient
Procedure ПриОткрытииЗавершениеПроверкиСуществованияБиблиотекиЗавершениеУдаленияФайлов(ДополнительныеПараметры) Экспорт
	ПриОткрытииЗавершениеПроверкиСуществованияБиблиотекиФрагмент();
EndProcedure  

&AtClient
Procedure ПриОткрытииЗавершениеПроверкиСуществованияБиблиотекиЗавершениеОшибкаУдаленияФайлов(ДополнительныеПараметры) Экспорт
	КаталогСохраненияБибилиотеки=КаталогСохраненияБибилиотеки + "1";
	
	НачатьУдалениеФайлов(New NotifyDescription("ПриОткрытииЗавершениеПроверкиСуществованияБиблиотекиЗавершениеУдаленияФайлов", ЭтаФорма,,"ПриОткрытииЗавершениеПроверкиСуществованияБиблиотекиЗавершениеОшибкаУдаленияФайлов", ЭтотОбъект), КаталогСохраненияБибилиотеки);
EndProcedure  



&AtClient
Procedure ПриОткрытииЗавершениеПроверкиСуществованияБиблиотекиФрагмент()
	
	НачатьСозданиеКаталога(New NotifyDescription("ПриОткрытииЗавершениеСозданияКаталогаБиблиотеки", ЭтаФорма), КаталогСохраненияБибилиотеки);

EndProcedure

&AtClient
Procedure ПриОткрытииЗавершениеСозданияКаталогаБиблиотеки(ИмяКаталога, ДополнительныеПараметры) Экспорт
	
	СохранитьБиблиотекуРедактораНаДиск();
	УстановитьТекстHTMLПоляРедактора();

EndProcedure

&AtClient
Procedure ОбработчикОжиданияУстановитьРедактируемуюСтрокуВРедакторДерево()
	Try
		УстановитьJSONВHTML(Items.ПолеРедактораДерево, РедактируемаяСтрока);
		РазвернутьСтрокиДереваJSON(Items.ПолеРедактораДерево);
	Except
		ПодключитьОбработчикОжидания("ОбработчикОжиданияУстановитьРедактируемуюСтрокуВРедакторДерево", 0.5, True);
	EndTry;
EndProcedure

&AtClient
Procedure ОбработчикОжиданияУстановитьРедактируемуюСтрокуВРедакторСтроки()
	Try
		УстановитьJSONВHTML(Items.ПолеРедактораСтрока, РедактируемаяСтрока);
		//Форматируем строку JSON по формату редактора
		УстановитьJSONВHTML(Items.ПолеРедактораСтрока, СтрокаJSONИзПоляРедактора(Элементы.ПолеРедактораСтрока));

	Except
		ПодключитьОбработчикОжидания("ОбработчикОжиданияУстановитьРедактируемуюСтрокуВРедакторСтроки", 0.5, True);
	EndTry;
EndProcedure

&AtClient
Procedure УстановитьJSONВHTML(ЭлементПоляРедактора, СтрокаJSON)
	ДокументHTML=ЭлементПоляРедактора.Документ;
	If ДокументHTML.parentWindow = Undefined Then
		СтруктураДокументаДОМ = ДокументHTML.defaultView;
	Else
		СтруктураДокументаДОМ = ДокументHTML.parentWindow;
	EndIf;
	СтруктураДокументаДОМ.editor.setText(СтрокаJSON);

EndProcedure

&AtClient
Procedure РазвернутьСтрокиДереваJSON(ЭлементПоляРедактора)
	ДокументHTML=ЭлементПоляРедактора.Документ;
	If ДокументHTML.parentWindow = Undefined Then
		СтруктураДокументаДОМ = ДокументHTML.defaultView;
	Else
		СтруктураДокументаДОМ = ДокументHTML.parentWindow;
	EndIf;
	СтруктураДокументаДОМ.editor.expandAll();

EndProcedure

&AtClient
Function СтрокаJSONИзПоляРедактора(ЭлементПоляРедактора)
	ДокументHTML=ЭлементПоляРедактора.Документ;
	If ДокументHTML.parentWindow = Undefined Then
		СтруктураДокументаДОМ = ДокументHTML.defaultView;
	Else
		СтруктураДокументаДОМ = ДокументHTML.parentWindow;
	EndIf;
//	Возврат СтруктураДокументаДОМ.editor.getText();
	Return СтруктураДокументаДОМ.getJSON();

EndFunction

&AtClient
Procedure УстановитьТекстHTMLПоляРедактора()
	ТекстCSS=КаталогСохраненияБибилиотеки + ПолучитьРазделительПути() + "jsoneditor.css";
	ТекстJS=КаталогСохраненияБибилиотеки + ПолучитьРазделительПути() + "jsoneditor.js";

	Шаблон= "<!DOCTYPE HTML>
			|<html>
			|<head>
			|  <title>JSONEditor | Synchronize two editors</title>
			|
			|	<link href=""" + ТекстCSS + """ rel=""stylesheet"" type=""text/css"">
											 |  <script src=""" + ТекстJS + """></script>
																			|
																			|  <style type=""text/css"">
																			|    body {
																			|      font-family: sans-serif;
																			|    }
																			|
																			|   .jsoneditor {
																			|      width: 100%;
																			|      height: 100%;
																			|    }
																			|  </style>
																			|</head>
																			|<body>
																			|	<div class=""jsoneditor"" id=""jsoneditor""></div>
																			|
																			|<script>
																			|
																			|  var container = document.getElementById('jsoneditor')
																			|  var options = {
																			|    // switch between pt-BR or en for testing forcing a language
																			|    // leave blank to get language
																			|   'language': 'ru-RU',
																			|   mode: '###РежимРедактора###'
																			|  }
																			|  var editor = new JSONEditor(container, options)
																			|
																			|	function getJSON(){
																			|		return JSON.stringify(editor.get(), null, 2);
																			|	}
																			|
																			|
																			|</script>
																			|	<div id=""footer""></div>
																			|
																			|</body>
																			|</html>";

	СохранитьФайлHTMLПоляРедатора(СтрЗаменить(Шаблон, "###РежимРедактора###", "tree"), КаталогСохраненияБибилиотеки + ПолучитьРазделительПути() + "tree.html" ,"ПолеРедактораДерево");
	СохранитьФайлHTMLПоляРедатора(СтрЗаменить(Шаблон, "###РежимРедактора###", "code"), КаталогСохраненияБибилиотеки + ПолучитьРазделительПути() + "code.html","ПолеРедактораСтрока");
EndProcedure

&AtClient
Procedure СохранитьФайлHTMLПоляРедатора(ТекстHTML, ИмяФайла, ИмяПоляРедактора)
	Текст=New ТекстовыйДокумент;
	Текст.УстановитьТекст(ТекстHTML);
	
	ДопПараметры=New Structure;
	ДопПараметры.Insert("ИмяПоляРедактора", ИмяПоляРедактора);
	ДопПараметры.Insert("ИмяФайла", ИмяФайла);
	
	Текст.НачатьЗапись(New NotifyDescription("СохранитьФайлHTMLПоляРедатораЗаверешение", ЭтотОбъект, ДопПараметры), ИмяФайла);
EndProcedure

&AtClient
Procedure СохранитьФайлHTMLПоляРедатораЗаверешение(Результат, ДополнительныеПараметры) Экспорт
	ЭтотОбъект[ДополнительныеПараметры.ИмяПоляРедактора] = ДополнительныеПараметры.ИмяФайла;
EndProcedure

&AtClient
Procedure СохранитьБиблиотекуРедактораНаДиск()
	СоответствиеФайловБиблиотеки=ПолучитьИзВременногоХранилища(АдресБиблиотеки);
	For Each КлючЗначение In СоответствиеФайловБиблиотеки Do
		ИмяФайла=КаталогСохраненияБибилиотеки + ПолучитьРазделительПути() + КлючЗначение.Ключ;

		КлючЗначение.Значение.Записать(ИмяФайла);
	EndDo;
EndProcedure

&AtServer
Procedure УстановитьАдресБиблиотекиНаСервере()
	ОбработкаОбъект=РеквизитФормыВЗначение("Объект");

	ДвоичныеДанныеБиблиотеки=ОбработкаОбъект.ПолучитьМакет("jsoneditor");

	КаталогНаСервере=ПолучитьИмяВременногоФайла();
	СоздатьКаталог(КаталогНаСервере);

	Поток=ДвоичныеДанныеБиблиотеки.ОткрытьПотокДляЧтения();

	ЧтениеZIP=New ЧтениеZipФайла(Поток);
	ЧтениеZIP.ИзвлечьВсе(КаталогНаСервере, РежимВосстановленияПутейФайловZIP.Восстанавливать);

	СтруктураБиблиотеки=New Map;

	ФайлыАрхива=НайтиФайлы(КаталогНаСервере, "*", True);
	For Each ФайлБиблиотеки In ФайлыАрхива Do
		КлючФайла=СтрЗаменить(ФайлБиблиотеки.ПолноеИмя, КаталогНаСервере + ПолучитьРазделительПути(), "");
		If ФайлБиблиотеки.ЭтоКаталог() Then
			Continue;
		EndIf;

		СтруктураБиблиотеки.Insert(КлючФайла, New ДвоичныеДанные(ФайлБиблиотеки.ПолноеИмя));
	EndDo;

	АдресБиблиотеки=ПоместитьВоВременноеХранилище(СтруктураБиблиотеки, УникальныйИдентификатор);

	Try
		УдалитьФайлы(КаталогНаСервере);
	Except
		// TODO:
	EndTry;

EndProcedure

#EndRegion

#Region СтандартныеПроцедурыИнструментов

&AtClient
Function СтруктураОписанияСохраняемогоФайла()
	Структура=UT_CommonClient.EmptyDescriptionStructureOfSelectedFile();
	Структура.ИмяФайла=ИмяФайлаДанныхИнструмента;

	UT_CommonClient.AddFormatToSavingFileDescription(Структура, "Файл JSOM(*.json)", "json");
	Return Структура;
EndFunction
&AtClient
Procedure ОткрытьФайл(Команда)
	UT_CommonClient.ReadConsoleFromFile("РедактовJSON", СтруктураОписанияСохраняемогоФайла(),
		New NotifyDescription("ОткрытьФайлЗавершение", ЭтотОбъект));
EndProcedure

&AtClient
Procedure ОткрытьФайлЗавершение(Результат, ДополнительныеПараметры) Экспорт
	If Результат = Undefined Then
		Return;
	EndIf;

	Модифицированность=False;
	ИмяФайлаДанныхИнструмента = Результат.ИмяФайла;

	ДанныеФайла=ПолучитьИзВременногоХранилища(Результат.Адрес);

	Текст=New ТекстовыйДокумент;
	Текст.НачатьЧтение(New NotifyDescription("ОткрытьФайлЗавершениеЧтенияТекста", ЭтаФорма, New Structure("Текст", Текст)), ДанныеФайла.ОткрытьПотокДляЧтения());
EndProcedure

&AtClient
Procedure ОткрытьФайлЗавершениеЧтенияТекста(ДополнительныеПараметры1) Экспорт
	
	Текст = ДополнительныеПараметры1.Текст;
	
	УстановитьJSONВHTML(Items.ПолеРедактораСтрока, Текст.ПолучитьТекст());
	УстановитьJSONВHTML(Items.ПолеРедактораДерево, Текст.ПолучитьТекст());
	УстановитьЗаголовок();

EndProcedure

&AtClient
Procedure СохранитьФайл(Команда)
	СохранитьФайлНаДиск();
EndProcedure

&AtClient
Procedure СохранитьФайлКак(Команда)
	СохранитьФайлНаДиск(True);
EndProcedure

&AtClient
Procedure СохранитьФайлНаДиск(СохранитьКак = False)
	UT_CommonClient.SaveConsoleDataToFile("РедактовHTML", СохранитьКак,
		СтруктураОписанияСохраняемогоФайла(), СтрокаJSONИзПоляРедактора(Items.ПолеРедактораСтрока),
		New NotifyDescription("СохранитьФайлЗавершение", ЭтотОбъект));
EndProcedure

&AtClient
Procedure СохранитьФайлЗавершение(ИмяФайлаСохранения, ДополнительныеПараметры) Экспорт
	If ИмяФайлаСохранения = Undefined Then
		Return;
	EndIf;

	If Not ЗначениеЗаполнено(ИмяФайлаСохранения) Then
		Return;
	EndIf;

	Модифицированность=False;
	ИмяФайлаДанныхИнструмента=ИмяФайлаСохранения;
	УстановитьЗаголовок();
EndProcedure

&AtClient
Procedure НовыйФайл(Команда)
	ИмяФайлаДанныхИнструмента="";

	УстановитьJSONВHTML(Items.ПолеРедактораСтрока, "");
	УстановитьJSONВHTML(Items.ПолеРедактораДерево, "");

	УстановитьЗаголовок();
EndProcedure

&AtClient
Procedure УстановитьЗаголовок()
	Заголовок=ИмяФайлаДанныхИнструмента;
EndProcedure

&AtClient
Procedure ЗакрытьИнструмент(Команда)
	ПоказатьВопрос(New NotifyDescription("ЗакрытьИнструментЗавершение", ЭтаФорма), "Выйти из редактора?",
		РежимДиалогаВопрос.ДаНет);
EndProcedure

&AtClient
Procedure ЗакрытьИнструментЗавершение(Результат, ДополнительныеПараметры) Экспорт

	If Результат = КодВозвратаДиалога.Да Then
		ЗакрытиеФормыПодтверждено = True;
		Закрыть();
	EndIf;

EndProcedure

#EndRegion

ЗакрытиеФормыПодтверждено=False;