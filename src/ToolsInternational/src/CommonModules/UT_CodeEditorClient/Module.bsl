#Область ПрограммныйИнтерфейс

Процедура FormOnOpen(Форма, ОписаниеОповещенияОЗавершении = Неопределено) Экспорт
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ОписаниеОповещенияОЗавершении", ОписаниеОповещенияОЗавершении);
	ДопПараметры.Вставить("Форма", Форма);

	UT_CommonClient.AttachFileSystemExtensionWithPossibleInstallation(
			Новый ОписаниеОповещения("ФормаПриОткрытииЗавершениеПодключенияРасширенияРаботыСФайлами", ЭтотОбъект,
		ДопПараметры));
КонецПроцедуры

Функция ВсеРедакторыФормыИнициализированы(РедакторыФормы)
	Результат = Истина;
	Для Каждого КлючЗначение Из РедакторыФормы Цикл
		Если Не КлючЗначение.Значение.Инициализирован Тогда
			Результат = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;
КонецФункции

Процедура ИнициализироватьРедаторыФормыПослеФормированияПолей(Форма, РедакторыФормы, ВидРедактора, ВидыРедактора)
	Для Каждого КлючЗначение Из РедакторыФормы Цикл
		ПараметрыРедактора = КлючЗначение.Значение;
		ЭлементФормыРедактора = Форма.Элементы[ПараметрыРедактора.ПолеРедактора];
		Если Не ЭлементФормыРедактора.Видимость Тогда
			Продолжить;
		КонецЕсли;
			
		Если ВидРедактора = ВидыРедактора.Текст Тогда
			Если ЗначениеЗаполнено(ПараметрыРедактора.ПараметрыРедактора.РазмерШрифта) Тогда
				ЭлементФормыРедактора.Шрифт = Новый Шрифт(, ПараметрыРедактора.ПараметрыРедактора.РазмерШрифта);
			КонецЕсли;
		ИначеЕсли ВидРедактора = ВидыРедактора.Ace Тогда 
			ДокументView = ЭлементФормыРедактора.Документ.defaultView;
			Если ЗначениеЗаполнено(ПараметрыРедактора.ПараметрыРедактора.РазмерШрифта) Тогда
				ДокументView.editor.setFontSize(ПараметрыРедактора.ПараметрыРедактора.РазмерШрифта);		
			КонецЕсли;
		ИначеЕсли ВидРедактора = ВидыРедактора.Monaco Тогда
			ДокументView = ЭлементФормыРедактора.Документ.defaultView;
			ДокументView.setOption("autoResizeEditorLayout", True);

			Инфо = Новый СистемнаяИнформация;
			ДокументView.init(Инфо.ВерсияПриложения);
			ДокументView.hideScrollX();
			ДокументView.hideScrollY();
			ДокументView.showStatusBar();
			ДокументView.enableQuickSuggestions();
			Если ЗначениеЗаполнено(ПараметрыРедактора.ПараметрыРедактора.РазмерШрифта) Тогда
				ДокументView.setFontSize(ПараметрыРедактора.ПараметрыРедактора.РазмерШрифта);
			КонецЕсли;
			Если ЗначениеЗаполнено(ПараметрыРедактора.ПараметрыРедактора.ВысотаСтрок) Тогда
				ДокументView.setLineHeight(ПараметрыРедактора.ПараметрыРедактора.ВысотаСтрок);
			КонецЕсли;

			ДокументView.disableKeyBinding(9);
			ДокументView.setOption("dragAndDrop", Истина);

			ТемыРедактора = UT_CodeEditorClientServer.ВариантыТемыРедактораMonaco();
			Если ПараметрыРедактора.ПараметрыРедактора.Тема = ТемыРедактора.Темная Тогда
				ДокументView.setTheme("bsl-dark");
			Иначе
				ДокументView.setTheme("bsl-white");
			КонецЕсли;

			ЯзыкиРедактора = UT_CodeEditorClientServer.ВариантыЯзыкаСинтаксисаРедактораMonaco();
			Если ПараметрыРедактора.ПараметрыРедактора.ЯзыкСинтаксиса = ЯзыкиРедактора.Английский Тогда
				ДокументView.switchLang();
			ИначеЕсли ПараметрыРедактора.ПараметрыРедактора.ЯзыкСинтаксиса = ЯзыкиРедактора.Авто Тогда
				ЯзыкСинтаксиса = UT_ApplicationParameters["ЯзыкСинтаксисаКонфигурации"];
				Если ЯзыкСинтаксиса = "Английский" Тогда
					ДокументView.switchLang();
				КонецЕсли;
			КонецЕсли;

			ДокументView.minimap(ПараметрыРедактора.ПараметрыРедактора.ИспользоватьКартуКода);

			Если ПараметрыРедактора.ПараметрыРедактора.СкрытьНомераСтрок Тогда
				ДокументView.hideLineNumbers();
			КонецЕсли;

			ДокументView.clearMetadata();

			ОписаниеКонфигурацииДляИнициализации = ОписаниеМетаданныйДляИнициализацииРедактораMonaco();

	//		МетаданныеКонфигурации = ОписаниеМетаданныхКонфигурацииДляРедактораMonaco();
			ДокументView.updateMetadata(UT_CommonClientServer.mWriteJSON(
				ПолучитьСписокОбъектовМетаданныхИзКоллекцииДляРедактораMonaco(
				ОписаниеКонфигурацииДляИнициализации.ОбщиеМодули)), "commonModules.items");
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура CodeEditorDeferredInitializingEditors(Форма) Экспорт
	ВидРедактора = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаВидРедактора()];
	ВидыРедактора = UT_CodeEditorClientServer.ВариантыРедактораКода();
	РедакторыФормы = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаСписокРедакторовФормы()];

	ИнициализироватьРедаторыФормыПослеФормированияПолей(Форма, РедакторыФормы, ВидРедактора, ВидыРедактора);
	Форма.Attachable_CodeEditorInitializingCompletion();
//	Форма.Attachable_CodeEditorInitializingCompletion(УИ_РедакторКодаКлиентСервер.ИдентификаторРедактораПоЭлементуФормы(Форма, Элемент));
КонецПроцедуры

Процедура HTMLEditorFieldDocumentGenerated(Форма, Элемент) Экспорт
	ИдентификаторРедактора = UT_CodeEditorClientServer.ИдентификаторРедактораПоЭлементуФормы(Форма, Элемент);
	РедакторыФормы = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаСписокРедакторовФормы()];

	ПараметрыРедактора = РедакторыФормы[ИдентификаторРедактора];
	ПараметрыРедактора.Вставить("Инициализирован", Истина);

	Если Не ВсеРедакторыФормыИнициализированы(РедакторыФормы) Тогда
		Возврат;
	КонецЕсли;
	Форма.ПодключитьОбработчикОжидания("Attached_CodeEditorDeferredInitializingEditors", 0.1, Истина);
КонецПроцедуры

Процедура HTMLEditorFieldOnClick(Форма, Элемент, ДанныеСобытия, СтандартнаяОбработка) Экспорт
	ВидРедактора = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаВидРедактора()];
	ВидыРедактора = UT_CodeEditorClientServer.ВариантыРедактораКода();

	Если ВидРедактора = ВидыРедактора.Monaco Тогда
		ПолеРедактораHTMLПриНажатииMonaco(Форма, Элемент, ДанныеСобытия, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

Процедура УстановитьТекстРедактораЭлементаФормы(Форма, Элемент, Текст) Экспорт
	ИдентификаторРедактора = UT_CodeEditorClientServer.ИдентификаторРедактораПоЭлементуФормы(Форма, Элемент);
	Если ИдентификаторРедактора = Неопределено Тогда
		Возврат;
	КонецЕсли;

	SetEditorText(Форма, ИдентификаторРедактора, Текст);
КонецПроцедуры

Процедура SetEditorText(Форма, ИдентификаторРедактора, Текст) Экспорт
	ВидыРедакторов = UT_CodeEditorClientServer.ВариантыРедактораКода();
	ВидРедактора = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаВидРедактора()];

	РедакторыФормы = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаСписокРедакторовФормы()];
	Если Не ВсеРедакторыФормыИнициализированы(РедакторыФормы) Тогда
		Возврат;
	КонецЕсли;

	ПараметрыРедактора = РедакторыФормы[ИдентификаторРедактора];
	Если ВидРедактора = ВидыРедакторов.Текст Тогда
		Форма[ПараметрыРедактора.ИмяРеквизита] = Текст;
	ИначеЕсли ВидРедактора = ВидыРедакторов.Ace Тогда
		ДокументHTML=Форма.Элементы[ПараметрыРедактора.ПолеРедактора].Документ.defaultView;
		ДокументHTML.editor.setValue(Текст, -1);
	ИначеЕсли ВидРедактора = ВидыРедакторов.Monaco Тогда
		ДокументHTML=Форма.Элементы[ПараметрыРедактора.ПолеРедактора].Документ.defaultView;
		ДокументHTML.updateText(Текст);
	КонецЕсли;
КонецПроцедуры

Функция EditorCodeText(Форма, ИдентификаторРедактора) Экспорт
	ВидыРедакторов = UT_CodeEditorClientServer.ВариантыРедактораКода();
	ВидРедактора = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаВидРедактора()];

	РедакторыФормы = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаСписокРедакторовФормы()];
	Если Не ВсеРедакторыФормыИнициализированы(РедакторыФормы) Тогда
		Возврат "";
	КонецЕсли;
	ПараметрыРедактора = РедакторыФормы[ИдентификаторРедактора];

	ТекстКода="";

	Если ВидРедактора = ВидыРедакторов.Текст Тогда
		ТекстКода = Форма[ПараметрыРедактора.ИмяРеквизита];
	ИначеЕсли ВидРедактора = ВидыРедакторов.Ace Тогда
		ДокументHTML=Форма.Элементы[ПараметрыРедактора.ПолеРедактора].Документ.defaultView;
		ТекстКода = ДокументHTML.editor.getValue();
	ИначеЕсли ВидРедактора = ВидыРедакторов.Monaco Тогда
		ДокументHTML=Форма.Элементы[ПараметрыРедактора.ПолеРедактора].Документ.defaultView;
		ТекстКода = ДокументHTML.getText();
	КонецЕсли;

	Возврат СокрЛП(ТекстКода);
КонецФункции

Функция ТекстКодаРедактораЭлементаФормы(Форма, Элемент) Экспорт
	ИдентификаторРедактора = UT_CodeEditorClientServer.ИдентификаторРедактораПоЭлементуФормы(Форма, Элемент);
	Если ИдентификаторРедактора = Неопределено Тогда
		Возврат "";
	КонецЕсли;

	Возврат EditorCodeText(Форма, ИдентификаторРедактора);
КонецФункции

Функция EditorSelectionBounds(Форма, ИдентификаторРедактора) Экспорт
	ВидыРедакторов = UT_CodeEditorClientServer.ВариантыРедактораКода();
	ВидРедактора = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаВидРедактора()];

	РедакторыФормы = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаСписокРедакторовФормы()];
	Если Не ВсеРедакторыФормыИнициализированы(РедакторыФормы) Тогда
		Возврат НовыйГраницыВыделения();
	КонецЕсли;

	ПараметрыРедактора = РедакторыФормы[ИдентификаторРедактора];

	ГраницыВыделения = НовыйГраницыВыделения();

	Если ВидРедактора = ВидыРедакторов.Текст Тогда
		ЭлементРедактора = Форма.Элементы[ПараметрыРедактора.ПолеРедактора];
			
		ЭлементРедактора.ПолучитьГраницыВыделения(ГраницыВыделения.НачалоСтроки, ГраницыВыделения.НачалоКолонки,
			ГраницыВыделения.КонецСтроки, ГраницыВыделения.КонецКолонки);		
	ИначеЕсли ВидРедактора = ВидыРедакторов.Ace Тогда
		ДокументHTML=Форма.Элементы[ПараметрыРедактора.ПолеРедактора].Документ.defaultView;
		ВыделеннаяОбласть = ДокументHTML.editor.getSelectionRange();
		
		ГраницыВыделения.НачалоСтроки = ВыделеннаяОбласть.start.row;
		ГраницыВыделения.НачалоКолонки = ВыделеннаяОбласть.start.column;
		ГраницыВыделения.КонецСтроки = ВыделеннаяОбласть.end.row;
		ГраницыВыделения.КонецКолонки = ВыделеннаяОбласть.end.column;
	ИначеЕсли ВидРедактора = ВидыРедакторов.Monaco Тогда
		ДокументHTML=Форма.Элементы[ПараметрыРедактора.ПолеРедактора].Документ.defaultView;
		
		Выделение = ДокументHTML.getSelection();
		ГраницыВыделения.НачалоСтроки = Выделение.startLineNumber;
		ГраницыВыделения.НачалоКолонки = Выделение.startColumn;
		ГраницыВыделения.КонецСтроки = Выделение.endLineNumber;
		ГраницыВыделения.КонецКолонки = Выделение.endColumn;
	КонецЕсли;

	Возврат ГраницыВыделения;
	
КонецФункции

Функция ГраницыВыделенияРедактораЭлементаФормы(Форма, Элемент) Экспорт
	ИдентификаторРедактора = UT_CodeEditorClientServer.ИдентификаторРедактораПоЭлементуФормы(Форма, Элемент);
	Если ИдентификаторРедактора = Неопределено Тогда
		Возврат НовыйГраницыВыделения();
	КонецЕсли;
	
	Возврат EditorSelectionBounds(Форма, ИдентификаторРедактора);	
КонецФункции

Процедура SetTextSelectionBounds(Форма, ИдентификаторРедактора, НачалоСтроки, НачалоКолонки, КонецСтроки,
	КонецКолонки) Экспорт

	ВидыРедакторов = UT_CodeEditorClientServer.ВариантыРедактораКода();
	ВидРедактора = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаВидРедактора()];

	РедакторыФормы = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаСписокРедакторовФормы()];
	Если Не ВсеРедакторыФормыИнициализированы(РедакторыФормы) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРедактора = РедакторыФормы[ИдентификаторРедактора];

	Если ВидРедактора = ВидыРедакторов.Текст Тогда
		ЭлементРедактора = Форма.Элементы[ПараметрыРедактора.ПолеРедактора];
			
		ЭлементРедактора.УстановитьГраницыВыделения(НачалоСтроки, НачалоКолонки, КонецСтроки, КонецКолонки);		
	ИначеЕсли ВидРедактора = ВидыРедакторов.Ace Тогда
		ДокументHTML=Форма.Элементы[ПараметрыРедактора.ПолеРедактора].Документ.defaultView;
		ДокументHTML.setSelection(НачалоСтроки, НачалоКолонки, КонецСтроки, КонецКолонки);
	ИначеЕсли ВидРедактора = ВидыРедакторов.Monaco Тогда
		ДокументHTML=Форма.Элементы[ПараметрыРедактора.ПолеРедактора].Документ.defaultView;
		ДокументHTML.setSelection(НачалоСтроки, НачалоКолонки, КонецСтроки, КонецКолонки);
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьГраницыВыделенияЭлементаФормы(Форма, Элемент, НачалоСтроки, НачалоКолонки, КонецСтроки,
	КонецКолонки) Экспорт

	ИдентификаторРедактора = UT_CodeEditorClientServer.ИдентификаторРедактораПоЭлементуФормы(Форма, Элемент);
	Если ИдентификаторРедактора = Неопределено Тогда
		Возврат;
	КонецЕсли;

	SetTextSelectionBounds(Форма, ИдентификаторРедактора, НачалоСтроки, НачалоКолонки, КонецСтроки, КонецКолонки);

КонецПроцедуры

Процедура InsertTextInCursorLocation(Форма, ИдентификаторРедактора, Текст) Экспорт
	ВидыРедакторов = UT_CodeEditorClientServer.ВариантыРедактораКода();
	ВидРедактора = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаВидРедактора()];

	РедакторыФормы = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаСписокРедакторовФормы()];
	Если Не ВсеРедакторыФормыИнициализированы(РедакторыФормы) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРедактора = РедакторыФормы[ИдентификаторРедактора];

	Если ВидРедактора = ВидыРедакторов.Текст Тогда
		ЭлементРедактора = Форма.Элементы[ПараметрыРедактора.ПолеРедактора];
		ЭлементРедактора.ВыделенныйТекст = Текст;	
	ИначеЕсли ВидРедактора = ВидыРедакторов.Ace Тогда
		ДокументHTML=Форма.Элементы[ПараметрыРедактора.ПолеРедактора].Документ.defaultView;
		ДокументHTML.editor.insert(Текст);
	ИначеЕсли ВидРедактора = ВидыРедакторов.Monaco Тогда
		ДокументHTML=Форма.Элементы[ПараметрыРедактора.ПолеРедактора].Документ.defaultView;
		ДокументHTML.selectedText(Текст);
	КонецЕсли;
КонецПроцедуры

Процедура ВставитьТекстПоПозицииКурсораЭлементаФормы(Форма, Элемент, Текст) Экспорт
	ИдентификаторРедактора = UT_CodeEditorClientServer.ИдентификаторРедактораПоЭлементуФормы(Форма, Элемент);
	Если ИдентификаторРедактора = Неопределено Тогда
		Возврат;
	КонецЕсли;

	InsertTextInCursorLocation(Форма, ИдентификаторРедактора, Текст);
	
КонецПроцедуры

Процедура AddCodeEditorContext(Форма, ИдентификаторРедактора, ДобавляемыйКонтекст) Экспорт
	ВидыРедакторов = UT_CodeEditorClientServer.ВариантыРедактораКода();
	ВидРедактора = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаВидРедактора()];

	РедакторыФормы = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаСписокРедакторовФормы()];
	Если Не ВсеРедакторыФормыИнициализированы(РедакторыФормы) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРедактора = РедакторыФормы[ИдентификаторРедактора];

	Если ВидРедактора = ВидыРедакторов.Monaco Тогда
		ДокументHTML=Форма.Элементы[ПараметрыРедактора.ПолеРедактора].Документ.defaultView;

		СоответствиеТипов = СоответствиеСсылочныхТиповКонфигурации();

		ОбъектыДобавления = Новый Структура;

		Для Каждого КлючЗначение Из ДобавляемыйКонтекст Цикл
			ОбъектДобавляемый = Новый Структура("ref");
			Если ТипЗнч(КлючЗначение.Значение) = Тип("Структура") Тогда
				ИмяТипа = КлючЗначение.Значение.Тип;
			
				ОбъектДобавляемый.Вставить("properties", Новый Структура);

				Для Каждого Свойство Из КлючЗначение.Значение.ПодчиненныеСвойства Цикл
					ДобавитьОписаниеРеквизитаДляРедактораMonaco(ОбъектДобавляемый.properties, Свойство, Истина,
						СоответствиеТипов);
				КонецЦикла;
				
			Иначе
				ИмяТипа = КлючЗначение.Значение;
			КонецЕсли;
			ОбъектДобавляемый.ref = ТипРедактораМонакоПоСтрокеТипа1С(ИмяТипа, СоответствиеТипов);
			ОбъектыДобавления.Вставить(КлючЗначение.Ключ, ОбъектДобавляемый);
		КонецЦикла;

		ДокументHTML.updateMetadata(UT_CommonClientServer.mWriteJSON(Новый Структура("customObjects",
			ОбъектыДобавления)));
	КонецЕсли;
КонецПроцедуры

Процедура ОткрытьКонструкторЗапроса(ТекстЗапроса, ОписаниеОповещенияОЗавершении, РежимКомпоновки = Ложь) Экспорт
#Если Не МобильныйКлиент Тогда
	Конструктор=Новый КонструкторЗапроса;
	Если UT_CommonClientServer.PlatformVersionNotLess_8_3_14() Тогда
		Конструктор.РежимКомпоновкиДанных=РежимКомпоновки;
	КонецЕсли;

	Если ЗначениеЗаполнено(СокрЛП(ТекстЗапроса)) Тогда
		Конструктор.Текст=ТекстЗапроса;
	КонецЕсли;

	Конструктор.Показать(ОписаниеОповещенияОЗавершении);
#КонецЕсли
КонецПроцедуры

Процедура ОткрытьКонструкторФорматнойСтроки(ФорматнаяСтрока, ОписаниеОповещенияОЗавершении) Экспорт
	Конструктор = Новый КонструкторФорматнойСтроки;
	Попытка
		Конструктор.Текст = ФорматнаяСтрока;
	Исключение
		Инфо = ИнформацияОбОшибке();
		ПоказатьПредупреждение( , "Ошибка в тексте форматной строки:" + Символы.ПС + Инфо.Причина.Описание);
		Возврат;
	КонецПопытки;
	Конструктор.Показать(ОписаниеОповещенияОЗавершении);
КонецПроцедуры

Процедура СохранитьМодулиКонфигурацииВФайлы(ОписаниеОповещенияОЗавершении, ТекущиеКаталоги) Экспорт
	ДопПараметрыОповещения = Новый Структура;
	ДопПараметрыОповещения.Вставить("ОписаниеОповещенияОЗавершении", ОписаниеОповещенияОЗавершении);
	ДопПараметрыОповещения.Вставить("ТекущиеКаталоги", ТекущиеКаталоги);

	UT_CommonClient.AttachFileSystemExtensionWithPossibleInstallation(
		Новый ОписаниеОповещения("СохранитьМодулиКонфигурацииВФайлыЗавершениеПодключенияРасширенияРаботыСФайлами",
		ЭтотОбъект, ДопПараметрыОповещения));

КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ФормаПриОткрытииЗавершениеПодключенияРасширенияРаботыСФайлами(Результат, ДополнительныеПараметры) Экспорт
	АдресБиблиотеки =  ДополнительныеПараметры.Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаАдресБиблиотеки()];
	Если АдресБиблиотеки = Неопределено Или Не ЗначениеЗаполнено(АдресБиблиотеки) Тогда
		ФормаПриОткрытииЗавершениеСохраненияБиблиотекиРедактора(Истина, ДополнительныеПараметры);
	Иначе
		ВидРедактора = ДополнительныеПараметры.Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаВидРедактора()];

		СохранитьБиблиотекуРедактораНаДиск(АдресБиблиотеки, ВидРедактора,
			Новый ОписаниеОповещения("ФормаПриОткрытииЗавершениеСохраненияБиблиотекиРедактора", ЭтотОбъект,
			ДополнительныеПараметры));
	КонецЕсли;
КонецПроцедуры

Процедура ФормаПриОткрытииЗавершениеСохраненияБиблиотекиРедактора(Результат, ДополнительныеПараметры) Экспорт
	Форма = ДополнительныеПараметры.Форма;
	ВидРедактора = Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаВидРедактора()];
	ВидыРедакторов = UT_CodeEditorClientServer.ВариантыРедактораКода();

	Если UT_CodeEditorClientServer.РедакторКодаИспользуетПолеHTML(ВидРедактора) Тогда
		Для Каждого КлючЗначение Из Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаСписокРедакторовФормы()] Цикл
			//ИмяРеквизитаРедактора = УИ_РедакторКодаКлиентСервер.ИмяРеквизитаРедактораКода(КлючЗначение.Значение.ИмяРеквизита);	

			Если ВидРедактора = ВидыРедакторов.Monaco Тогда
				Форма[КлючЗначение.Значение.ИмяРеквизита] = КаталогСохраненияРедактора(ВидРедактора)
					+ GetPathSeparator() + "index.html";
			ИначеЕсли ВидРедактора = ВидыРедакторов.Ace Тогда
				Форма[КлючЗначение.Значение.ИмяРеквизита] = ИмяФайлаРедактораAceДляЯзыка(КлючЗначение.Значение.Язык);
			КонецЕсли;
		КонецЦикла;
	Иначе
		CodeEditorDeferredInitializingEditors(Форма);
	КонецЕсли;
	
	// Оповестим о завершении обработки инициализации редакторов при открытии формы
	ОписаниеОповещенияОЗавершении= ДополнительныеПараметры.ОписаниеОповещенияОЗавершении;
	Если ОписаниеОповещенияОЗавершении = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗавершении, Истина);
КонецПроцедуры

Процедура СохранитьБиблиотекуРедактораНаДискЗавершениеСозданияКаталогаБиблиотеки(ИмяКаталога, ДополнительныеПараметры) Экспорт

	АдресБиблиотеки = ДополнительныеПараметры.АдресБиблиотеки;
	КаталогСохраненияБибилиотеки = ДополнительныеПараметры.КаталогСохраненияБибилиотеки;

	МассивСохраненныхФайлов = Новый Массив;
	СоответствиеФайловБиблиотеки=ПолучитьИзВременногоХранилища(АдресБиблиотеки);

	Если ДополнительныеПараметры.ВидРедактора = "Ace" Тогда
		ДобавитьКСохранениюТекстовыйДокументДляЯзыкаРедактораКодаAce(СоответствиеФайловБиблиотеки,
			КаталогСохраненияБибилиотеки, "bsl");
		ДобавитьКСохранениюТекстовыйДокументДляЯзыкаРедактораКодаAce(СоответствиеФайловБиблиотеки,
			КаталогСохраненияБибилиотеки, "css");
		ДобавитьКСохранениюТекстовыйДокументДляЯзыкаРедактораКодаAce(СоответствиеФайловБиблиотеки,
			КаталогСохраненияБибилиотеки, "javascript");
		ДобавитьКСохранениюТекстовыйДокументДляЯзыкаРедактораКодаAce(СоответствиеФайловБиблиотеки,
			КаталогСохраненияБибилиотеки, "html");
	КонецЕсли;

	ДополнительныеПараметры.Вставить("МассивСохраненныхФайлов", МассивСохраненныхФайлов);
	ДополнительныеПараметры.Вставить("СоответствиеФайловБиблиотеки", СоответствиеФайловБиблиотеки);

	СохранитьБиблиотекуРедактораЗаписатьНачатьЗаписьОчередногоФайла(ДополнительныеПараметры);
КонецПроцедуры

Процедура СохранитьБиблиотекуРедактораРаспаковатьБиблиотекуРедактораВКаталог(ДополнительныеПараметры,
	ОписаниеОповещенияОЗаверешнии) Экспорт
#Если Не ВебКлиент И Не МобильныйКлиент Тогда
	Поток=ДополнительныеПараметры.СоответствиеФайловБиблиотеки[ДополнительныеПараметры.ТекКлючФайла].ОткрытьПотокДляЧтения();

	ЧтениеZIP=Новый ЧтениеZipФайла(Поток);
	ЧтениеZIP.ИзвлечьВсе(ДополнительныеПараметры.КаталогСохраненияБибилиотеки,
		РежимВосстановленияПутейФайловZIP.Восстанавливать);

#КонецЕсли

КонецПроцедуры

Процедура СохранитьБиблиотекуРедактораРаспаковатьБиблиотекуРедактораВКаталогЗавершение(Результат,
	ДополнительныеПараметры) Экспорт

КонецПроцедуры

Процедура СохранитьБиблиотекуРедактораЗаписатьНачатьЗаписьОчередногоФайлаЗавершение(ДополнительныеПараметры) Экспорт
	МассивСохраненныхФайлов = ДополнительныеПараметры.МассивСохраненныхФайлов;
	МассивСохраненныхФайлов.Добавить(ДополнительныеПараметры.ТекКлючФайла);

	Файл = Новый Файл(ДополнительныеПараметры.ТекКлючФайла);

	Если Файл.Расширение = ".zip" Тогда
		СохранитьБиблиотекуРедактораРаспаковатьБиблиотекуРедактораВКаталог(ДополнительныеПараметры,
			Новый ОписаниеОповещения("СохранитьБиблиотекуРедактораРаспаковатьБиблиотекуРедактораВКаталогЗавершение",
			ЭтотОбъект, ДополнительныеПараметры));
	КонецЕсли;	
		//Иначе
	СохранитьБиблиотекуРедактораЗаписатьНачатьЗаписьОчередногоФайла(ДополнительныеПараметры);
	//КонецЕсли;
КонецПроцедуры

Процедура СохранитьБиблиотекуРедактораЗаписатьНачатьЗаписьОчередногоФайлаТекстовогоДокументаЗавершение(Результат,
	ДополнительныеПараметры) Экспорт
	МассивСохраненныхФайлов = ДополнительныеПараметры.МассивСохраненныхФайлов;
	МассивСохраненныхФайлов.Добавить(ДополнительныеПараметры.ТекКлючФайла);

	СохранитьБиблиотекуРедактораЗаписатьНачатьЗаписьОчередногоФайла(ДополнительныеПараметры);
КонецПроцедуры

Процедура СохранитьБиблиотекуРедактораНаДискЗавершениеПроверкиСуществованияБиблиотекиНаДиске(Существует,
	ДополнительныеПараметры) Экспорт
	Если Существует Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещенияОЗавершении);
		Возврат;
	КонецЕсли;

	КаталогСохраненияБибилиотеки = ДополнительныеПараметры.КаталогСохраненияБибилиотеки;

	НачатьСозданиеКаталога(
		Новый ОписаниеОповещения("СохранитьБиблиотекуРедактораНаДискЗавершениеСозданияКаталогаБиблиотеки", ЭтотОбъект,
		ДополнительныеПараметры), КаталогСохраненияБибилиотеки);

КонецПроцедуры

Процедура СохранитьМодулиКонфигурацииВФайлыЗавершениеПодключенияРасширенияРаботыСФайлами(Результат,
	ДополнительныеПараметры) Экспорт
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущиеКаталоги", ДополнительныеПараметры.ТекущиеКаталоги);

	ДополнительныеПараметрыОповещения = Новый Структура;
	ДополнительныеПараметрыОповещения.Вставить("ОписаниеОповещенияОЗавершении",
		ДополнительныеПараметры.ОписаниеОповещенияОЗавершении);

	ОткрытьФорму("ОбщаяФорма.УИ_НастройкиСохраненияИсходныхФайловКонфигурации", ПараметрыФормы, , , , ,
		Новый ОписаниеОповещения("СохранитьМодулиКонфигурацииВФайлыЗавершениеНастроек", ЭтотОбъект,
		ДополнительныеПараметрыОповещения), РежимОткрытияОкнаФормы.Независимый);

КонецПроцедуры

Процедура СохранитьМодулиКонфигурацииВФайлыЗавершениеНастроек(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ОписаниеМетаданныхКонфигурации = UT_CodeEditorServerCall.ОписаниеМетаданныхКонфигурации(Ложь);

	ПараметрыСохраненияИсходныхФайлов = Новый Структура;
	ПараметрыСохраненияИсходныхФайлов.Вставить("ОписаниеМетаданныхКонфигурации", ОписаниеМетаданныхКонфигурации);
	ПараметрыСохраненияИсходныхФайлов.Вставить("Параметры", Результат);
	ПараметрыСохраненияИсходныхФайлов.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	ПараметрыСохраненияИсходныхФайлов.Вставить("ИндексКаталога", 0);

	СохранитьМодулиКонфигурацииВФайлыНачалоОбработкиКаталогаИсточника(ПараметрыСохраненияИсходныхФайлов);

КонецПроцедуры

Процедура СохранитьМодулиКонфигурацииВФайлыНачалоОбработкиКаталогаИсточника(ПараметрыСохранения)
	Если ПараметрыСохранения.ИндексКаталога >= ПараметрыСохранения.Параметры.КаталогиИсточников.Количество() Тогда
		СохранитьМодулиКонфигурацииВФайлыЗавершение(ПараметрыСохранения);
		Возврат;
	КонецЕсли;

	ОписаниеКаталогаИсточника = ПараметрыСохранения.Параметры.КаталогиИсточников[ПараметрыСохранения.ИндексКаталога];

	ПараметрыСохранения.Вставить("ОписаниеКаталогаИсточника", ОписаниеКаталогаИсточника);
	
	//Сначала нужно очистить каталог
	НачатьУдалениеФайлов(Новый ОписаниеОповещения("СохранитьМодулиКонфигурацииВФайлыЗавершениеУдалениеФайловКаталога",
		ЭтотОбъект, ПараметрыСохранения), ОписаниеКаталогаИсточника.Каталог, "*");

КонецПроцедуры

Процедура СохранитьМодулиКонфигурацииВФайлыЗавершениеУдалениеФайловКаталога(ПараметрыСохранения) Экспорт
	Если ПараметрыСохранения.ОписаниеКаталогаИсточника.ТолькоМодули Тогда
		СохранитьМодулиКонфигурацииВФайлыСохранитьСписокМетаданныхСМодулями(ПараметрыСохранения);
	Иначе
		СохранитьМодулиКонфигурацииВФайлыЗАпуститьКонфигуратовДляВыгрузкиМетаданных(ПараметрыСохранения);
	КонецЕсли;
КонецПроцедуры

Процедура СохранитьМодулиКонфигурацииВФайлыСохранитьСписокМетаданныхСМодулями(ПараметрыСохранения) Экспорт
	ТекстМетаданных = Новый ТекстовыйДокумент;

	Если ПараметрыСохранения.ОписаниеКаталогаИсточника.Источник <> "ОсновнаяКонфигурация" Тогда
		ИмяРасширения = ПараметрыСохранения.ОписаниеКаталогаИсточника.Источник;
	Иначе
		ИмяРасширения = Неопределено;
	КонецЕсли;
	
	Для Каждого ТекКоллекция Из ПараметрыСохранения.ОписаниеМетаданныхКонфигурации Цикл
		Если ТипЗнч(ТекКоллекция.Значение)<> Тип("Структура") Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекКоллекция.Ключ = "Справочники" Тогда
			ИмяКоллекцииДляФайла = "Catalog";
		ИначеЕсли ТекКоллекция.Ключ = "Документы" Тогда
			ИмяКоллекцииДляФайла = "Document";
		ИначеЕсли ТекКоллекция.Ключ = "РегистрыСведений" Тогда
			ИмяКоллекцииДляФайла = "InformationRegister";
		ИначеЕсли ТекКоллекция.Ключ = "РегистрыНакопления" Тогда
			ИмяКоллекцииДляФайла = "AccumulationRegister";
		ИначеЕсли ТекКоллекция.Ключ = "РегистрыБухгалтерии" Тогда
			ИмяКоллекцииДляФайла = "AccountingRegister";
		ИначеЕсли ТекКоллекция.Ключ = "РегистрыРасчета" Тогда
			ИмяКоллекцииДляФайла = "CalculationRegister";
		ИначеЕсли ТекКоллекция.Ключ = "Обработки" Тогда
			ИмяКоллекцииДляФайла = "DataProcessor";
		ИначеЕсли ТекКоллекция.Ключ = "Отчеты" Тогда
			ИмяКоллекцииДляФайла = "Report";
		ИначеЕсли ТекКоллекция.Ключ = "Перечисления" Тогда
			ИмяКоллекцииДляФайла = "Enum";
		ИначеЕсли ТекКоллекция.Ключ = "ОбщиеМодули" Тогда
			ИмяКоллекцииДляФайла = "CommonModule";
		ИначеЕсли ТекКоллекция.Ключ = "ПланыСчетов" Тогда
			ИмяКоллекцииДляФайла = "ChartOfAccounts";
		ИначеЕсли ТекКоллекция.Ключ = "БизнесПроцессы" Тогда
			ИмяКоллекцииДляФайла = "BusinessProcess";
		ИначеЕсли ТекКоллекция.Ключ = "Задачи" Тогда
			ИмяКоллекцииДляФайла = "Task";
		ИначеЕсли ТекКоллекция.Ключ = "ПланыОбмена" Тогда
			ИмяКоллекцииДляФайла = "ExchangePlan";
		ИначеЕсли ТекКоллекция.Ключ = "ПланыВидовХарактеристик" Тогда
			ИмяКоллекцииДляФайла = "ChartOfCharacteristicTypes";
		ИначеЕсли ТекКоллекция.Ключ = "ПланыВидовРасчета" Тогда
			ИмяКоллекцииДляФайла = "ChartOfCalculationTypes";
		ИначеЕсли ТекКоллекция.Ключ = "Константы" Тогда
			ИмяКоллекцииДляФайла = "Constant";
		Иначе
			Продолжить;
		КонецЕсли;
		
		Для Каждого КлючЗначениеМетаданных Из ТекКоллекция.Значение Цикл
			Если КлючЗначениеМетаданных.Значение.Расширение<>ИмяРасширения Тогда
				Продолжить;
			КонецЕсли;
			ТекстМетаданных.ДобавитьСтроку(ИмяКоллекцииДляФайла+"."+КлючЗначениеМетаданных.Ключ);
		КонецЦикла;
	КонецЦикла;
	
	SessionFileVariablesStructure = UT_CommonClient.SessionFileVariablesStructure();
	ИмяФайлаСохранения = SessionFileVariablesStructure.TempFilesDirectory + GetPathSeparator() + "tools_ui_1c_list_metadata.txt";
	ПараметрыСохранения.Вставить("ИмяФайлаСпискаМетаданных", ИмяФайлаСохранения);
	ТекстМетаданных.НачатьЗапись(
		Новый ОписаниеОповещения("СохранитьМодулиКонфигурацииВФайлыСохранитьСписокМетаданныхСМодулямиЗавершение",
		ЭтотОбъект, ПараметрыСохранения), ИмяФайлаСохранения);

КонецПроцедуры

Процедура СохранитьМодулиКонфигурацииВФайлыСохранитьСписокМетаданныхСМодулямиЗавершение(Результат, ПараметрыСохранения) Экспорт
	Если Результат<>Истина Тогда
		Сообщить("Не удалось сохранить список метаданных с модулями в файл для источника "
			+ ПараметрыСохранения.ОписаниеКаталогаИсточника.Источник);

		ПараметрыСохранения.ИндексКаталога = ПараметрыСохранения.ИндексКаталога + 1;
		СохранитьМодулиКонфигурацииВФайлыНачалоОбработкиКаталогаИсточника(ПараметрыСохранения);
		Возврат;
	КонецЕсли;	
	
	СохранитьМодулиКонфигурацииВФайлыЗАпуститьКонфигуратовДляВыгрузкиМетаданных(ПараметрыСохранения);

КонецПроцедуры

Процедура СохранитьМодулиКонфигурацииВФайлыЗАпуститьКонфигуратовДляВыгрузкиМетаданных(ПараметрыСохранения) Экспорт
	СтрокаЗапускаПриложения = UT_StringFunctionsClientServer.WrapInOuotationMarks(
		ПараметрыСохранения.Параметры.ФайлЗапускаПлатформы) + " DESIGNER";

	Если ПараметрыСохранения.Параметры.РасположениеБазы = 0 Тогда
		СтрокаЗапускаПриложения = СтрокаЗапускаПриложения + " /F " + UT_StringFunctionsClientServer.WrapInOuotationMarks(
			ПараметрыСохранения.Параметры.КаталогИнформационнойБазы);
	Иначе
		ПутьКБазе = ПараметрыСохранения.Параметры.СерверИБ + "\" + ПараметрыСохранения.Параметры.ИмяБазы;
		СтрокаЗапускаПриложения = СтрокаЗапускаПриложения + " /S " + UT_StringFunctionsClientServer.WrapInOuotationMarks(
			ПутьКБазе);
	КонецЕсли;
	СтрокаЗапускаПриложения = СтрокаЗапускаПриложения + " /N" + UT_StringFunctionsClientServer.WrapInOuotationMarks(
		ПараметрыСохранения.Параметры.Пользователь);

	Если ЗначениеЗаполнено(ПараметрыСохранения.Параметры.Пароль) Тогда
		СтрокаЗапускаПриложения = СтрокаЗапускаПриложения + " /P" + UT_StringFunctionsClientServer.WrapInOuotationMarks(
			ПараметрыСохранения.Параметры.Пароль);
	КонецЕсли;
	СтрокаЗапускаПриложения = СтрокаЗапускаПриложения +" /DisableStartupMessages /DisableStartupDialogs";

	СтрокаЗапускаПриложения = СтрокаЗапускаПриложения + " /DumpConfigToFiles "
		+ UT_StringFunctionsClientServer.WrapInOuotationMarks(ПараметрыСохранения.ОписаниеКаталогаИсточника.Каталог)
		+ " -format Hierarchical";
		
	Если ПараметрыСохранения.ОписаниеКаталогаИсточника.Источник <> "ОсновнаяКонфигурация" Тогда
		СтрокаЗапускаПриложения = СтрокаЗапускаПриложения + " -Extension "
			+ ПараметрыСохранения.ОписаниеКаталогаИсточника.Источник;
	КонецЕсли;
	Если ПараметрыСохранения.ОписаниеКаталогаИсточника.ТолькоМодули Тогда
		СтрокаЗапускаПриложения = СтрокаЗапускаПриложения + " -listFile "
			+ UT_StringFunctionsClientServer.WrapInOuotationMarks(ПараметрыСохранения.ИмяФайлаСпискаМетаданных);

	КонецЕсли;
	SessionFileVariablesStructure = UT_CommonClient.SessionFileVariablesStructure();
	
	ПараметрыСохранения.Вставить("ИмяФайлаЛогаЗапускаКонфигуратора",
		SessionFileVariablesStructure.TempFilesDirectory + GetPathSeparator()
		+ "tools_ui_1c_list_metadata_out.txt");

	СтрокаЗапускаПриложения = СтрокаЗапускаПриложения + " /Out " + UT_StringFunctionsClientServer.WrapInOuotationMarks(
		ПараметрыСохранения.ИмяФайлаЛогаЗапускаКонфигуратора);

	НачатьЗапускПриложения(
		Новый ОписаниеОповещения("СохранитьМодулиКонфигурацииВФайлыЗавершениеВыгрузкиИсходниковМетаданныхВКаталог",
		ЭтотОбъект, ПараметрыСохранения), СтрокаЗапускаПриложения, , Истина);
КонецПроцедуры

Процедура СохранитьМодулиКонфигурацииВФайлыЗавершениеВыгрузкиИсходниковМетаданныхВКаталог(КодЗавершения,
	ПараметрыСохранения) Экспорт
	Если КодЗавершения <> 0 Тогда
		ТекстовыйДокумент = Новый ТекстовыйДокумент;

		ДополнительныеПараметрыОповещения = Новый Структура;
		ДополнительныеПараметрыОповещения.Вставить("ТекстовыйДокумент", ТекстовыйДокумент);
		ДополнительныеПараметрыОповещения.Вставить("ПараметрыСохранения", ПараметрыСохранения);

		ТекстовыйДокумент.НачатьЧтение(
			Новый ОписаниеОповещения("СохранитьМодулиКонфигурацииВФайлыЗавершениеВыгрузкиИсходниковМетаданныхВКаталогЗавершениеЧтенияЛога",
			ЭтотОбъект, ДополнительныеПараметрыОповещения), ПараметрыСохранения.ИмяФайлаЛогаЗапускаКонфигуратора);
		Возврат;
	КонецЕсли;
	ПараметрыСохранения.ИндексКаталога = ПараметрыСохранения.ИндексКаталога + 1;
	СохранитьМодулиКонфигурацииВФайлыНачалоОбработкиКаталогаИсточника(ПараметрыСохранения);
КонецПроцедуры

Процедура СохранитьМодулиКонфигурацииВФайлыЗавершениеВыгрузкиИсходниковМетаданныхВКаталогЗавершениеЧтенияЛога(ДополнительныеПараметры) Экспорт
	ПараметрыСохранения = ДополнительныеПараметры.ПараметрыСохранения;
	ТекстовыйДокумент = ДополнительныеПараметры.ТекстовыйДокумент;
	Сообщить("Не удалось сохранить исходные файлы для источника "
		+ ПараметрыСохранения.ОписаниеКаталогаИсточника.Источник + ":" + Символы.ПС + ТекстовыйДокумент.ПолучитьТекст());
		
	ПараметрыСохранения.ИндексКаталога = ПараметрыСохранения.ИндексКаталога + 1;
	СохранитьМодулиКонфигурацииВФайлыНачалоОбработкиКаталогаИсточника(ПараметрыСохранения);

КонецПроцедуры

Процедура СохранитьМодулиКонфигурацииВФайлыЗавершение(ПараметрыСохранения)
	ВыполнитьОбработкуОповещения(ПараметрыСохранения.ДополнительныеПараметры.ОписаниеОповещенияОЗавершении,
		ПараметрыСохранения.Параметры.КаталогиИсточников);
КонецПроцедуры

#Область Monaco

Процедура ПриЗавершенииРедактированияФорматнойСтрокиMonaco(Текст, ДополнительныеПараметры) Экспорт
	Если Текст = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ФорматнаяСтрока = СтрЗаменить(Текст, "'", "");
	ФорматнаяСтрока = """" + ФорматнаяСтрока + """";

	ДокументView = ДополнительныеПараметры.Форма.Элементы[ДополнительныеПараметры.Элемент.Имя].Документ.defaultView;

	Если ДополнительныеПараметры.Свойство("Позиция") Тогда
		УстановитьТекстMonaco(ДокументView, ФорматнаяСтрока, UT_CommonClientServer.mWriteJSON(
			ДополнительныеПараметры.Позиция), Истина);
	Иначе
		УстановитьТекстMonaco(ДокументView, ФорматнаяСтрока, , Истина);
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗавершенииРедактированияЗапросаMonaco(Текст, ДополнительныеПараметры) Экспорт
	Если Текст = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТекстЗапроса = СтрЗаменить(Текст, Символы.ПС, Символы.ПС + "|");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """", """""");
	ТекстЗапроса = """" + ТекстЗапроса + """";

	ДокументView = ДополнительныеПараметры.Форма.Элементы[ДополнительныеПараметры.Элемент.Имя].Документ.defaultView;

	Если ДополнительныеПараметры.Свойство("Позиция") Тогда
		УстановитьТекстMonaco(ДокументView, ТекстЗапроса, UT_CommonClientServer.mWriteJSON(
			ДополнительныеПараметры.Позиция), Истина);
	Иначе
		УстановитьТекстMonaco(ДокументView, ТекстЗапроса, , Истина);
	КонецЕсли;
КонецПроцедуры

Процедура ОткрытьКонструкторЗапросаMonacoЗавершениеВопроса(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	ОткрытьКонструкторЗапроса("", Новый ОписаниеОповещения("ПриЗавершенииРедактированияЗапросаMonaco", ЭтотОбъект,
		ДополнительныеПараметры));

КонецПроцедуры

Процедура ОткрытьКонструкторФорматнойСтрокиMonacoЗавершениеВопроса(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	ОткрытьКонструкторФорматнойСтроки("", Новый ОписаниеОповещения("ПриЗавершенииРедактированияФорматнойСтрокиMonaco",
		ЭтотОбъект, ДополнительныеПараметры));

КонецПроцедуры
#КонецОбласти
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПодготовитьТекстЗапросаДляКонструктора(Текст)

	ТекстЗапроса = СтрЗаменить(Текст, "|", "");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """""", "$");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """", "");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "$", """");

	Возврат ТекстЗапроса;
КонецФункции

Функция НовыйГраницыВыделения()
	Границы = Новый Структура;
	Границы.Вставить("НачалоСтроки", 1);
	Границы.Вставить("НачалоКолонки", 1);
	Границы.Вставить("КонецСтроки", 1);
	Границы.Вставить("КонецКолонки", 1);
	
	Возврат Границы;
КонецФункции

#Область Monaco

Функция ОписаниеМетаданныйДляИнициализацииРедактораMonaco()
	Описание = UT_ApplicationParameters["ОписаниеМетаданныйДляИнициализацииРедактораMonaco"];
	Если Описание <> Неопределено Тогда
		Возврат Описание;
	КонецЕсли;

	ОписаниеКонфигурацииДляИнициализации = UT_CodeEditorServerCall.ОписнаиеМетаданныйДляИнициализацииРедактораMonaco();
	UT_ApplicationParameters.Вставить("ОписаниеМетаданныйДляИнициализацииРедактораMonaco",
		ОписаниеКонфигурацииДляИнициализации);

	Возврат ОписаниеКонфигурацииДляИнициализации;

КонецФункции

Процедура УстановитьТекстMonaco(ДокументView, Текст, Позиция = Неопределено, УчитыватьОтступПервойСтроки = Истина)
	ДокументView.setText(Текст, Позиция);
КонецПроцедуры

Процедура ОткрытьКонструкторФорматнойСтрокиMonaco(ПараметрыСобытия, ДополнительныеПараметры)
	Если ПараметрыСобытия = Неопределено Тогда
		UT_CommonClient.ShowQuestionToUser(
			Новый ОписаниеОповещения("ОткрытьКонструкторФорматнойСтрокиMonacoЗавершениеВопроса", ЭтотОбъект,
			ДополнительныеПараметры), "Форматная строка не найдена." + Символы.ПС + "Создать новую форматную строку?",
			РежимДиалогаВопрос.ДаНет);
	Иначе
		ФорматнаяСтрока = СтрЗаменить(СтрЗаменить(ПараметрыСобытия.text, "|", ""), """", "");

		ПараметрыОповещения = ДополнительныеПараметры;

		Позиция = Новый Структура;
		Позиция.Вставить("startLineNumber", ПараметрыСобытия.range.startLineNumber);
		Позиция.Вставить("startColumn", ПараметрыСобытия.range.startColumn);
		Позиция.Вставить("endLineNumber", ПараметрыСобытия.range.endLineNumber);
		Позиция.Вставить("endColumn", ПараметрыСобытия.range.endColumn);

		ПараметрыОповещения.Вставить("Позиция", Позиция);

		ОткрытьКонструкторФорматнойСтроки(ФорматнаяСтрока,
			Новый ОписаниеОповещения("ПриЗавершенииРедактированияФорматнойСтрокиMonaco", ЭтотОбъект,
			ПараметрыОповещения));
	КонецЕсли;
КонецПроцедуры

Процедура ОткрытьКонструкторЗапросаMonaco(ПараметрыСобытия, ДополнительныеПараметры)
	Если ПараметрыСобытия = Неопределено Тогда
		UT_CommonClient.ShowQuestionToUser(
			Новый ОписаниеОповещения("ОткрытьКонструкторЗапросаMonacoЗавершениеВопроса", ЭтотОбъект,
			ДополнительныеПараметры), "Не найден текст запроса." + Символы.ПС + "Создать новый запрос?",
			РежимДиалогаВопрос.ДаНет);
	Иначе
		ТекстЗапроса = ПодготовитьТекстЗапросаДляКонструктора(ПараметрыСобытия.text);

		ПараметрыОповещения = ДополнительныеПараметры;

		Позиция = Новый Структура;
		Позиция.Вставить("startLineNumber", ПараметрыСобытия.range.startLineNumber);
		Позиция.Вставить("startColumn", ПараметрыСобытия.range.startColumn);
		Позиция.Вставить("endLineNumber", ПараметрыСобытия.range.endLineNumber);
		Позиция.Вставить("endColumn", ПараметрыСобытия.range.endColumn);

		ПараметрыОповещения.Вставить("Позиция", Позиция);

		ОткрытьКонструкторЗапроса(ТекстЗапроса, Новый ОписаниеОповещения("ПриЗавершенииРедактированияЗапросаMonaco",
			ЭтотОбъект, ПараметрыОповещения));
	КонецЕсли;
КонецПроцедуры

Процедура ПолеРедактораHTMLПриНажатииMonaco(Форма, Элемент, ДанныеСобытия, СтандартнаяОбработка)
	Событие = ДанныеСобытия.Event.eventData1C;

	Если Событие = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Событие.event = "EVENT_QUERY_CONSTRUCT" Тогда
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Форма", Форма);
		ДополнительныеПараметры.Вставить("Элемент", Элемент);

		ОткрытьКонструкторЗапросаMonaco(Событие.params, ДополнительныеПараметры);
	ИначеЕсли Событие.event = "EVENT_FORMAT_CONSTRUCT" Тогда
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Форма", Форма);
		ДополнительныеПараметры.Вставить("Элемент", Элемент);

		ОткрытьКонструкторФорматнойСтрокиMonaco(Событие.params, ДополнительныеПараметры);
	ИначеЕсли Событие.event = "EVENT_GET_METADATA" Тогда
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Форма", Форма);
		ДополнительныеПараметры.Вставить("Элемент", Элемент);

		МассивИменМетаданного = СтрРазделить(Событие.params, ".");

		Если МассивИменМетаданного[0] = "module" Тогда

			УстановитьОписаниеМодуляДляРедактораMonaco(Событие.params, ДополнительныеПараметры);

		Иначе

			УстановитьОписаниеМетаданныхДляРедактораMonaco(Событие.params, ДополнительныеПараметры);

		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ИмяКаталогаВидаМетаданных(ВидОбъектаМетаданных)
	Если ВидОбъектаМетаданных = "справочники" Тогда
		Возврат "Catalogs";
	ИначеЕсли ВидОбъектаМетаданных = "документы" Тогда
		Возврат "Documents";
	ИначеЕсли ВидОбъектаМетаданных = "константы" Тогда
		Возврат "Constants";
	ИначеЕсли ВидОбъектаМетаданных = "перечисления" Тогда
		Возврат "Enums";
	ИначеЕсли ВидОбъектаМетаданных = "отчеты" Тогда
		Возврат "Reports";
	ИначеЕсли ВидОбъектаМетаданных = "обработки" Тогда
		Возврат "DataProcessors";
	ИначеЕсли ВидОбъектаМетаданных = "планывидовхарактеристик" Тогда
		Возврат "ChartsOfCharacteristicTypes";
	ИначеЕсли ВидОбъектаМетаданных = "планысчетов" Тогда
		Возврат "ChartsOfAccounts";
	ИначеЕсли ВидОбъектаМетаданных = "планывидоврасчета" Тогда
		Возврат "ChartsOfCalculationTypes";
	ИначеЕсли ВидОбъектаМетаданных = "регистрысведений" Тогда
		Возврат "InformationRegisters";
	ИначеЕсли ВидОбъектаМетаданных = "регистрынакопления" Тогда
		Возврат "AccumulationRegisters";
	ИначеЕсли ВидОбъектаМетаданных = "регистрыбухгалтерии" Тогда
		Возврат "AccountingRegisters";
	ИначеЕсли ВидОбъектаМетаданных = "регистрырасчета" Тогда
		Возврат "CalculationRegisters";
	ИначеЕсли ВидОбъектаМетаданных = "бизнеспроцессы" Тогда
		Возврат "BusinessProcesses";
	ИначеЕсли ВидОбъектаМетаданных = "задачи" Тогда
		Возврат "Tasks";
	ИначеЕсли ВидОбъектаМетаданных = "планыобмена" Тогда
		Возврат "ExchangePlans";
	КонецЕсли;

КонецФункции

Процедура НачатьПоискФайлаМодуляВКаталогеИсходныхФайлов(ДополнительныеПараметры)
	Если ДополнительныеПараметры.КаталогиИсходников.Количество() <= ДополнительныеПараметры.ИндексКаталогаИсходников Тогда
		Возврат;
	КонецЕсли;
	КаталогИсходныхФайлов = ДополнительныеПараметры.КаталогиИсходников[ДополнительныеПараметры.ИндексКаталогаИсходников].Каталог;

	Если Не ЗначениеЗаполнено(КаталогИсходныхФайлов) Тогда
		ДополнительныеПараметры.ИндексКаталогаИсходников = ДополнительныеПараметры.ИндексКаталогаИсходников + 1;
		НачатьПоискФайлаМодуляВКаталогеИсходныхФайлов(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;

	ИмяКаталогаПоискаФайла = КаталогИсходныхФайлов + GetPathSeparator() + ДополнительныеПараметры.КаталогМодуля
		+ GetPathSeparator() + ДополнительныеПараметры.ОписаниеОбъектаМетаданных.Имя;
	ДополнительныеПараметры.Вставить("ИмяКаталогаПоискаФайла", ИмяКаталогаПоискаФайла);

	НачатьПоискФайлов(Новый ОписаниеОповещения("УстановитьОписаниеМодуляДляРедактораMonacoЗавершениеПоискаФайловМодуля",
		ЭтотОбъект, ДополнительныеПараметры), ИмяКаталогаПоискаФайла, ДополнительныеПараметры.ИмяФайлаМодуля, Истина);

КонецПроцедуры

Процедура УстановитьОписаниеМодуляДляРедактораMonaco(ОбновляемыйОбъектМетаданных, ДополнительныеПараметры)
	МассивИменМетаданного = СтрРазделить(ОбновляемыйОбъектМетаданных, ".");

	Если МассивИменМетаданного.Количество() < 2 Тогда
		Возврат;
	КонецЕсли;

	РедакторыФормы = ДополнительныеПараметры.Форма[UT_CodeEditorClientServer.ИмяРеквизитаРедактораКодаСписокРедакторовФормы()];
	ИдентификаторРедактора = UT_CodeEditorClientServer.ИдентификаторРедактораПоЭлементуФормы(
		ДополнительныеПараметры.Форма, ДополнительныеПараметры.Элемент);
	ПараметрыРедактора = РедакторыФормы[ИдентификаторРедактора];
	ДополнительныеПараметры.Вставить("КаталогиИсходников", ПараметрыРедактора.ПараметрыРедактора.КаталогиИсходныхФайлов);

	Если ДополнительныеПараметры.КаталогиИсходников.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ДополнительныеПараметры.Вставить("ИндексКаталогаИсходников", 0);

	ВидМодуля = МассивИменМетаданного[1];

	ДополнительныеПараметры.Вставить("ОбновляемыйОбъектМетаданных", ОбновляемыйОбъектМетаданных);
	ДополнительныеПараметры.Вставить("МассивИменМетаданного", МассивИменМетаданного);

	Если ВидМодуля = "manager" Тогда
		ОписаниеОбъектаМетаданных = UT_CodeEditorServerCall.ОписаниеОбъектаМетаданныхКонфигурацииПоИмени(
			МассивИменМетаданного[2], МассивИменМетаданного[3]);

		КаталогМодуля = ИмяКаталогаВидаМетаданных(МассивИменМетаданного[2]);
		ИмяФайла = "ManagerModule.bsl";

		ДополнительныеПараметры.Вставить("ЭтоОбщийМодуль", Ложь);

	ИначеЕсли ВидМодуля = "object" Тогда
		ОписаниеОбъектаМетаданных = UT_CodeEditorServerCall.ОписаниеОбъектаМетаданныхКонфигурацииПоИмени(
			МассивИменМетаданного[2], МассивИменМетаданного[3]);

		КаталогМодуля = ИмяКаталогаВидаМетаданных(МассивИменМетаданного[2]);
		ИмяФайла = "ObjectModule.bsl";

		ДополнительныеПараметры.Вставить("ЭтоОбщийМодуль", Ложь);
	Иначе
		ОписаниеОбъектаМетаданных = UT_CodeEditorServerCall.ОписаниеОбъектаМетаданныхКонфигурацииПоИмени(
			"ОбщиеМодули", МассивИменМетаданного[1]);

		КаталогМодуля = "CommonModules";
		ИмяФайла = "Module.bsl";

		ДополнительныеПараметры.Вставить("ЭтоОбщийМодуль", Истина);
	КонецЕсли;

	ДополнительныеПараметры.Вставить("ОписаниеОбъектаМетаданных", ОписаниеОбъектаМетаданных);
	ДополнительныеПараметры.Вставить("КаталогМодуля", КаталогМодуля);
	ДополнительныеПараметры.Вставить("ИмяФайлаМодуля", ИмяФайла);

	НачатьПоискФайлаМодуляВКаталогеИсходныхФайлов(ДополнительныеПараметры);
КонецПроцедуры
Процедура УстановитьОписаниеМодуляДляРедактораMonacoЗавершениеПоискаФайловМодуля(НайденныеФайлы,
	ДополнительныеПараметры) Экспорт
	Если НайденныеФайлы = Неопределено Тогда
		ДополнительныеПараметры.ИндексКаталогаИсходников = ДополнительныеПараметры.ИндексКаталогаИсходников + 1;
		НачатьПоискФайлаМодуляВКаталогеИсходныхФайлов(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;

	Если НайденныеФайлы.Количество() = 0 Тогда
		ДополнительныеПараметры.ИндексКаталогаИсходников = ДополнительныеПараметры.ИндексКаталогаИсходников + 1;
		НачатьПоискФайлаМодуляВКаталогеИсходныхФайлов(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;

	ИмяФайла = НайденныеФайлы[0].ПолноеИмя;
	ДополнительныеПараметры.Вставить("ИмяФайла", ИмяФайла);

	ТекстовыйДокумент = Новый ТекстовыйДокумент;

	ДополнительныеПараметры.Вставить("ТекстовыйДокумент", ТекстовыйДокумент);
	ТекстовыйДокумент.НачатьЧтение(
		Новый ОписаниеОповещения("УстановитьОписаниеМодуляДляРедактораMonacoЗавершениеЧтенияФайла", ЭтотОбъект,
		ДополнительныеПараметры), ДополнительныеПараметры.ИмяФайла);

КонецПроцедуры

Процедура УстановитьОписаниеМодуляДляРедактораMonacoЗавершениеЧтенияФайла(ДополнительныеПараметры) Экспорт
	ТекстМодуля = ДополнительныеПараметры.ТекстовыйДОкумент.ПолучитьТекст();

	ДокументView = ДополнительныеПараметры.Элемент.Документ.defaultView;

	Если ДополнительныеПараметры.ЭтоОбщийМодуль Тогда
		ДокументView.parseCommonModule(ДополнительныеПараметры.ОписаниеОбъектаМетаданных.Имя, ТекстМодуля, Ложь);
	Иначе
		СоответствиеОбновляемыхОбъектовМетаданных = СоответствиеОбновляемыхОбъектовМетаданныхРедактораMonacoИПараметровСобытияОблновленияМетаданных();
		ОбновляемаяКоллекцияРедактора = СоответствиеОбновляемыхОбъектовМетаданных[ДополнительныеПараметры.ОписаниеОбъектаМетаданных.ВидОбъекта];
		ОбновляемаяКоллекцияРедактора = ОбновляемаяКоллекцияРедактора + "."
			+ ДополнительныеПараметры.ОписаниеОбъектаМетаданных.Имя + "."
			+ ДополнительныеПараметры.МассивИменМетаданного[1];

		ДокументView.parseMetadataModule(ТекстМодуля, ОбновляемаяКоллекцияРедактора);
	КонецЕсли;
	ДокументView.triggerSuggestions();

КонецПроцедуры

Процедура УстановитьОписаниеМетаданныхДляРедактораMonaco(ОбновляемыйОбъектМетаданных, ДополнительныеПараметры)

	МассивИменМетаданного = СтрРазделить(ОбновляемыйОбъектМетаданных, ".");

	ВидОбъекта = МассивИменМетаданного[0];

	СоответствиеОбновляемыхОбъектовМетаданных = СоответствиеОбновляемыхОбъектовМетаданныхРедактораMonacoИПараметровСобытияОблновленияМетаданных();
	ОбновляемаяКоллекцияРедактора = СоответствиеОбновляемыхОбъектовМетаданных[ВидОбъекта];

	Если МассивИменМетаданного.Количество() = 1 Тогда
		ОбновляемыеДанные = Новый Структура;

		МассивИмен = UT_CodeEditorServerCall.СписокМетаданныхПоВиду(ВидОбъекта);
		Для Каждого ТекИмя Из МассивИмен Цикл
			ОбновляемыеДанные.Вставить(ТекИмя, Новый Структура);
		КонецЦикла;
	Иначе
		ОписаниеОбъектаМетаданных = UT_CodeEditorServerCall.ОписаниеОбъектаМетаданныхКонфигурацииПоИмени(
			ВидОбъекта, МассивИменМетаданного[1]);
		Описание = ОписаниеОбъектаМетаданныхДляРедактораMonaco(ОписаниеОбъектаМетаданных);

		ОбновляемыеДанные = Описание;

		ОбновляемаяКоллекцияРедактора = ОбновляемаяКоллекцияРедактора + "." + ОписаниеОбъектаМетаданных.Имя;
	КонецЕсли;

	ДокументView = ДополнительныеПараметры.Элемент.Документ.defaultView;
	ДокументView.updateMetadata(UT_CommonClientServer.mWriteJSON(
			ОбновляемыеДанные), ОбновляемаяКоллекцияРедактора);

	ДокументView.triggerSuggestions();
КонецПроцедуры

Функция ВидОбъектаРедактораMonacoПоВидуОбъекта1С(ВидОбъекта)

КонецФункции

Функция ТипРедактораМонакоПоСтрокеТипа1С(Тип1СИлиСтрока, СоответствиеСсылочныхТипов)
	Если СоответствиеСсылочныхТипов = Неопределено Тогда
		Возврат "";
	КонецЕсли;

	Тип1С = Тип1СИлиСтрока;
	Если ТипЗнч(Тип1С) = Тип("Строка") Тогда
		Если СтрНайти(Тип1СИлиСтрока, ".") > 0 Тогда
			Возврат Тип1СИлиСтрока;
		КонецЕсли;
		
		Попытка
			Тип1С = Тип(Тип1С);
		Исключение
			Возврат "types." + Тип1СИлиСтрока;
		КонецПопытки;
	КонецЕсли;

	МетаданныеТипа=СоответствиеСсылочныхТипов[Тип1С];

	Если МетаданныеТипа = Неопределено Тогда
		Если ТипЗнч(Тип1СИлиСтрока) = Тип("Строка") Тогда
			Попытка
				Стр = Новый(Тип1СИлиСтрока);
				Возврат "classes." + Тип1СИлиСтрока;
			Исключение
				Возврат "types." + Тип1СИлиСтрока;
			КонецПопытки;
		Иначе
			Возврат "";
		КонецЕсли;
	КонецЕсли;

	Если МетаданныеТипа.ВидОбъекта = "Справочник" Тогда
		Возврат "catalogs." + МетаданныеТипа.Имя;
	ИначеЕсли МетаданныеТипа.ВидОбъекта = "Документ" Тогда
		Возврат "documents." + МетаданныеТипа.Имя;
	ИначеЕсли МетаданныеТипа.ВидОбъекта = "Задача" Тогда
		Возврат "tasks." + МетаданныеТипа.Имя;
	ИначеЕсли МетаданныеТипа.ВидОбъекта = "ПланВидовРасчета" Тогда
		Возврат "chartsOfCalculationTypes." + МетаданныеТипа.Имя;
	ИначеЕсли МетаданныеТипа.ВидОбъекта = "ПланВидовХарактеристик" Тогда
		Возврат "chartsOfCharacteristicTypes." + МетаданныеТипа.Имя;
	ИначеЕсли МетаданныеТипа.ВидОбъекта = "ПланОбмена" Тогда
		Возврат "exchangePlans." + МетаданныеТипа.Имя;
	ИначеЕсли МетаданныеТипа.ВидОбъекта = "ПланСчетов" Тогда
		Возврат "сhartsOfAccounts." + МетаданныеТипа.Имя;
	КонецЕсли;

	Возврат "";
КонецФункции

Функция ПолучитьСвязьСОбъектомМетаданныхДляРедактораMonaco(Реквизит, СоответствиеТипов)

	Связь = "";

	Типы = Реквизит.Тип.Типы();

	Индекс = 0;

	Для Каждого ТекТип Из Типы Цикл
		Связь = ТипРедактораМонакоПоСтрокеТипа1С(ТекТип, СоответствиеТипов);

		Если ЗначениеЗаполнено(Связь) Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат Связь;

КонецФункции

Процедура ДобавитьОписаниеРеквизитаДляРедактораMonaco(ОписаниеРеквизитов, Реквизит, ПолучатьСвязиРеквизита,
	СоответствиеТипов)

	Связь = "";
	Если ПолучатьСвязиРеквизита Тогда
		Связь= ПолучитьСвязьСОбъектомМетаданныхДляРедактораMonaco(Реквизит, СоответствиеТипов);
	КонецЕсли;

	ОписаниеРеквизита = Новый Структура("name", Реквизит.Имя);

	Если ЗначениеЗаполнено(Связь) Тогда
		ОписаниеРеквизита.Вставить("ref", Связь);
	КонецЕсли;

	ОписаниеРеквизитов.Вставить(Реквизит.Имя, ОписаниеРеквизита);

КонецПроцедуры

Функция ОписаниеОбъектаМетаданныхДляРедактораMonaco(ОписаниеОбъектаМетаданных)
	СоответствиеТипов = СоответствиеСсылочныхТиповКонфигурации();
	ОписаниеРеквизитов = Новый Структура;
	ОписаниеРесурсов = Новый Структура;
	ОписаниеПредопределенных = Новый Структура;
	ОписаниеТабличныхЧастей = Новый Структура;
	ДополнительныеСвойства = Новый Структура;

	Если ОписаниеОбъектаМетаданных.ВидОбъекта = "Перечисление" Или ОписаниеОбъектаМетаданных.ВидОбъекта
		= "перечисления" Тогда

		Для Каждого КлючЗначениеЗначенияПеречисления Из ОписаниеОбъектаМетаданных.ЗначенияПеречисления Цикл
			ОписаниеРеквизитов.Вставить(КлючЗначениеЗначенияПеречисления.Ключ, Новый Структура("name",
				КлючЗначениеЗначенияПеречисления.Значение));
		КонецЦикла;

	Иначе

		Если ОписаниеОбъектаМетаданных.Свойство("Реквизиты") Тогда
			Для Каждого КлючЗначениеРеквизит Из ОписаниеОбъектаМетаданных.Реквизиты Цикл
				ДобавитьОписаниеРеквизитаДляРедактораMonaco(ОписаниеРеквизитов, КлючЗначениеРеквизит.Значение, Истина,
					СоответствиеТипов);
			КонецЦикла;
		КонецЕсли;
		Если ОписаниеОбъектаМетаданных.Свойство("СтандартныеРеквизиты") Тогда
			Для Каждого КлючЗначениеРеквизит Из ОписаниеОбъектаМетаданных.СтандартныеРеквизиты Цикл
				ДобавитьОписаниеРеквизитаДляРедактораMonaco(ОписаниеРеквизитов, КлючЗначениеРеквизит.Значение, Ложь,
					СоответствиеТипов);
			КонецЦикла;
		КонецЕсли;
		Если ОписаниеОбъектаМетаданных.Свойство("Предопределенные") Тогда
				
				//Если ИмяМетаданных(ПолноеИмя) = "ПланСчетов" Тогда
				//	
				//	Запрос = Новый Запрос(
				//	"ВЫБРАТЬ
				//	|	ПланСчетов.Код КАК Код,
				//	|	ПланСчетов.ИмяПредопределенныхДанных КАК Имя
				//	|ИЗ
				//	|	&Таблица КАК ПланСчетов
				//	|ГДЕ
				//	|	ПланСчетов.Предопределенный");				
				//						
				//	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Таблица", ПолноеИмя);
				//	
				//	Выборка = Запрос.Выполнить().Выбрать();
				//	
				//	Пока Выборка.Следующий() Цикл 
				//		ОписаниеПредопределенных.Вставить(Выборка.Имя, СтрШаблон("%1 (%2)", Выборка.Имя, Выборка.Код));
				//	КонецЦикла;
				//	
				//Иначе				
			Для Каждого КлючЗначениеИмя Из ОписаниеОбъектаМетаданных.Предопределенные Цикл
				ОписаниеПредопределенных.Вставить(КлючЗначениеИмя.Ключ, "");
			КонецЦикла;
				
				//КонецЕсли;

		КонецЕсли;

		Если ОписаниеОбъектаМетаданных.Свойство("Измерения") Тогда

			Для Каждого КлючЗначениеРеквизит Из ОписаниеОбъектаМетаданных.Измерения Цикл
				ДобавитьОписаниеРеквизитаДляРедактораMonaco(ОписаниеРеквизитов, КлючЗначениеРеквизит.Значение, Истина,
					СоответствиеТипов);
			КонецЦикла;
			Для Каждого КлючЗначениеРеквизит Из ОписаниеОбъектаМетаданных.Ресурсы Цикл
				ДобавитьОписаниеРеквизитаДляРедактораMonaco(ОписаниеРеквизитов, КлючЗначениеРеквизит.Значение, Истина,
					СоответствиеТипов);
			КонецЦикла;
				
				//ЗаполнитьТипРегистра(ДополнительныеСвойства, ОбъектМетаданных, ПолноеИмя);				

		КонецЕсли;

		Если ОписаниеОбъектаМетаданных.Свойство("ТабличныеЧасти") Тогда

			Для Каждого КлючЗначениеТабличнаяЧасть Из ОписаниеОбъектаМетаданных.ТабличныеЧасти Цикл

				ТабличнаяЧасть = КлючЗначениеТабличнаяЧасть.Значение;
				ОписаниеРеквизитов.Вставить(ТабличнаяЧасть.Имя, Новый Структура("name", "ТЧ: "
					+ ТабличнаяЧасть.Синоним));

				ОписаниеТабличнойЧасти = Новый Структура;

				Если ТабличнаяЧасть.Свойство("СтандартныеРеквизиты") Тогда
					Для Каждого РеквизитТЧ Из ТабличнаяЧасть.СтандартныеРеквизиты Цикл
						ОписаниеТабличнойЧасти.Вставить(РеквизитТЧ.Значение.Имя, РеквизитТЧ.Значение.Синоним);
					КонецЦикла;
				КонецЕсли;

				Если ТабличнаяЧасть.Свойство("Реквизиты") Тогда
					Для Каждого РеквизитТЧ Из ТабличнаяЧасть.Реквизиты Цикл
						ДобавитьОписаниеРеквизитаДляРедактораMonaco(ОписаниеТабличнойЧасти, РеквизитТЧ.Значение,
							Истина, СоответствиеТипов);
					КонецЦикла;
				КонецЕсли;

				ОписаниеТабличныхЧастей.Вставить(ТабличнаяЧасть.Имя, ОписаниеТабличнойЧасти);

			КонецЦикла;

		КонецЕсли;
		Если ОписаниеОбъектаМетаданных.Свойство("СтандартныеТабличныеЧасти") Тогда

			Для Каждого КлючЗначениеТабличнаяЧасть Из ОписаниеОбъектаМетаданных.СтандартныеТабличныеЧасти Цикл

				ТабличнаяЧасть = КлючЗначениеТабличнаяЧасть.Значение;
				ОписаниеРеквизитов.Вставить(ТабличнаяЧасть.Имя, Новый Структура("name", "ТЧ: "
					+ ТабличнаяЧасть.Синоним));

				ОписаниеТабличнойЧасти = Новый Структура;

				Если ТабличнаяЧасть.Свойство("СтандартныеРеквизиты") Тогда
					Для Каждого РеквизитТЧ Из ТабличнаяЧасть.СтандартныеРеквизиты Цикл
						ОписаниеТабличнойЧасти.Вставить(РеквизитТЧ.Значение.Имя, РеквизитТЧ.Значение.Синоним);
					КонецЦикла;
				КонецЕсли;

				Если ТабличнаяЧасть.Свойство("Реквизиты") Тогда
					Для Каждого РеквизитТЧ Из ТабличнаяЧасть.Реквизиты Цикл
						ДобавитьОписаниеРеквизитаДляРедактораMonaco(ОписаниеТабличнойЧасти, РеквизитТЧ.Значение,
							Истина, СоответствиеТипов);
					КонецЦикла;
				КонецЕсли;

				ОписаниеТабличныхЧастей.Вставить(ТабличнаяЧасть.Имя, ОписаниеТабличнойЧасти);

			КонецЦикла;

		КонецЕсли;

	КонецЕсли;

	СтруктураОбъекта = Новый Структура;
	СтруктураОбъекта.Вставить("properties", ОписаниеРеквизитов);

	Для Каждого Обход Из ДополнительныеСвойства Цикл
		СтруктураОбъекта.Вставить(Обход.Ключ, Обход.Значение);
	КонецЦикла;

	Если ОписаниеРесурсов.Количество() > 0 Тогда
		СтруктураОбъекта.Вставить("resources", ОписаниеРесурсов);
	КонецЕсли;

	Если ОписаниеПредопределенных.Количество() > 0 Тогда
		СтруктураОбъекта.Вставить("predefined", ОписаниеПредопределенных);
	КонецЕсли;

	Если ОписаниеТабличныхЧастей.Количество() > 0 Тогда
		СтруктураОбъекта.Вставить("tabulars", ОписаниеТабличныхЧастей);
	КонецЕсли;

	Возврат СтруктураОбъекта;
КонецФункции

Функция ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(Коллекция, СоответствиеТипов)

	ОписаниеКоллекции = Новый Структура;

	Для Каждого КлючЗначениеЭлементКоллекции Из Коллекция Цикл

		ОписаниеРеквизитов = Новый Структура;
		ОписаниеРесурсов = Новый Структура;
		ОписаниеПредопределенных = Новый Структура;
		ОписаниеТабличныхЧастей = Новый Структура;
		ДополнительныеСвойства = Новый Структура;

		ОбъектМетаданных = КлючЗначениеЭлементКоллекции.Значение;

		Если ОбъектМетаданных.ВидОбъекта = "Перечисление" Тогда

			Для Каждого КлючЗначениеЗначенияПеречисления Из ОбъектМетаданных.ЗначенияПеречисления Цикл
				ОписаниеРеквизитов.Вставить(КлючЗначениеЗначенияПеречисления.Ключ, Новый Структура("name",
					КлючЗначениеЗначенияПеречисления.Значение));
			КонецЦикла;

		Иначе

			Если ОбъектМетаданных.Свойство("Реквизиты") Тогда
				Для Каждого КлючЗначениеРеквизит Из ОбъектМетаданных.Реквизиты Цикл
					ДобавитьОписаниеРеквизитаДляРедактораMonaco(ОписаниеРеквизитов, КлючЗначениеРеквизит.Значение,
						Истина, СоответствиеТипов);
				КонецЦикла;
			КонецЕсли;
			Если ОбъектМетаданных.Свойство("СтандартныеРеквизиты") Тогда
				Для Каждого КлючЗначениеРеквизит Из ОбъектМетаданных.СтандартныеРеквизиты Цикл
					ДобавитьОписаниеРеквизитаДляРедактораMonaco(ОписаниеРеквизитов, КлючЗначениеРеквизит.Значение, Ложь,
						СоответствиеТипов);
				КонецЦикла;
			КонецЕсли;
			Если ОбъектМетаданных.Свойство("Предопределенные") Тогда
				
				//Если ИмяМетаданных(ПолноеИмя) = "ПланСчетов" Тогда
				//	
				//	Запрос = Новый Запрос(
				//	"ВЫБРАТЬ
				//	|	ПланСчетов.Код КАК Код,
				//	|	ПланСчетов.ИмяПредопределенныхДанных КАК Имя
				//	|ИЗ
				//	|	&Таблица КАК ПланСчетов
				//	|ГДЕ
				//	|	ПланСчетов.Предопределенный");				
				//						
				//	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Таблица", ПолноеИмя);
				//	
				//	Выборка = Запрос.Выполнить().Выбрать();
				//	
				//	Пока Выборка.Следующий() Цикл 
				//		ОписаниеПредопределенных.Вставить(Выборка.Имя, СтрШаблон("%1 (%2)", Выборка.Имя, Выборка.Код));
				//	КонецЦикла;
				//	
				//Иначе				
				Для Каждого КлючЗначениеИмя Из ОбъектМетаданных.Предопределенные Цикл
					ОписаниеПредопределенных.Вставить(КлючЗначениеИмя.Ключ, Новый Структура("name, ref",
						КлючЗначениеИмя.Ключ, ""));
				КонецЦикла;
				
				//КонецЕсли;

			КонецЕсли;

			Если ОбъектМетаданных.Свойство("Измерения") Тогда

				Для Каждого КлючЗначениеРеквизит Из ОбъектМетаданных.Измерения Цикл
					ДобавитьОписаниеРеквизитаДляРедактораMonaco(ОписаниеРеквизитов, КлючЗначениеРеквизит.Значение,
						Истина, СоответствиеТипов);
				КонецЦикла;
				Для Каждого КлючЗначениеРеквизит Из ОбъектМетаданных.Ресурсы Цикл
					ДобавитьОписаниеРеквизитаДляРедактораMonaco(ОписаниеРеквизитов, КлючЗначениеРеквизит.Значение,
						Истина, СоответствиеТипов);
				КонецЦикла;
				
				//ЗаполнитьТипРегистра(ДополнительныеСвойства, ОбъектМетаданных, ПолноеИмя);				

			КонецЕсли;

			Если ОбъектМетаданных.Свойство("ТабличныеЧасти") Тогда

				Для Каждого КлючЗначениеТабличнаяЧасть Из ОбъектМетаданных.ТабличныеЧасти Цикл

					ТабличнаяЧасть = КлючЗначениеТабличнаяЧасть.Значение;
					ОписаниеРеквизитов.Вставить(ТабличнаяЧасть.Имя, Новый Структура("name", "ТЧ: "
						+ ТабличнаяЧасть.Синоним));

					ОписаниеТабличнойЧасти = Новый Структура;

					Если ТабличнаяЧасть.Свойство("СтандартныеРеквизиты") Тогда
						Для Каждого РеквизитТЧ Из ТабличнаяЧасть.СтандартныеРеквизиты Цикл
							ОписаниеТабличнойЧасти.Вставить(РеквизитТЧ.Значение.Имя, РеквизитТЧ.Значение.Синоним);
						КонецЦикла;
					КонецЕсли;

					Если ТабличнаяЧасть.Свойство("Реквизиты") Тогда
						Для Каждого РеквизитТЧ Из ТабличнаяЧасть.Реквизиты Цикл
							ДобавитьОписаниеРеквизитаДляРедактораMonaco(ОписаниеТабличнойЧасти, РеквизитТЧ.Значение,
								Истина, СоответствиеТипов);
						КонецЦикла;
					КонецЕсли;

					ОписаниеТабличныхЧастей.Вставить(ТабличнаяЧасть.Имя, ОписаниеТабличнойЧасти);

				КонецЦикла;

			КонецЕсли;
			Если ОбъектМетаданных.Свойство("СтандартныеТабличныеЧасти") Тогда

				Для Каждого КлючЗначениеТабличнаяЧасть Из ОбъектМетаданных.СтандартныеТабличныеЧасти Цикл

					ТабличнаяЧасть = КлючЗначениеТабличнаяЧасть.Значение;
					ОписаниеРеквизитов.Вставить(ТабличнаяЧасть.Имя, Новый Структура("name", "ТЧ: "
						+ ТабличнаяЧасть.Синоним));

					ОписаниеТабличнойЧасти = Новый Структура;

					Если ТабличнаяЧасть.Свойство("СтандартныеРеквизиты") Тогда
						Для Каждого РеквизитТЧ Из ТабличнаяЧасть.СтандартныеРеквизиты Цикл
							ОписаниеТабличнойЧасти.Вставить(РеквизитТЧ.Значение.Имя, РеквизитТЧ.Значение.Синоним);
						КонецЦикла;
					КонецЕсли;

					Если ТабличнаяЧасть.Свойство("Реквизиты") Тогда
						Для Каждого РеквизитТЧ Из ТабличнаяЧасть.Реквизиты Цикл
							ДобавитьОписаниеРеквизитаДляРедактораMonaco(ОписаниеТабличнойЧасти, РеквизитТЧ.Значение,
								Истина, СоответствиеТипов);
						КонецЦикла;
					КонецЕсли;

					ОписаниеТабличныхЧастей.Вставить(ТабличнаяЧасть.Имя, ОписаниеТабличнойЧасти);

				КонецЦикла;

			КонецЕсли;

		КонецЕсли;

		СтруктураОбъекта = Новый Структура;
		СтруктураОбъекта.Вставить("properties", ОписаниеРеквизитов);

		Для Каждого Обход Из ДополнительныеСвойства Цикл
			СтруктураОбъекта.Вставить(Обход.Ключ, Обход.Значение);
		КонецЦикла;

		Если 0 < ОписаниеРесурсов.Количество() Тогда
			СтруктураОбъекта.Вставить("resources", ОписаниеРесурсов);
		КонецЕсли;

		Если 0 < ОписаниеПредопределенных.Количество() Тогда
			СтруктураОбъекта.Вставить("predefined", ОписаниеПредопределенных);
		КонецЕсли;

		Если 0 < ОписаниеТабличныхЧастей.Количество() Тогда
			СтруктураОбъекта.Вставить("tabulars", ОписаниеТабличныхЧастей);
		КонецЕсли;

		ОписаниеКоллекции.Вставить(ОбъектМетаданных.Имя, СтруктураОбъекта);

	КонецЦикла;

	Возврат ОписаниеКоллекции;

КонецФункции

Функция ПолучитьСписокОбъектовМетаданныхИзКоллекцииДляРедактораMonaco(Коллекция)

	ОписаниеКоллекции = Новый Структура;

	Для Каждого КлючЗначение Из Коллекция Цикл
		ОписаниеКоллекции.Вставить(КлючЗначение.Ключ, Новый Структура);
	КонецЦикла;

	Возврат ОписаниеКоллекции;

КонецФункции

Функция СоответствиеСсылочныхТиповКонфигурации()
	Соответствие = UT_ApplicationParameters["СоответствиеСсылочныхТиповКонфигурации"];
	Если Соответствие <> Неопределено Тогда
		Возврат Соответствие;
	КонецЕсли;

	СоответствиеТипов = UT_CodeEditorServerCall.СоответствиеСсылочныхТипов();
	UT_ApplicationParameters.Вставить("СоответствиеСсылочныхТиповКонфигурации", СоответствиеТипов);

	Возврат СоответствиеТипов;
КонецФункции

Функция ОписаниеМетаданныхКонфигурацииДляРедактораMonaco()
	ОписаниеМетаданных = UT_ApplicationParameters["ОписаниеМетаданныхДляРедактораMonaco"];
	Если ОписаниеМетаданных <> Неопределено Тогда
		Возврат ОписаниеМетаданных;
	КонецЕсли;

	АдресОписанияМетаданных = UT_ApplicationParameters["АдресОписанияМетаданныхКонфигурации"];
	Если Не ЭтоАдресВременногоХранилища(АдресОписанияМетаданных) Тогда
		АдресОписанияМетаданных = UT_CommonServerCall.ConfigurationMetadataDescriptionAdress();
		UT_ApplicationParameters.Вставить("АдресОписанияМетаданныхКонфигурации", АдресОписанияМетаданных);
	КонецЕсли;
	МетаданныеКонфигурации = ПолучитьИзВременногоХранилища(АдресОписанияМетаданных);

	СоответствиеТипов = МетаданныеКонфигурации.СоответствиеСсылочныхТипов;

	КоллекцияМетаданных = Новый Структура;
	КоллекцияМетаданных.Вставить("catalogs", ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(
		МетаданныеКонфигурации.Справочники, СоответствиеТипов));
	КоллекцияМетаданных.Вставить("documents", ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(
		МетаданныеКонфигурации.Документы, СоответствиеТипов));
	КоллекцияМетаданных.Вставить("infoRegs", ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(
		МетаданныеКонфигурации.РегистрыСведений, СоответствиеТипов));
	КоллекцияМетаданных.Вставить("accumRegs", ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(
		МетаданныеКонфигурации.РегистрыНакопления, СоответствиеТипов));
	КоллекцияМетаданных.Вставить("accountRegs", ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(
		МетаданныеКонфигурации.РегистрыБухгалтерии, СоответствиеТипов));
	КоллекцияМетаданных.Вставить("calcRegs", ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(
		МетаданныеКонфигурации.РегистрыРасчета, СоответствиеТипов));
	КоллекцияМетаданных.Вставить("dataProc", ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(
		МетаданныеКонфигурации.Обработки, СоответствиеТипов));
	КоллекцияМетаданных.Вставить("reports", ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(
		МетаданныеКонфигурации.Отчеты, СоответствиеТипов));
	КоллекцияМетаданных.Вставить("enums", ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(
		МетаданныеКонфигурации.Перечисления, СоответствиеТипов));
	КоллекцияМетаданных.Вставить("commonModules", ПолучитьСписокОбъектовМетаданныхИзКоллекцииДляРедактораMonaco(
		МетаданныеКонфигурации.ОбщиеМодули));
	КоллекцияМетаданных.Вставить("сhartsOfAccounts", ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(
		МетаданныеКонфигурации.ПланыСчетов, СоответствиеТипов));
	КоллекцияМетаданных.Вставить("businessProcesses", ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(
		МетаданныеКонфигурации.БизнесПроцессы, СоответствиеТипов));
	КоллекцияМетаданных.Вставить("tasks", ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(
		МетаданныеКонфигурации.Задачи, СоответствиеТипов));
	КоллекцияМетаданных.Вставить("exchangePlans", ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(
		МетаданныеКонфигурации.ПланыОбмена, СоответствиеТипов));
	КоллекцияМетаданных.Вставить("chartsOfCharacteristicTypes", ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(
		МетаданныеКонфигурации.ПланыВидовХарактеристик, СоответствиеТипов));
	КоллекцияМетаданных.Вставить("chartsOfCalculationTypes", ОписатьКоллекциюОбъектовМетаданыхДляРедактораMonaco(
		МетаданныеКонфигурации.ПланыВидовРасчета, СоответствиеТипов));
	КоллекцияМетаданных.Вставить("constants", ПолучитьСписокОбъектовМетаданныхИзКоллекцииДляРедактораMonaco(
		МетаданныеКонфигурации.Константы));

	UT_ApplicationParameters.Вставить("ОписаниеМетаданныхДляРедактораMonaco",
		UT_CommonClientServer.CopyStructure(КоллекцияМетаданных));
	UT_ApplicationParameters.Вставить("СоответствиеСсылочныхТиповКонфигурации", СоответствиеТипов);

	Возврат КоллекцияМетаданных;
КонецФункции

Функция СоответствиеОбновляемыхОбъектовМетаданныхРедактораMonacoИПараметровСобытияОблновленияМетаданных()
	Соответствие = Новый Структура;
	Соответствие.Вставить("справочники", "catalogs.items");
	Соответствие.Вставить("catalogs", "catalogs.items");
	Соответствие.Вставить("документы", "documents.items");
	Соответствие.Вставить("documents", "documents.items");
	Соответствие.Вставить("регистрысведений", "infoRegs.items");
	Соответствие.Вставить("informationregisters", "infoRegs.items");
	Соответствие.Вставить("регистрынакопления", "accumRegs.items");
	Соответствие.Вставить("accumulationregisters", "accumRegs.items");
	Соответствие.Вставить("регистрыбухгалтерии", "accountRegs.items");
	Соответствие.Вставить("accountingregisters", "accountRegs.items");
	Соответствие.Вставить("регистрырасчета", "calcRegs.items");
	Соответствие.Вставить("calculationregisters", "calcRegs.items");
	Соответствие.Вставить("обработки", "dataProc.items");
	Соответствие.Вставить("dataprocessors", "dataProc.items");
	Соответствие.Вставить("отчеты", "reports.items");
	Соответствие.Вставить("reports", "reports.items");
	Соответствие.Вставить("перечисления", "enums.items");
	Соответствие.Вставить("enums", "enums.items");
	Соответствие.Вставить("планысчетов", "сhartsOfAccounts.items");
	Соответствие.Вставить("chartsofaccounts", "сhartsOfAccounts.items");
	Соответствие.Вставить("бизнеспроцессы", "businessProcesses.items");
	Соответствие.Вставить("businessprocesses", "businessProcesses.items");
	Соответствие.Вставить("задачи", "tasks.items");
	Соответствие.Вставить("tasks", "tasks.items");
	Соответствие.Вставить("планыобмена", "exchangePlans.items");
	Соответствие.Вставить("exchangeplans", "exchangePlans.items");
	Соответствие.Вставить("планывидовхарактеристик", "chartsOfCharacteristicTypes.items");
	Соответствие.Вставить("chartsofcharacteristictypes", "chartsOfCharacteristicTypes.items");
	Соответствие.Вставить("планывидоврасчета", "chartsOfCalculationTypes.items");
	Соответствие.Вставить("chartsofcalculationtypes", "chartsOfCalculationTypes.items");
	Соответствие.Вставить("константы", "constants.items");
	Соответствие.Вставить("constants", "chartsOfCalculationTypes.items");
	Соответствие.Вставить("module", "commonModules.items");

	Возврат Соответствие;
КонецФункции

#КонецОбласти
Процедура СохранитьБиблиотекуРедактораНаДиск(АдресБиблиотеки, ВидРедактора, ОписаниеОповещенияОЗавершении)
	КаталогСохраненияБибилиотеки=КаталогСохраненияРедактора(ВидРедактора);
	ФайлРедактора=Новый Файл(КаталогСохраненияБибилиотеки);

	ДопПараметры= Новый Структура;
	ДопПараметры.Вставить("АдресБиблиотеки", АдресБиблиотеки);
	ДопПараметры.Вставить("КаталогСохраненияБибилиотеки", КаталогСохраненияБибилиотеки);
	ДопПараметры.Вставить("ВидРедактора", ВидРедактора);
	ДопПараметры.Вставить("ОписаниеОповещенияОЗавершении", ОписаниеОповещенияОЗавершении);
	ФайлРедактора.НачатьПроверкуСуществования(
		Новый ОписаниеОповещения("СохранитьБиблиотекуРедактораНаДискЗавершениеПроверкиСуществованияБиблиотекиНаДиске",
		ЭтотОбъект, ДопПараметры));
КонецПроцедуры

Процедура СохранитьБиблиотекуРедактораЗаписатьНачатьЗаписьОчередногоФайла(ДополнительныеПараметры)
	МассивСохраненныхФайлов = ДополнительныеПараметры.МассивСохраненныхФайлов;
	КаталогСохраненияБибилиотеки = ДополнительныеПараметры.КаталогСохраненияБибилиотеки;
	СоответствиеФайловБиблиотеки = ДополнительныеПараметры.СоответствиеФайловБиблиотеки;
	ЕстьНеСохраненное = Ложь;
	Для Каждого КлючЗначение Из СоответствиеФайловБиблиотеки Цикл
		Если МассивСохраненныхФайлов.Найти(КлючЗначение.Ключ) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ЕстьНеСохраненное = Истина;

		ИмяФайла=КаталогСохраненияБибилиотеки + GetPathSeparator() + КлючЗначение.Ключ;
		ДополнительныеПараметры.Вставить("ТекКлючФайла", КлючЗначение.Ключ);

		Если ТипЗнч(КлючЗначение.Значение) = Тип("ТекстовыйДокумент") Тогда
			ОповещениеОЗаверешении = Новый ОписаниеОповещения("СохранитьБиблиотекуРедактораЗаписатьНачатьЗаписьОчередногоФайлаТекстовогоДокументаЗавершение",
				ЭтотОбъект, ДополнительныеПараметры);
		Иначе
			ОповещениеОЗаверешении = Новый ОписаниеОповещения("СохранитьБиблиотекуРедактораЗаписатьНачатьЗаписьОчередногоФайлаЗавершение",
				ЭтотОбъект, ДополнительныеПараметры);
		КонецЕсли;

		КлючЗначение.Значение.НачатьЗапись(ОповещениеОЗаверешении, ИмяФайла);
		Прервать;
	КонецЦикла;

	Если Не ЕстьНеСохраненное Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещенияОЗавершении, Истина);
	КонецЕсли;
КонецПроцедуры

Функция КаталогСохраненияРедактора(ВидРедактора)
	СтруктураФайловыхПеременных=UT_CommonClient.SessionFileVariablesStructure();
	Если Не СтруктураФайловыхПеременных.Свойство("TempFilesDirectory") Тогда
		Возврат "";
	КонецЕсли;

	Возврат СтруктураФайловыхПеременных.TempFilesDirectory + "tools_ui_1c" + GetPathSeparator() + Формат(
		UT_CommonClientServer.Version(), "ЧГ=0;") + GetPathSeparator() + ВидРедактора;
КонецФункции

Функция ИмяФайлаРедактораAceДляЯзыка(Язык = "bsl") Экспорт
	Возврат КаталогСохраненияРедактора(UT_CodeEditorClientServer.ВариантыРедактораКода().Ace)
		+ GetPathSeparator() + Язык + ".html";
КонецФункции

Функция ТекстHTMLРедактораКодаAce(КаталогСохраненияБибилиотеки, Язык)

	ТекстAce=КаталогСохраненияБибилиотеки + GetPathSeparator() + "ace" + GetPathSeparator() + "ace.js";
	ТекстLT=КаталогСохраненияБибилиотеки + GetPathSeparator() + "ace" + GetPathSeparator()
		+ "ext-language_tools.js";

	ТекЯзык=НРег(Язык);
	Если ТекЯзык = "bsl" Тогда
		ТекЯзык="_1c";
	КонецЕсли;
	ТекстHTML= "<!DOCTYPE html>
			   |<html lang=""ru"">
			   |<head>
			   |<title>ACE in Action</title>
			   |<style type=""text/css"" media=""screen"">
			   |    #editor { 
			   |        position: absolute;
			   |        top: 0;
			   |        right: 0;
			   |        bottom: 0;
			   |        left: 0;
			   |    }
			   |</style>
			   |</head>
			   |<body>
			   |
			   |<div id=""editor""></div>
			   |    
			   |<script src=""file://" + ТекстAce + """ type=""text/javascript"" charset=""utf-8""></script>
													|<script src=""file://" + ТекстLT + """ type=""text/javascript"" charset=""utf-8""></script>
																						|<script>
																						|    // trigger extension
																						|    ace.require(""ace/ext/language_tools"");
																						|    var editor = ace.edit(""editor"");
																						|    editor.session.setMode(""ace/mode/"
		+ ТекЯзык + """);
					|    editor.setTheme(""ace/theme/ones"");
					|    // enable autocompletion and snippets
					|    editor.setOptions({
					|        selectionStyle: 'line',
					|        highlightSelectedWord: true,
					|        showLineNumbers: true,
					|        enableBasicAutocompletion: true,
					|        enableSnippets: true,
					|        enableLiveAutocompletion: true
					|    });
					|
					|	editor.setHighlightSelectedWord(true);
					|
					|	function setSelection(startRow, startColumn, endRow, endColumn) {
					|		editor.clearSelection();
					|		var rangeEditor = new ace.Range(startRow, startColumn, endRow, endColumn);
					|       var selection = editor.getSelection();
					|       selection.setSelectionRange(rangeEditor, false);
					|		editor.centerSelection();
					|
					|	}
					|
					|</script>
					|
					|</body>
					|</html>";

	Возврат ТекстHTML;
КонецФункции

Процедура ДобавитьКСохранениюТекстовыйДокументДляЯзыкаРедактораКодаAce(СоответствиеФайловБиблиотеки,
	КаталогСохраненияБибилиотеки, Язык)
	Текст= Новый ТекстовыйДокумент;
	Текст.УстановитьТекст(ТекстHTMLРедактораКодаAce(КаталогСохраненияБибилиотеки, Язык));

	СоответствиеФайловБиблиотеки.Вставить(Язык + ".html", Текст);

КонецПроцедуры
#КонецОбласти