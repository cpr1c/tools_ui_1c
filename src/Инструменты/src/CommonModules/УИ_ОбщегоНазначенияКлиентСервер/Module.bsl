
#Область ПрограммныйИнтерфейс

#Область РасписаниеРегЗаданий

// Преобразует РасписаниеРегламентногоЗадания в структуру.
//
// Параметры:
//  Расписание - РасписаниеРегламентногоЗадания - исходное расписание.
// 
// Возвращаемое значение:
//  Структура - расписание в виде структуры.
//
Функция РасписаниеВСтруктуру(Знач Расписание) Экспорт

	ЗначениеРасписания = Расписание;
	Если ЗначениеРасписания = Неопределено Тогда
		ЗначениеРасписания = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	СписокПолей = "ВремяЗавершения,ВремяКонца,ВремяНачала,ДатаКонца,ДатаНачала,ДеньВМесяце,ДеньНеделиВМесяце,"
		+ "ДниНедели,ИнтервалЗавершения,Месяцы,ПаузаПовтора,ПериодНедель,ПериодПовтораВТечениеДня,ПериодПовтораДней";
	Результат = Новый Структура(СписокПолей);
	ЗаполнитьЗначенияСвойств(Результат, ЗначениеРасписания, СписокПолей);
	ДетальныеРасписанияДня = Новый Массив;
	Для Каждого РасписаниеДля Из Расписание.ДетальныеРасписанияДня Цикл
		ДетальныеРасписанияДня.Добавить(РасписаниеВСтруктуру(РасписаниеДля));
	КонецЦикла;
	Результат.Вставить("ДетальныеРасписанияДня", ДетальныеРасписанияДня);
	Возврат Результат;

КонецФункции

// Преобразует структуру в РасписаниеРегламентногоЗадания.
//
// Параметры:
//  СтруктураРасписания - Структура - расписание в виде структуры.
// 
// Возвращаемое значение:
//  РасписаниеРегламентногоЗадания - расписание.
//
Функция СтруктураВРасписание(Знач СтруктураРасписания) Экспорт

	Если СтруктураРасписания = Неопределено Тогда
		Возврат Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	СписокПолей = "ВремяЗавершения,ВремяКонца,ВремяНачала,ДатаКонца,ДатаНачала,ДеньВМесяце,ДеньНеделиВМесяце,"
		+ "ДниНедели,ИнтервалЗавершения,Месяцы,ПаузаПовтора,ПериодНедель,ПериодПовтораВТечениеДня,ПериодПовтораДней";
	Результат = Новый РасписаниеРегламентногоЗадания;
	ЗаполнитьЗначенияСвойств(Результат, СтруктураРасписания, СписокПолей);
	ДетальныеРасписанияДня = Новый Массив;
	Для Каждого Расписание Из СтруктураРасписания.ДетальныеРасписанияДня Цикл
		ДетальныеРасписанияДня.Добавить(СтруктураВРасписание(Расписание));
	КонецЦикла;
	Результат.ДетальныеРасписанияДня = ДетальныеРасписанияДня;
	Возврат Результат;

КонецФункции


#КонецОбласти

#Область УниверсальныеКоллекции

// Дополняет массив МассивПриемник значениями из массива МассивИсточник.
//
// Параметры:
//  МассивПриемник - Массив из Произвольный - массив, в который необходимо добавить значения.
//  МассивИсточник - Массив из Произвольный - массив значений для заполнения.
//  ТолькоУникальныеЗначения - Булево - если истина, то в массив будут включены только уникальные значения.
//
Процедура ДополнитьМассив(МассивПриемник, МассивИсточник, ТолькоУникальныеЗначения = Ложь) Экспорт

	Если ТолькоУникальныеЗначения Тогда

		УникальныеЗначения = Новый Соответствие;

		Для Каждого Значение Из МассивПриемник Цикл
			УникальныеЗначения.Вставить(Значение, Истина);
		КонецЦикла;

		Для Каждого Значение Из МассивИсточник Цикл
			Если УникальныеЗначения[Значение] = Неопределено Тогда
				МассивПриемник.Добавить(Значение);
				УникальныеЗначения.Вставить(Значение, Истина);
			КонецЕсли;
		КонецЦикла;

	Иначе

		Для Каждого Значение Из МассивИсточник Цикл
			МассивПриемник.Добавить(Значение);
		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

// Возвращает значение свойства структуры.
//
// Параметры:
//   Структура - Структура, ФиксированнаяСтруктура - Объект, из которого необходимо прочитать значение ключа.
//   Ключ - Строка - Имя свойства структуры, для которого необходимо прочитать значение.
//   ЗначениеПоУмолчанию - Произвольный - Необязательный. Возвращается когда в структуре нет значения по указанному
//                                        ключу.
//       Для скорости рекомендуется передавать только быстро вычисляемые значения (например примитивные типы),
//       а инициализацию более тяжелых значений выполнять после проверки полученного значения (только если это
//       требуется).
//
// Возвращаемое значение:
//   Произвольный - Значение свойства структуры. ЗначениеПоУмолчанию если в структуре нет указанного свойства.
//
Функция СвойствоСтруктуры(Структура, Ключ, ЗначениеПоУмолчанию = Неопределено) Экспорт

	Если Структура = Неопределено Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;

	Результат = ЗначениеПоУмолчанию;
	Если Структура.Свойство(Ключ, Результат) Тогда
		Возврат Результат;
	Иначе
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;

КонецФункции

// Создает копию значения типа Структура, рекурсивно, с учетом типов значений свойств. 
// Если свойства структуры содержат значения объектных типов (СправочникОбъект, ДокументОбъект и т.п.),
// то их содержимое не копируются, а возвращаются ссылки на исходный объект.
//
// Параметры:
//  СтруктураИсточник - Структура - копируемая структура.
// 
// Возвращаемое значение:
//  Структура - копия исходной структуры.
//
Функция СкопироватьСтруктуру(СтруктураИсточник) Экспорт

	СтруктураРезультат = Новый Структура;

	Для Каждого КлючИЗначение Из СтруктураИсточник Цикл
		СтруктураРезультат.Вставить(КлючИЗначение.Ключ, СкопироватьРекурсивно(КлючИЗначение.Значение));
	КонецЦикла;

	Возврат СтруктураРезультат;

КонецФункции

// Дополняет структуру значениями из другой структуры.
//
// Параметры:
//   Приемник - Структура - коллекция, в которую будут добавляться новые значения.
//   Источник - Структура - коллекция, из которой будут считываться пары Ключ и Значение для заполнения.
//   Заменять - Булево, Неопределено - что делать в местах пересечения ключей источника и приемника:
//                                       Истина - заменять значения приемника (самый быстрый способ),
//                                       Ложь   - не заменять значения приемника (пропускать),
//                                       Неопределено - значение по умолчанию. Бросать исключение.
//
Процедура ДополнитьСтруктуру(Приемник, Источник, Заменять = Неопределено) Экспорт

	Для Каждого Элемент Из Источник Цикл
		Если Заменять <> Истина И Приемник.Свойство(Элемент.Ключ) Тогда
			Если Заменять = Ложь Тогда
				Продолжить;
			Иначе
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Пересечение ключей источника и приемника: ""%1"".'"),
					Элемент.Ключ);
			КонецЕсли;
		КонецЕсли;
		Приемник.Вставить(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;

КонецПроцедуры

// Создает полную копию структуры, соответствия, массива, списка или таблицы значений, рекурсивно, 
// с учетом типов дочерних элементов. При этом содержимое значений объектных типов 
// (СправочникОбъект, ДокументОбъект и т.п.) не копируются, а возвращаются ссылки на исходный объект.
//
// Параметры:
//  Источник - Структура, Соответствие из Произвольный, Массив, СписокЗначений, ТаблицаЗначений - объект, который необходимо 
//             скопировать.
//
// Возвращаемое значение:
//  Структура, Соответствие из Произвольный, Массив, СписокЗначений, ТаблицаЗначений - копия объекта, переданного в параметре Источник.
//
//@skip-check doc-comment-collection-item-type
Функция СкопироватьРекурсивно(Источник) Экспорт

	Перем Приемник;

	ТипИсточника = ТипЗнч(Источник);

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Если ТипИсточника = Тип("ТаблицаЗначений") Тогда
		Возврат Источник.Скопировать();
	КонецЕсли;
#КонецЕсли
	Если ТипИсточника = Тип("Структура") Тогда
		Приемник = СкопироватьСтруктуру(Источник);
	ИначеЕсли ТипИсточника = Тип("Соответствие") Тогда
		Приемник = СкопироватьСоответствие(Источник);
	ИначеЕсли ТипИсточника = Тип("Массив") Тогда
		Приемник = СкопироватьМассив(Источник);
	ИначеЕсли ТипИсточника = Тип("СписокЗначений") Тогда
		Приемник = СкопироватьСписокЗначений(Источник);
	Иначе
		Приемник = Источник;
	КонецЕсли;

	//@skip-check constructor-function-return-section
	Возврат Приемник;

КонецФункции

// Создает копию значения типа Соответствие, рекурсивно, с учетом типов значений.
// Если значения соответствия содержат значения объектных типов (СправочникОбъект, ДокументОбъект и т.п.),
// то их содержимое не копируются, а возвращаются ссылки на исходный объект.
//
// Параметры:
//  СоответствиеИсточник - Соответствие - соответствие, копию которого необходимо получить.
// 
// Возвращаемое значение:
//  Соответствие - копия исходного соответствия.
//
//@skip-check doc-comment-collection-item-type
Функция СкопироватьСоответствие(СоответствиеИсточник) Экспорт

	СоответствиеРезультат = Новый Соответствие;

	Для Каждого КлючИЗначение Из СоответствиеИсточник Цикл
		СоответствиеРезультат.Вставить(КлючИЗначение.Ключ, СкопироватьРекурсивно(КлючИЗначение.Значение));
	КонецЦикла;

	Возврат СоответствиеРезультат;

КонецФункции

// Создает копию значения типа Массив, рекурсивно, с учетом типов значений элементов массива.
// Если элементы массива содержат значения объектных типов (СправочникОбъект, ДокументОбъект и т.п.),
// то их содержимое не копируются, а возвращаются ссылки на исходный объект.
//
// Параметры:
//  МассивИсточник - Массив - массив, копию которого необходимо получить.
// 
// Возвращаемое значение:
//  Массив - копия исходного массива.
//
//@skip-check doc-comment-collection-item-type
Функция СкопироватьМассив(МассивИсточник) Экспорт

	МассивРезультат = Новый Массив;

	Для Каждого Элемент Из МассивИсточник Цикл
		МассивРезультат.Добавить(СкопироватьРекурсивно(Элемент));
	КонецЦикла;

	Возврат МассивРезультат;

КонецФункции

// Создает копию значения типа СписокЗначений, рекурсивно, с учетом типов его значений.
// Если в списке значений есть значения объектных типов (СправочникОбъект, ДокументОбъект и т.п.),
// то их содержимое не копируются, а возвращаются ссылки на исходный объект.
//
// Параметры:
//  СписокИсточник - СписокЗначений - список значений, копию которого необходимо получить.
// 
// Возвращаемое значение:
//  СписокЗначений - копия исходного списка значений.
//
//@skip-check doc-comment-collection-item-type
Функция СкопироватьСписокЗначений(СписокИсточник) Экспорт

	СписокРезультат = Новый СписокЗначений;

	Для Каждого ЭлементСписка Из СписокИсточник Цикл
		СписокРезультат.Добавить(СкопироватьРекурсивно(ЭлементСписка.Значение), ЭлементСписка.Представление,
			ЭлементСписка.Пометка, ЭлементСписка.Картинка);
	КонецЦикла;

	Возврат СписокРезультат;

КонецФункции



#КонецОбласти

#Область КонтекстИсполнения

// Это контекст сервера.
// 
// Возвращаемое значение:
//  Булево -  Это контекст сервера
Функция ЭтоКонтекстСервера() Экспорт

	#Если Сервер Тогда
		Возврат Истина;
	#Иначе
		Возврат Ложь;
	#КонецЕсли
КонецФункции

// Это windows.
// 
// Возвращаемое значение:
//  Булево -  Это windows
Функция ЭтоWindows() Экспорт
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Возврат СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86 Или СистемнаяИнформация.ТипПлатформы
		= ТипПлатформы.Windows_x86_64;
КонецФункции

// Это linux.
// 
// Возвращаемое значение:
//  Булево -  Это linux
Функция ЭтоLinux() Экспорт
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Возврат СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86 Или СистемнаяИнформация.ТипПлатформы
		= ТипПлатформы.Linux_x86_64;
КонецФункции

// Это mac os.
// 
// Возвращаемое значение:
//  Булево -  Это mac os
Функция ЭтоMacOs() Экспорт
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Возврат СистемнаяИнформация.ТипПлатформы = ТипПлатформы.MacOS_x86 Или СистемнаяИнформация.ТипПлатформы
		= ТипПлатформы.MacOS_x86_64;
КонецФункции

// Это разрядность х86.
// 
// Возвращаемое значение:
//  Булево -  Это разрядность х86
Функция ЭтоРазрядностьХ86() Экспорт
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Возврат СистемнаяИнформация.ТипПлатформы = ТипПлатформы.MacOS_x86
			Или СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86
			Или СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86;
КонецФункции

// Это разрядность х64.
// 
// Возвращаемое значение:
//  Булево -  Это разрядность х64
Функция ЭтоРазрядностьХ64() Экспорт
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Возврат СистемнаяИнформация.ТипПлатформы = ТипПлатформы.MacOS_x86_64
			Или СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86_64
			Или СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64;
КонецФункции

// Поле HTMLПостроено на webkit.
// 
// Возвращаемое значение:
//  Булево -  Поле HTMLПостроено на webkit
Функция ПолеHTMLПостроеноНаWebkit() Экспорт
	Возврат ВерсияПлатформыНеМладше_8_3_14() Или ЭтоLinux()
КонецФункции

#КонецОбласти

#Область Переменные

// Имя переменной корректно.
// 
// Параметры:
//  Имя -Строка -Имя
// 
// Возвращаемое значение:
//  Булево -  Имя переменной корректно
Функция ИмяПеременнойКорректно(Имя) Экспорт
	Если НЕ ЗначениеЗаполнено(Имя) Тогда
		Возврат Ложь;
	КонецЕсли;
	ИмяКорректно = Ложь;
	//@skip-check empty-except-statement
	Попытка
		//@skip-check module-unused-local-variable
		СтруктураТест = Новый Структура(Имя);
		ИмяКорректно=Истина;
	Исключение
	КонецПопытки;
	
	Возврат ИмяКорректно;
КонецФункции

// Текст предупреждения о нерпавильном имени переменной.
// 
// Возвращаемое значение:
//  Строка -  Текст предупреждения о нерпавильном имени переменной
Функция ТекстПредупрежденияОНерпавильномИмениПеременной() Экспорт
	Возврат "Неверное имя колонки! Имя должно состоять из одного слова, начинаться с буквы и не содержать специальных символов кроме ""_"".";
КонецФункции

#КонецОбласти

#Область ДинамическийСписок

////////////////////////////////////////////////////////////////////////////////
// Функции для работы с отборами и параметрами динамических списков.
//

// Найти элемент или группу отбора по заданному имени поля или представлению.
//
// Параметры:
//  ОбластьПоиска - ОтборКомпоновкиДанных, КоллекцияЭлементовОтбораКомпоновкиДанных,
//                  ГруппаЭлементовОтбораКомпоновкиДанных - контейнер
//                  с элементами и группами отбора, например Список.Отбор или группа в отборе.
//  ИмяПоля       - Строка - имя поля компоновки (не используется для групп).
//  Представление - Строка - представление поля компоновки.
//
// Возвращаемое значение:
//  Массив - коллекция отборов.
//
Функция НайтиЭлементыИГруппыОтбора(Знач ОбластьПоиска, Знач ИмяПоля = Неопределено, Знач Представление = Неопределено) Экспорт

	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;

	МассивЭлементов = Новый Массив;

	НайтиРекурсивно(ОбластьПоиска.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);

	Возврат МассивЭлементов;

КонецФункции

// Добавить группу отбора в коллекцию КоллекцияЭлементов.
//
// Параметры:
//  КоллекцияЭлементов - ОтборКомпоновкиДанных, КоллекцияЭлементовОтбораКомпоновкиДанных,
//                       ГруппаЭлементовОтбораКомпоновкиДанных - контейнер
//                       с элементами и группами отбора, например Список.Отбор или группа в отборе.
//  Представление      - Строка - представление группы.
//  ТипГруппы          - ТипГруппыЭлементовОтбораКомпоновкиДанных - тип группы.
//
// Возвращаемое значение:
//  ГруппаЭлементовОтбораКомпоновкиДанных - группа отбора.
//
Функция СоздатьГруппуЭлементовОтбора(Знач КоллекцияЭлементов, Представление, ТипГруппы) Экспорт

	Если ТипЗнч(КоллекцияЭлементов) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
		КоллекцияЭлементов = КоллекцияЭлементов.Элементы;
	КонецЕсли;

	ГруппаЭлементовОтбора = НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление);
	Если ГруппаЭлементовОтбора = Неопределено Тогда
		ГруппаЭлементовОтбора = КоллекцияЭлементов.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	Иначе
		ГруппаЭлементовОтбора.Элементы.Очистить();
	КонецЕсли;

	ГруппаЭлементовОтбора.Представление = Представление;
	ГруппаЭлементовОтбора.Применение = ТипПримененияОтбораКомпоновкиДанных.Элементы;
	ГруппаЭлементовОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ГруппаЭлементовОтбора.ТипГруппы = ТипГруппы;
	ГруппаЭлементовОтбора.Использование = Истина;

	Возврат ГруппаЭлементовОтбора;

КонецФункции

// Добавить элемент компоновки в контейнер элементов компоновки.
//
// Параметры:
//  ОбластьДобавления - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                                 например, Список.Отбор или группа в отборе.
//  ИмяПоля                 - Строка - имя поля компоновки данных (заполняется всегда).
//  ПравоеЗначение          - Произвольный - сравниваемое значение.
//  ВидСравнения            - ВидСравненияКомпоновкиДанных - вид сравнения.
//  Представление           - Строка - представление элемента компоновки данных.
//  Использование           - Булево - использование элемента.
//  РежимОтображения        - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - режим отображения.
//  ИдентификаторПользовательскойНастройки - Строка - см. ОтборКомпоновкиДанных.ИдентификаторПользовательскойНастройки
//                                                    в синтакс-помощнике.
// Возвращаемое значение:
//  ЭлементОтбораКомпоновкиДанных - элемент компоновки.
//
Функция ДобавитьЭлементКомпоновки(ОбластьДобавления, Знач ИмяПоля, Знач ВидСравнения, Знач ПравоеЗначение = Неопределено,
	Знач Представление = Неопределено, Знач Использование = Неопределено, Знач РежимОтображения = Неопределено,
	Знач ИдентификаторПользовательскойНастройки = Неопределено) Экспорт

	Элемент = ОбластьДобавления.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
	Элемент.ВидСравнения = ВидСравнения;

	Если РежимОтображения = Неопределено Тогда
		Элемент.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	Иначе
		Элемент.РежимОтображения = РежимОтображения;
	КонецЕсли;

	Если ПравоеЗначение <> Неопределено Тогда
		Элемент.ПравоеЗначение = ПравоеЗначение;
	КонецЕсли;

	Если Представление <> Неопределено Тогда
		Элемент.Представление = Представление;
	КонецЕсли;

	Если Использование <> Неопределено Тогда
		Элемент.Использование = Использование;
	КонецЕсли;

	// Важно: установка идентификатора должна выполняться
	// в конце настройки элемента, иначе он будет скопирован
	// в пользовательские настройки частично заполненным.
	Если ИдентификаторПользовательскойНастройки <> Неопределено Тогда
		Элемент.ИдентификаторПользовательскойНастройки = ИдентификаторПользовательскойНастройки;
	ИначеЕсли Элемент.РежимОтображения <> РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
		Элемент.ИдентификаторПользовательскойНастройки = ИмяПоля;
	КонецЕсли;

	Возврат Элемент;

КонецФункции

// Изменить элемент отбора с заданным именем поля или представлением.
//
// Параметры:
//  ОбластьПоиска - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                             например Список.Отбор или группа в отборе.
//  ИмяПоля                 - Строка - имя поля компоновки данных (заполняется всегда).
//  Представление           - Строка - представление элемента компоновки данных.
//  ПравоеЗначение          - Произвольный - сравниваемое значение.
//  ВидСравнения            - ВидСравненияКомпоновкиДанных - вид сравнения.
//  Использование           - Булево - использование элемента.
//  РежимОтображения        - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - режим отображения.
//  ИдентификаторПользовательскойНастройки - Строка - см. ОтборКомпоновкиДанных.ИдентификаторПользовательскойНастройки
//                                                    в синтакс-помощнике.
//
// Возвращаемое значение:
//  Число - количество измененных элементов.
//
Функция ИзменитьЭлементыОтбора(ОбластьПоиска, Знач ИмяПоля = Неопределено, Знач Представление = Неопределено,
	Знач ПравоеЗначение = Неопределено, Знач ВидСравнения = Неопределено, Знач Использование = Неопределено,
	Знач РежимОтображения = Неопределено, Знач ИдентификаторПользовательскойНастройки = Неопределено) Экспорт

	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;

	МассивЭлементов = Новый Массив;

	НайтиРекурсивно(ОбластьПоиска.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);

	Для Каждого Элемент Из МассивЭлементов Цикл
		Если ИмяПоля <> Неопределено Тогда
			Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
		КонецЕсли;
		Если Представление <> Неопределено Тогда
			Элемент.Представление = Представление;
		КонецЕсли;
		Если Использование <> Неопределено Тогда
			Элемент.Использование = Использование;
		КонецЕсли;
		Если ВидСравнения <> Неопределено Тогда
			Элемент.ВидСравнения = ВидСравнения;
		КонецЕсли;
		Если ПравоеЗначение <> Неопределено Тогда
			Элемент.ПравоеЗначение = ПравоеЗначение;
		КонецЕсли;
		Если РежимОтображения <> Неопределено Тогда
			Элемент.РежимОтображения = РежимОтображения;
		КонецЕсли;
		Если ИдентификаторПользовательскойНастройки <> Неопределено Тогда
			Элемент.ИдентификаторПользовательскойНастройки = ИдентификаторПользовательскойНастройки;
		КонецЕсли;
	КонецЦикла;

	Возврат МассивЭлементов.Количество();

КонецФункции

// Удалить элементы отбора с заданным именем поля или представлением.
//
// Параметры:
//  ОбластьУдаления - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                               например, Список.Отбор или группа в отборе..
//  ИмяПоля         - Строка - имя поля компоновки (не используется для групп).
//  Представление   - Строка - представление поля компоновки.
//
Процедура УдалитьЭлементыГруппыОтбора(Знач ОбластьУдаления, Знач ИмяПоля = Неопределено,
	Знач Представление = Неопределено) Экспорт

	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;

	МассивЭлементов = Новый Массив;

	НайтиРекурсивно(ОбластьУдаления.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);

	Для Каждого Элемент Из МассивЭлементов Цикл
		Если Элемент.Родитель = Неопределено Тогда
			ОбластьУдаления.Элементы.Удалить(Элемент);
		Иначе
			Элемент.Родитель.Элементы.Удалить(Элемент);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

// Добавить или заменить существующий элемент отбора.
//
// Параметры:
//  ОбластьПоискаДобавления - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                     например, Список.Отбор или группа в отборе.
//  ИмяПоля                 - Строка - имя поля компоновки данных (заполняется всегда).
//  ПравоеЗначение          - произвольный - сравниваемое значение.
//  ВидСравнения            - ВидСравненияКомпоновкиДанных - вид сравнения.
//  Представление           - Строка - представление элемента компоновки данных.
//  Использование           - Булево - использование элемента.
//  РежимОтображения        - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - режим отображения.
//  ИдентификаторПользовательскойНастройки - Строка - см. ОтборКомпоновкиДанных.ИдентификаторПользовательскойНастройки
//                                                    в синтакс-помощнике.
//
Процедура УстановитьЭлементОтбора(ОбластьПоискаДобавления, Знач ИмяПоля, Знач ПравоеЗначение = Неопределено,
	Знач ВидСравнения = Неопределено, Знач Представление = Неопределено, Знач Использование = Неопределено,
	Знач РежимОтображения = Неопределено, Знач ИдентификаторПользовательскойНастройки = Неопределено) Экспорт

	ЧислоИзмененных = ИзменитьЭлементыОтбора(ОбластьПоискаДобавления, ИмяПоля, Представление, ПравоеЗначение,
		ВидСравнения, Использование, РежимОтображения, ИдентификаторПользовательскойНастройки);

	Если ЧислоИзмененных = 0 Тогда
		Если ВидСравнения = Неопределено Тогда
			Если ТипЗнч(ПравоеЗначение) = Тип("Массив") Или ТипЗнч(ПравоеЗначение) = Тип("ФиксированныйМассив")
				Или ТипЗнч(ПравоеЗначение) = Тип("СписокЗначений") Тогда
				ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
			Иначе
				ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			КонецЕсли;
		КонецЕсли;
		Если РежимОтображения = Неопределено Тогда
			РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		КонецЕсли;
		ДобавитьЭлементКомпоновки(ОбластьПоискаДобавления, ИмяПоля, ВидСравнения, ПравоеЗначение, Представление,
			Использование, РежимОтображения, ИдентификаторПользовательскойНастройки);
	КонецЕсли;

КонецПроцедуры

// Добавить или заменить существующий элемент отбора динамического списка.
//
// Параметры:
//   ДинамическийСписок - ДинамическийСписок - список, в котором требуется установить отбор.
//   ИмяПоля            - Строка - поле, по которому необходимо установить отбор.
//   ПравоеЗначение     - Произвольный - значение отбора.
//       Необязательный. Значение по умолчанию Неопределено.
//       Внимание! Если передать Неопределено, то значение не будет изменено.
//   ВидСравнения  - ВидСравненияКомпоновкиДанных - условие отбора.
//   Представление - Строка - представление элемента компоновки данных.
//       Необязательный. Значение по умолчанию Неопределено.
//       Если указано, то выводится только флажок использования с указанным представлением (значение не выводится).
//       Для очистки (чтобы значение снова выводилось) следует передать пустую строку.
//   Использование - Булево - флажок использования этого отбора.
//       Необязательный. Значение по умолчанию: Неопределено.
//   РежимОтображения - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - способ отображения этого отбора
//                                                                          пользователю.
//       * РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ - В группе быстрых настроек над списком.
//       * РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный       - В настройка списка (в подменю Еще).
//       * РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный   - Запретить пользователю менять этот отбор.
//   ИдентификаторПользовательскойНастройки - Строка - Уникальный идентификатор этого отбора.
//       Используется для связи с пользовательскими настройками.
//
Процедура УстановитьЭлементОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля, ПравоеЗначение = Неопределено,
	ВидСравнения = Неопределено, Представление = Неопределено, Использование = Неопределено,
	РежимОтображения = Неопределено, ИдентификаторПользовательскойНастройки = Неопределено) Экспорт

	Если РежимОтображения = Неопределено Тогда
		РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли;

	Если РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
		ОтборДинамическогоСписка = ДинамическийСписок.КомпоновщикНастроек.ФиксированныеНастройки.Отбор;
	Иначе
		ОтборДинамическогоСписка = ДинамическийСписок.КомпоновщикНастроек.Настройки.Отбор;
	КонецЕсли;

	УстановитьЭлементОтбора(ОтборДинамическогоСписка, ИмяПоля, ПравоеЗначение, ВидСравнения, Представление,
		Использование, РежимОтображения, ИдентификаторПользовательскойНастройки);

КонецПроцедуры

// Удалить элемент группы отбора динамического списка.
//
// Параметры:
//  ДинамическийСписок - ДинамическийСписок - реквизит формы, для которого требуется установить отбор.
//  ИмяПоля         - Строка - имя поля компоновки (не используется для групп).
//  Представление   - Строка - представление поля компоновки.
//
Процедура УдалитьЭлементыГруппыОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля = Неопределено,
	Представление = Неопределено) Экспорт

	УдалитьЭлементыГруппыОтбора(ДинамическийСписок.КомпоновщикНастроек.ФиксированныеНастройки.Отбор, ИмяПоля,
		Представление);

	УдалитьЭлементыГруппыОтбора(ДинамическийСписок.КомпоновщикНастроек.Настройки.Отбор, ИмяПоля, Представление);

КонецПроцедуры

// Установить или обновить значение параметра ИмяПараметра динамического списка Список.
//
// Параметры:
//  Список          - ДинамическийСписок - реквизит формы, для которого требуется установить параметр.
//  ИмяПараметра    - Строка             - имя параметра динамического списка.
//  Значение        - Произвольный        - новое значение параметра.
//  Использование   - Булево             - признак использования параметра.
//
Процедура УстановитьПараметрДинамическогоСписка(Список, ИмяПараметра, Значение, Использование = Истина) Экспорт

	ЗначениеПараметраКомпоновкиДанных = Список.Параметры.НайтиЗначениеПараметра(
		Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	Если ЗначениеПараметраКомпоновкиДанных <> Неопределено Тогда
		Если Использование И ЗначениеПараметраКомпоновкиДанных.Значение <> Значение Тогда
			ЗначениеПараметраКомпоновкиДанных.Значение = Значение;
		КонецЕсли;
		Если ЗначениеПараметраКомпоновкиДанных.Использование <> Использование Тогда
			ЗначениеПараметраКомпоновкиДанных.Использование = Использование;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Функция УстановитьЗначениеПараметраСКД(КомпоновщикНастроек, ИмяПараметра, ЗначениеПараметра,
	ИспользоватьНеЗаполненный = Истина) Экспорт

	ПараметрУстановлен = Ложь;

	ПараметрКомпоновкиДанных = Новый ПараметрКомпоновкиДанных(ИмяПараметра);
	ЗначениеПараметраКомпоновкиДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(
		ПараметрКомпоновкиДанных);
	Если ЗначениеПараметраКомпоновкиДанных <> Неопределено Тогда

		ЗначениеПараметраКомпоновкиДанных.Значение = ЗначениеПараметра;
		ЗначениеПараметраКомпоновкиДанных.Использование = ?(ИспользоватьНеЗаполненный, Истина, ЗначениеЗаполнено(
			ЗначениеПараметраКомпоновкиДанных.Значение));

		ПараметрУстановлен = Истина;

	КонецЕсли;

	Возврат ПараметрУстановлен;

КонецФункции

Процедура НайтиРекурсивно(КоллекцияЭлементов, МассивЭлементов, СпособПоиска, ЗначениеПоиска)

	Для Каждого ЭлементОтбора Из КоллекцияЭлементов Цикл

		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда

			Если СпособПоиска = 1 Тогда
				Если ЭлементОтбора.ЛевоеЗначение = ЗначениеПоиска Тогда
					МассивЭлементов.Добавить(ЭлементОтбора);
				КонецЕсли;
			ИначеЕсли СпособПоиска = 2 Тогда
				Если ЭлементОтбора.Представление = ЗначениеПоиска Тогда
					МассивЭлементов.Добавить(ЭлементОтбора);
				КонецЕсли;
			КонецЕсли;
		Иначе

			НайтиРекурсивно(ЭлементОтбора.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);

			Если СпособПоиска = 2 И ЭлементОтбора.Представление = ЗначениеПоиска Тогда
				МассивЭлементов.Добавить(ЭлементОтбора);
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

// Выполняет поиск элемента отбора в коллекции по заданному представлению.
//
// Параметры:
//  КоллекцияЭлементов - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                                  например, Список.Отбор.Элементы или группа в отборе.
//  Представление - Строка - представление группы.
// 
// Возвращаемое значение:
//  ЭлементОтбораКомпоновкиДанных - элемент отбора.
//
Функция НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление) Экспорт

	ВозвращаемоеЗначение = Неопределено;

	Для Каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		Если ЭлементОтбора.Представление = Представление Тогда
			ВозвращаемоеЗначение = ЭлементОтбора;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат ВозвращаемоеЗначение
КонецФункции

Процедура СкопироватьЭлементы(ПриемникЗначения, ИсточникЗначения, ОчищатьПриемник = Истина) Экспорт

	Если ТипЗнч(ИсточникЗначения) = Тип("УсловноеОформлениеКомпоновкиДанных") Или ТипЗнч(ИсточникЗначения) = Тип(
		"ВариантыПользовательскогоПоляВыборКомпоновкиДанных") Или ТипЗнч(ИсточникЗначения) = Тип(
		"ОформляемыеПоляКомпоновкиДанных") Или ТипЗнч(ИсточникЗначения) = Тип(
		"ЗначенияПараметровДанныхКомпоновкиДанных") Тогда
		СоздаватьПоТипу = Ложь;
	Иначе
		СоздаватьПоТипу = Истина;
	КонецЕсли;
	ПриемникЭлементов = ПриемникЗначения.Элементы;
	ИсточникЭлементов = ИсточникЗначения.Элементы;
	Если ОчищатьПриемник Тогда
		ПриемникЭлементов.Очистить();
	КонецЕсли;

	Для Каждого ЭлементИсточник Из ИсточникЭлементов Цикл

		Если ТипЗнч(ЭлементИсточник) = Тип("ЭлементПорядкаКомпоновкиДанных") Тогда
			// Элементы порядка добавляем в начало
			Индекс = ИсточникЭлементов.Индекс(ЭлементИсточник);
			ЭлементПриемник = ПриемникЭлементов.Вставить(Индекс, ТипЗнч(ЭлементИсточник));
		Иначе
			Если СоздаватьПоТипу Тогда
				ЭлементПриемник = ПриемникЭлементов.Добавить(ТипЗнч(ЭлементИсточник));
			Иначе
				ЭлементПриемник = ПриемникЭлементов.Добавить();
			КонецЕсли;
		КонецЕсли;

		ЗаполнитьЗначенияСвойств(ЭлементПриемник, ЭлементИсточник);
		// В некоторых коллекциях необходимо заполнить другие коллекции
		Если ТипЗнч(ИсточникЭлементов) = Тип("КоллекцияЭлементовУсловногоОформленияКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Поля, ЭлементИсточник.Поля);
			СкопироватьЭлементы(ЭлементПриемник.Отбор, ЭлементИсточник.Отбор);
			ЗаполнитьЭлементы(ЭлементПриемник.Оформление, ЭлементИсточник.Оформление);
		ИначеЕсли ТипЗнч(ИсточникЭлементов) = Тип("КоллекцияВариантовПользовательскогоПоляВыборКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Отбор, ЭлементИсточник.Отбор);
		КонецЕсли;
		
		// В некоторых элементах коллекции необходимо заполнить другие коллекции
		Если ТипЗнч(ЭлементИсточник) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник, ЭлементИсточник);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник, ЭлементИсточник);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ПользовательскоеПолеВыборКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Варианты, ЭлементИсточник.Варианты);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных") Тогда
			ЭлементПриемник.УстановитьВыражениеДетальныхЗаписей (ЭлементИсточник.ПолучитьВыражениеДетальныхЗаписей());
			ЭлементПриемник.УстановитьВыражениеИтоговыхЗаписей(ЭлементИсточник.ПолучитьВыражениеИтоговыхЗаписей());
			ЭлементПриемник.УстановитьПредставлениеВыраженияДетальныхЗаписей(
				ЭлементИсточник.ПолучитьПредставлениеВыраженияДетальныхЗаписей ());
			ЭлементПриемник.УстановитьПредставлениеВыраженияИтоговыхЗаписей(
				ЭлементИсточник.ПолучитьПредставлениеВыраженияИтоговыхЗаписей ());
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры
Процедура ЗаполнитьЭлементы(ПриемникЗначения, ИсточникЗначения, ПервыйУровень = Неопределено) Экспорт

	Если ТипЗнч(ПриемникЗначения) = Тип("КоллекцияЗначенийПараметровКомпоновкиДанных") Тогда
		КоллекцияЗначений = ИсточникЗначения;
	Иначе
		КоллекцияЗначений = ИсточникЗначения.Элементы;
	КонецЕсли;

	Для Каждого ЭлементИсточник Из КоллекцияЗначений Цикл
		Если ПервыйУровень = Неопределено Тогда
			ЭлементПриемник = ПриемникЗначения.НайтиЗначениеПараметра(ЭлементИсточник.Параметр);
		Иначе
			ЭлементПриемник = ПервыйУровень.НайтиЗначениеПараметра(ЭлементИсточник.Параметр);
		КонецЕсли;
		Если ЭлементПриемник = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭлементПриемник, ЭлементИсточник);
		Если ТипЗнч(ЭлементИсточник) = Тип("ЗначениеПараметраКомпоновкиДанных") Тогда
			Если ЭлементИсточник.ЗначенияВложенныхПараметров.Количество() <> 0 Тогда
				ЗаполнитьЭлементы(ЭлементПриемник.ЗначенияВложенныхПараметров,
					ЭлементИсточник.ЗначенияВложенныхПараметров, ПриемникЗначения);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры


// Копирует настройки компоновки данных
//
// Параметры:
//	НастройкиПриемник	- НастройкиКомпоновкиДанных, НастройкиВложенногоОбъектаКомпоновкиДанных
//		ГруппировкаКомпоновкиДанных, ГруппировкаТаблицыКомпоновкиДанных, ГруппировкаДиаграммыКомпоновкиДанных,
//		ТаблицаКомпоновкиДанных, ДиаграммаКомпоновкиДанных - коллекция настроек КД, куда копируются настройки
//	НастройкиИсточник	- НастройкиКомпоновкиДанных, НастройкиВложенногоОбъектаКомпоновкиДанных
//		ГруппировкаКомпоновкиДанных, ГруппировкаТаблицыКомпоновкиДанных, ГруппировкаДиаграммыКомпоновкиДанных,
//		ТаблицаКомпоновкиДанных, ДиаграммаКомпоновкиДанных - коллекция настроек КД, откуда копируются настройки.
//
Процедура СкопироватьНастройкиКомпоновкиДанных(НастройкиПриемник, НастройкиИсточник) Экспорт
	
	Если НастройкиИсточник = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(НастройкиПриемник) = Тип("НастройкиКомпоновкиДанных") Тогда
		Для каждого Параметр Из НастройкиИсточник.ПараметрыДанных.Элементы Цикл
			ЗначениеПараметра = НастройкиПриемник.ПараметрыДанных.НайтиЗначениеПараметра(Параметр.Параметр);
			Если ЗначениеПараметра <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(ЗначениеПараметра, Параметр);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		ЗаполнитьЗначенияСвойств(НастройкиПриемник, НастройкиИсточник);
		СкопироватьНастройкиКомпоновкиДанных(НастройкиПриемник.Настройки, НастройкиИсточник.Настройки);
		Возврат;
	КонецЕсли;
	
	// Копирование настроек
	Если ТипЗнч(НастройкиИсточник) = Тип("НастройкиКомпоновкиДанных") Тогда
		
		СкопироватьЭлементы(НастройкиПриемник.ПараметрыДанных,		НастройкиИсточник.ПараметрыДанных);
		СкопироватьЭлементы(НастройкиПриемник.ПользовательскиеПоля,	НастройкиИсточник.ПользовательскиеПоля);
		СкопироватьЭлементы(НастройкиПриемник.Отбор,				НастройкиИсточник.Отбор);
		СкопироватьЭлементы(НастройкиПриемник.Порядок,				НастройкиИсточник.Порядок);
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		
		СкопироватьЭлементы(НастройкиПриемник.ПоляГруппировки,	НастройкиИсточник.ПоляГруппировки);
		СкопироватьЭлементы(НастройкиПриемник.Отбор,			НастройкиИсточник.Отбор);
		СкопироватьЭлементы(НастройкиПриемник.Порядок,			НастройкиИсточник.Порядок);
		ЗаполнитьЗначенияСвойств(НастройкиПриемник,				НастройкиИсточник);
		
	КонецЕсли;
	
	СкопироватьЭлементы(НастройкиПриемник.Выбор,				НастройкиИсточник.Выбор);
	СкопироватьЭлементы(НастройкиПриемник.УсловноеОформление,	НастройкиИсточник.УсловноеОформление);
	ЗаполнитьЭлементы(НастройкиПриемник.ПараметрыВывода,		НастройкиИсточник.ПараметрыВывода);
	
	// Копирование структуры
	Если ТипЗнч(НастройкиИсточник) = Тип("НастройкиКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Структура Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Структура.Добавить(ТипЗнч(ЭлементСтруктурыИсточник));
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Структура Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Структура.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ТаблицаКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Строки Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Строки.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Колонки Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Колонки.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ДиаграммаКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Серии Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Серии.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Точки Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Точки.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры



#КонецОбласти

#Область Отладка

Функция СериализоватьЗапросДляОтладки(ОбъектОтладки)
	СтруктураОбъекта = Новый Структура;

	СтруктураОбъекта.Вставить("Текст", ОбъектОтладки.Текст);

	СтруктураОбъекта.Вставить("Параметры", СкопироватьРекурсивно(ОбъектОтладки.Параметры));

	Если ОбъектОтладки.МенеджерВременныхТаблиц <> Неопределено Тогда
		СтруктураВременныхТаблиц = УИ_ОбщегоНазначенияВызовСервера.СтруктураВременныхТаблицМенеджераВременныхТаблиц(
			ОбъектОтладки.МенеджерВременныхТаблиц);
		СтруктураОбъекта.Вставить("ВременныеТаблицы", СтруктураВременныхТаблиц);
	КонецЕсли;

	Возврат СтруктураОбъекта;
КонецФункции

Функция СериализоватьМенеджерВременныхТаблицДляОтладки(ОбъектОтладки)
	СтруктураОбъекта = Новый Структура;

	СтруктураОбъекта.Вставить("Текст", "");
	СтруктураОбъекта.Вставить("Параметры", Новый Структура);
	СтруктураВременныхТаблиц = УИ_ОбщегоНазначенияВызовСервера.СтруктураВременныхТаблицМенеджераВременныхТаблиц(ОбъектОтладки);
	СтруктураОбъекта.Вставить("ВременныеТаблицы", СтруктураВременныхТаблиц);

	Возврат СтруктураОбъекта;
КонецФункции

Функция СериализоватьСКДДляОтладки(СКД, НастройкиСКД, ВнешниеНаборыДанных)
	Возврат УИ_ОбщегоНазначенияВызовСервера.СериализоватьОбъектСКДДляОтладки(СКД, НастройкиСКД, ВнешниеНаборыДанных);
КонецФункции

Функция СериализоватьОбъектБДДляОтладки(ОбъектОтладки)
	СтруктураОбъекта = Новый Структура;
	СтруктураОбъекта.Вставить("Объект", ОбъектОтладки);

	Возврат СтруктураОбъекта;
КонецФункции

Функция СериализоватьHTTPЗапросДляОтладки(ЗапросHTTP, СоединениеHTTP)
	СтруктураОбъекта = Новый Структура;
	СтруктураОбъекта.Вставить("АдресСервера", СоединениеHTTP.Сервер);
	СтруктураОбъекта.Вставить("Порт", СоединениеHTTP.Порт);
	СтруктураОбъекта.Вставить("ИспользоватьHTPPS", СоединениеHTTP.ЗащищенноеСоединение <> Неопределено);
	Если СоединениеHTTP.ЗащищенноеСоединение = Неопределено Тогда
		СтруктураОбъекта.Вставить("Протокол", "http");
	Иначе
		СтруктураОбъекта.Вставить("Протокол", "https");
	КонецЕсли;

	СтруктураОбъекта.Вставить("ПроксиСервер", СоединениеHTTP.Прокси.Сервер(СтруктураОбъекта.Протокол));
	СтруктураОбъекта.Вставить("ПроксиПорт", СоединениеHTTP.Прокси.Порт(СтруктураОбъекта.Протокол));
	СтруктураОбъекта.Вставить("ПроксиПользователь", СоединениеHTTP.Прокси.Пользователь(СтруктураОбъекта.Протокол));
	СтруктураОбъекта.Вставить("ПроксиПароль", СоединениеHTTP.Прокси.Пароль(СтруктураОбъекта.Протокол));
	СтруктураОбъекта.Вставить("ИспользоватьАутентификациюОС", СоединениеHTTP.Прокси.ИспользоватьАутентификациюОС(
		СтруктураОбъекта.Протокол));

	СтруктураОбъекта.Вставить("Запрос", ЗапросHTTP.АдресРесурса);
	СтруктураОбъекта.Вставить("ТелоЗапроса", ЗапросHTTP.ПолучитьТелоКакСтроку());
	СтруктураОбъекта.Вставить("Заголовки", УИ_ОбщегоНазначенияКлиентСервер.ПолучитьСтрокуЗаголовковHTTP(
		ЗапросHTTP.Заголовки));

	ДвоичныеДанныеТела = ЗапросHTTP.ПолучитьТелоКакДвоичныеДанные();
	СтруктураОбъекта.Вставить("ДвоичныеДанныеТела", ДвоичныеДанныеТела);
	СтруктураОбъекта.Вставить("ДвоичныеДанныеТелаСтрокой", Строка(ДвоичныеДанныеТела));

	СтруктураОбъекта.Вставить("ИмяФайлаЗапроса", ЗапросHTTP.ПолучитьИмяФайлаТела());

	Возврат СтруктураОбъекта;

КонецФункции

Функция СериализоватьОбъектДляОтладкиВСтруктуру(ОбъектОтладки, НастройкиСКДИлиСоединениеHTTP, ВнешниеНаборыДанных)
	ТипВсеСсылки = УИ_ОбщегоНазначенияПовтИсп.ОписаниеТипаВсеСсылки();

	СтруктураОбъекта = Новый Структура;
	Если ТипВсеСсылки.СодержитТип(ТипЗнч(ОбъектОтладки)) Тогда
		СтруктураОбъекта = СериализоватьОбъектБДДляОтладки(ОбъектОтладки);
	ИначеЕсли ТипЗнч(ОбъектОтладки) = Тип("HTTPЗапрос") Тогда
		СтруктураОбъекта = СериализоватьHTTPЗапросДляОтладки(ОбъектОтладки, НастройкиСКДИлиСоединениеHTTP);
	ИначеЕсли ТипЗнч(ОбъектОтладки) = Тип("Запрос") Тогда
		СтруктураОбъекта = СериализоватьЗапросДляОтладки(ОбъектОтладки);
	ИначеЕсли ТипЗнч(ОбъектОтладки) = Тип("СхемаКомпоновкиДанных") Тогда
		СтруктураОбъекта = СериализоватьСКДДляОтладки(ОбъектОтладки, НастройкиСКДИлиСоединениеHTTP, ВнешниеНаборыДанных);
	ИначеЕсли ТипЗнч(ОбъектОтладки) = Тип("ТаблицаФормы") Тогда 
		Если Не ЭтоКонтекстСервера() Тогда
			Возврат Неопределено;
		КонецЕсли;
		СКД = ОбъектОтладки.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
		НастройкиСКД = ОбъектОтладки.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
		СтруктураОбъекта = СериализоватьСКДДляОтладки(СКД, НастройкиСКД, Неопределено);
	ИначеЕсли ТипЗнч(ОбъектОтладки) = Тип("МенеджерВременныхТаблиц") Тогда 
		СтруктураОбъекта = СериализоватьМенеджерВременныхТаблицДляОтладки(ОбъектОтладки);
	КонецЕсли;

	Возврат СтруктураОбъекта;
КонецФункции

// Отладить объект.
// 
// Параметры:
// 	ОбъектДляОтладки - Запрос, СхемаКомпоновкиДанных, HTTPЗапрос, ЛюбаяСсылка, ТаблицаФормы - Объект типа Запрос
// 	НастройкиСКДИлиСоединениеHTTP - HTTPСоединение, НастройкиКомпоновкиДанных -
// 	ВнешниеНаборыДанных - Структура Из КлючИЗначение:
// 		* Ключ - Строка -
// 		* ЗНачение - ТаблицаЗначений -
// 	СохранитьВФайл - Булево - Признак сохранения данных отладки в файл на сервере, а не в базу	
// 	Наименование - Строка - Имя сохраненного объекта отладки
// 
// Возвращаемое значение:
//  Неопределено, Строка - Отладить объект
Функция ОтладитьОбъект(ОбъектДляОтладки, НастройкиСКДИлиСоединениеHTTP = Неопределено,
	ВнешниеНаборыДанных = Неопределено, СохранитьВФайл, Наименование = "") Экспорт
	ОткрыватьСразуКонсоль = Ложь;

#Если ТолстыйКлиентОбычноеПриложение Или ТолстыйКлиентУправляемоеПриложение Тогда
	ОткрыватьСразуКонсоль = Истина;
#КонецЕсли

	ТипВсеСсылки = УИ_ОбщегоНазначенияПовтИсп.ОписаниеТипаВсеСсылки();
	СериализованныйОбъект = СериализоватьОбъектДляОтладкиВСтруктуру(ОбъектДляОтладки, НастройкиСКДИлиСоединениеHTTP, ВнешниеНаборыДанных);
	Если ТипВсеСсылки.СодержитТип(ТипЗнч(ОбъектДляОтладки)) Тогда
		ТипОбъектаОтладки = "ОбъектБазыДанных";
	ИначеЕсли ТипЗнч(ОбъектДляОтладки) = Тип("HTTPЗапрос") Тогда
		ТипОбъектаОтладки = "HTTPЗапрос";
	ИначеЕсли ТипЗнч(ОбъектДляОтладки) = Тип("Запрос") Тогда
		ТипОбъектаОтладки = "ЗАПРОС";
	ИначеЕсли ТипЗнч(ОбъектДляОтладки) = Тип("МенеджерВременныхТаблиц") Тогда 
		ТипОбъектаОтладки = "МенеджерВременныхТаблиц";
	ИначеЕсли ТипЗнч(ОбъектДляОтладки) = Тип("СхемаКомпоновкиДанных") Тогда
		ТипОбъектаОтладки = "СхемаКомпоновкиДанных";
	ИначеЕсли ТипЗнч(ОбъектДляОтладки) = Тип("ТаблицаФормы") Тогда
		ТипОбъектаОтладки = "СхемаКомпоновкиДанных";
	КонецЕсли;

	Если ОткрыватьСразуКонсоль Тогда
		ДанныеДляОтладки = ПоместитьВоВременноеХранилище(СериализованныйОбъект);
#Если Клиент Тогда

		УИ_ОбщегоНазначенияКлиент.ОткрытьКонсольОтладки(ТипОбъектаОтладки, ДанныеДляОтладки);

#КонецЕсли
		Возврат Неопределено;

	ИначеЕсли СохранитьВФайл Тогда
		Возврат УИ_ОбщегоНазначенияВызовСервера.ЗаписатьДанныеДляОтладкиВФайл(ТипОбъектаОтладки,
																			  СериализованныйОбъект,
																			  Наименование);
	Иначе
		Возврат УИ_ОбщегоНазначенияВызовСервера.ЗаписатьДанныеДляОтладкиВСправочник(ТипОбъектаОтладки,
																					СериализованныйОбъект,
																					Наименование);
	КонецЕсли;
КонецФункции

Функция КлючДанныхОбъектаДанныхОтладкиВХранилищеНастроек() Экспорт
	Возврат "УИ_УниверсальныеИнструменты_ДанныеДляОтладки";
КонецФункции

Функция КлючОбъектаВХранилищеНастроек() Экспорт
	Возврат "УИ_УниверсальныеИнструменты";
КонецФункции

#КонецОбласти

#Область HTTPЗапросы

Функция ЗаголовкиHTTPЗапросаИзСтроки(СтрокаЗаголовков) Экспорт
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(СтрокаЗаголовков);

	Заголовки = Новый Соответствие;

	Для НомерСтроки = 1 По ТекстовыйДокумент.КоличествоСтрок() Цикл
		ЗаголовокСтр = ТекстовыйДокумент.ПолучитьСтроку(НомерСтроки);

		Если Не ЗначениеЗаполнено(ЗаголовокСтр) Тогда
			Продолжить;
		КонецЕсли;

		МассивЗаголовка = СтрРазделить(ЗаголовокСтр, ":");
		Если МассивЗаголовка.Количество() <> 2 Тогда
			Продолжить;
		КонецЕсли;

		Заголовки.Вставить(МассивЗаголовка[0], МассивЗаголовка[1]);

	КонецЦикла;

	Возврат Заголовки;
КонецФункции

Функция ПолучитьСтрокуЗаголовковHTTP(Заголовки) Экспорт
	СтрокаЗаголовков = "";

	Для Каждого КлючЗначение Из Заголовки Цикл
		СтрокаЗаголовков = СтрокаЗаголовков
						   + ?(ЗначениеЗаполнено(СтрокаЗаголовков), Символы.ПС, "")
						   + КлючЗначение.Ключ
						   + ":"
						   + КлючЗначение.Значение;
	КонецЦикла;

	Возврат СтрокаЗаголовков;
КонецФункции

#КонецОбласти

#Область РаботаСJSON

Функция мПрочитатьJSON(Значение, ПрочитатьВСоответствие = Ложь) Экспорт
#Если ВебКлиент Тогда
	Возврат УИ_ОбщегоНазначенияВызовСервера.мПрочитатьJSON(Значение);
#Иначе
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Значение);

		ДанныеДокументаJSON =ПрочитатьJSON(ЧтениеJSON,ПрочитатьВСоответствие);
		ЧтениеJSON.Закрыть();

		Возврат ДанныеДокументаJSON;
#КонецЕсли
КонецФункции // ПрочитатьJSON()

Функция мЗаписатьJSON(СтруктураДанных) Экспорт
#Если ВебКлиент Тогда
	Возврат УИ_ОбщегоНазначенияВызовСервера.мЗаписатьJSON(СтруктураДанных);
#Иначе

		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		ЗаписатьJSON(ЗаписьJSON, СтруктураДанных);
		СериализованнаяСтрока = ЗаписьJSON.Закрыть();
		Возврат СериализованнаяСтрока;
#КонецЕсли

КонецФункции // ЗаписатьJSON(

Функция мПрочитатьJSONИзФайла(ИмяФайла, КодировкаФайла = "UTF8") Экспорт
#Если Не ВебКлиент Тогда
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.ОткрытьФайл(ИмяФайла, КодировкаФайла);

	Данные = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();

	Возврат Данные;
#КонецЕсли

	Возврат Неопределено;
КонецФункции

Процедура мЗаписатьJSONВФайл(ИмяФайла, СтруктураДанных, КодировкаФайла = "UTF8") Экспорт
#Если Не ВебКлиент Тогда
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.ОткрытьФайл(ИмяФайла, КодировкаФайла);
	ЗаписатьJSON(ЗаписьJSON, СтруктураДанных);
	ЗаписьJSON.Закрыть();
#КонецЕсли
КонецПроцедуры

#КонецОбласти

#Область НедопустимыеСимволы
// Возвращает строку недопустимых символов.
// Согласно http://en.wikipedia.org/wiki/Filename - в разделе "Reserved characters and words".
// Возвращаемое значение:
//   Строка - строка недопустимых символов.
//
Функция ПолучитьНедопустимыеСимволыВИмениФайла() Экспорт

	НедопустимыеСимволы = """/\[]:;|=?*<>";
	НедопустимыеСимволы = НедопустимыеСимволы + Символы.Таб + Символы.ПС;
	Возврат НедопустимыеСимволы;

КонецФункции

// Проверяет наличие недопустимых символов в имени файла.
//
// Параметры:
//  ИмяФайла  - Строка - имя файла.
//
// Возвращаемое значение:
//   Массив   - массив обнаруженных в имени файла недопустимых символов.
//              Если недопустимых символов не обнаружено - возвращается пустой массив.
//
Функция НайтиНедопустимыеСимволыВИмениФайла(ИмяФайла) Экспорт

	НедопустимыеСимволы = ПолучитьНедопустимыеСимволыВИмениФайла();
	
	МассивНайденныхНедопустимыхСимволов = Новый Массив;
	
	Для ПозицияСимвола = 1 По СтрДлина(НедопустимыеСимволы) Цикл
		ПроверяемыйСимвол = Сред(НедопустимыеСимволы,ПозицияСимвола,1);
		Если СтрНайти(ИмяФайла,ПроверяемыйСимвол) <> 0 Тогда
			МассивНайденныхНедопустимыхСимволов.Добавить(ПроверяемыйСимвол);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивНайденныхНедопустимыхСимволов;

КонецФункции

// Заменяет недопустимые символы в имени файла.
//
// Параметры:
//  ИмяФайла     - Строка - исходное имя файла.
//  НаЧтоМенять  - Строка - строка, на которую необходимо заменить недопустимые символы.
//
// Возвращаемое значение:
//   Строка - преобразованное имя файла.
//
Функция ЗаменитьНедопустимыеСимволыВИмениФайла(Знач ИмяФайла, НаЧтоМенять = " ") Экспорт
	
	Возврат СокрЛП(СтрСоединить(СтрРазделить(ИмяФайла, ПолучитьНедопустимыеСимволыВИмениФайла(), Истина), НаЧтоМенять));

КонецФункции


#КонецОбласти

#Область СравнениеВерсий

// Версия платформы не младше 8 3 14.
// 
// Возвращаемое значение:
//  Булево -  Версия платформы не младше 8 3 14
Функция ВерсияПлатформыНеМладше_8_3_14() Экспорт
	Возврат ВерсияПлатформыНеМладше("8.3.14");
КонецФункции

// Версия платформы не младше.
// 
// Параметры:
//  ВерсияДляСравнения - Строка -  Версия для сравнения
// 
// Возвращаемое значение:
//  Булево -  Версия платформы не младше
Функция ВерсияПлатформыНеМладше(ВерсияДляСравнения) Экспорт
	ВерсияБезСборки=ВерсияКонфигурацииБезНомераСборки(ТекущаяВерсияПлатформы1СПредприятие());

	Возврат СравнитьВерсииБезНомераСборки(ВерсияБезСборки, ВерсияДляСравнения)>=0;
КонецФункции

// Получает номер версии конфигурации без номера сборки.
//
// Параметры:
//  Версия - Строка - версия конфигурации в формате РР.ПП.ЗЗ.СС,
//                    где СС - номер сборки, который будет удален.
// 
// Возвращаемое значение:
//  Строка - номер версии конфигурации без номера сборки в формате РР.ПП.ЗЗ.
//
Функция ВерсияКонфигурацииБезНомераСборки(Знач Версия) Экспорт

	Массив = СтрРазделить(Версия, ".");

	Если Массив.Количество() < 3 Тогда
		Возврат Версия;
	КонецЕсли;

	Результат = "[Редакция].[Подредакция].[Релиз]";
	Результат = СтрЗаменить(Результат, "[Редакция]", Массив[0]);
	Результат = СтрЗаменить(Результат, "[Подредакция]", Массив[1]);
	Результат = СтрЗаменить(Результат, "[Релиз]", Массив[2]);

	Возврат Результат;
КонецФункции

// Сравнить две строки версий.
//
// Параметры:
//  СтрокаВерсии1  - Строка - номер версии в формате РР.{П|ПП}.ЗЗ.СС.
//  СтрокаВерсии2  - Строка - второй сравниваемый номер версии.
//
// Возвращаемое значение:
//   Число   - больше 0, если СтрокаВерсии1 > СтрокаВерсии2; 0, если версии равны.
//
Функция СравнитьВерсии(Знач СтрокаВерсии1, Знач СтрокаВерсии2) Экспорт

	Строка1 = ?(ПустаяСтрока(СтрокаВерсии1), "0.0.0.0", СтрокаВерсии1);
	Строка2 = ?(ПустаяСтрока(СтрокаВерсии2), "0.0.0.0", СтрокаВерсии2);
	Версия1 = СтрРазделить(Строка1, ".");
	Если Версия1.Количество() <> 4 Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Неправильный формат параметра СтрокаВерсии1: %1'"), СтрокаВерсии1);
	КонецЕсли;
	Версия2 = СтрРазделить(Строка2, ".");
	Если Версия2.Количество() <> 4 Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Неправильный формат параметра СтрокаВерсии2: %1'"), СтрокаВерсии2);
	КонецЕсли;

	Результат = 0;
	Для Разряд = 0 По 3 Цикл
		Результат = Число(Версия1[Разряд]) - Число(Версия2[Разряд]);
		Если Результат <> 0 Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;

КонецФункции

// Сравнить две строки версий.
//
// Параметры:
//  СтрокаВерсии1  - Строка - номер версии в формате РР.{П|ПП}.ЗЗ.
//  СтрокаВерсии2  - Строка - второй сравниваемый номер версии.
//
// Возвращаемое значение:
//   Число   - больше 0, если СтрокаВерсии1 > СтрокаВерсии2; 0, если версии равны.
//
Функция СравнитьВерсииБезНомераСборки(Знач СтрокаВерсии1, Знач СтрокаВерсии2) Экспорт

	Строка1 = ?(ПустаяСтрока(СтрокаВерсии1), "0.0.0", СтрокаВерсии1);
	Строка2 = ?(ПустаяСтрока(СтрокаВерсии2), "0.0.0", СтрокаВерсии2);
	Версия1 = СтрРазделить(Строка1, ".");
	Если Версия1.Количество() <> 3 Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Неправильный формат параметра СтрокаВерсии1: %1'"), СтрокаВерсии1);
	КонецЕсли;
	Версия2 = СтрРазделить(Строка2, ".");
	Если Версия2.Количество() <> 3 Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Неправильный формат параметра СтрокаВерсии2: %1'"), СтрокаВерсии2);
	КонецЕсли;

	Результат = 0;
	Для Разряд = 0 По 2 Цикл
		Результат = Число(Версия1[Разряд]) - Число(Версия2[Разряд]);
		Если Результат <> 0 Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;

КонецФункции



#КонецОбласти

#Область ПараметрыЗаписи

Функция СтруктураПараметровЗаписиПоУмолчанию() Экспорт
	ПараметрыЗаписи=Новый Структура;
	ПараметрыЗаписи.Вставить("БезАвторегистрацииИзменений", Ложь);
	ПараметрыЗаписи.Вставить("ЗаписьВРежимеЗагрузки", Ложь);
	ПараметрыЗаписи.Вставить("ПривилегированныйРежим", Ложь);
	ПараметрыЗаписи.Вставить("ИспользоватьДопСвойства", Ложь);
	ПараметрыЗаписи.Вставить("ДополнительныеСвойства", Новый Структура);
	ПараметрыЗаписи.Вставить("ИспользоватьПроцедуруПередЗаписью", Ложь);
	ПараметрыЗаписи.Вставить("ПроцедураПередЗаписью", "");

	Возврат ПараметрыЗаписи;
КонецФункции

Функция ПараметрыЗаписиДляВыводаНаФормуИнструмента() Экспорт
	Массив=Новый Массив;
	Массив.Добавить("ЗаписьВРежимеЗагрузки");
	Массив.Добавить("ПривилегированныйРежим");
	Массив.Добавить("БезАвторегистрацииИзменений");

	Возврат Массив;
КонецФункции

Функция ПараметрыЗаписиФормы(Форма, ПрефиксРеквизитаФормы = "ПараметрЗаписи_") Экспорт
	ПараметрыЗаписи=СтруктураПараметровЗаписиПоУмолчанию();

	Для Каждого КлючЗначение Из ПараметрыЗаписи Цикл
		Если ТипЗнч(КлючЗначение.Значение) = Тип("Структура") Тогда
			Для Каждого Стр Из Форма[ПрефиксРеквизитаФормы + КлючЗначение.Ключ] Цикл
				ПараметрыЗаписи[КлючЗначение.Ключ].Вставить(Стр.Ключ, Стр.Значение);
			КонецЦикла;
		Иначе
			ПараметрыЗаписи[КлючЗначение.Ключ]=Форма[ПрефиксРеквизитаФормы + КлючЗначение.Ключ];
		КонецЕсли;
	КонецЦикла;
//	ЗаполнитьЗначенияСвойств(ПараметрыЗаписи, Форма);

	Возврат ПараметрыЗаписи;
КонецФункции

Процедура УстановитьПараметрыЗаписиНаФорму(Форма, ПараметрыЗаписи, ПрефиксРеквизитаФормы = "ПараметрЗаписи_") Экспорт
	Для Каждого КлючЗначение Из ПараметрыЗаписи Цикл
		Если ТипЗнч(КлючЗначение.Значение) = Тип("Структура") Тогда
			Для Каждого КЗ Из КлючЗначение.Значение Цикл
				НС=Форма[ПрефиксРеквизитаФормы + КлючЗначение.Ключ].Добавить();
				НС.Ключ=КЗ.Ключ;
				НС.Значение=КЗ.Значение;
			КонецЦикла;
		Иначе
			Форма[ПрефиксРеквизитаФормы + КлючЗначение.Ключ]=КлючЗначение.Значение;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СКД

#КонецОбласти

#Область ФайловыеФункции

// Объединяет компоненты файлового пути, с учетом разделителей, принятых в данной ОС. 
// При этом корректно, без дублирования, обрабатываются уже существующие разделители пути.
// 
// Параметры:
//  Путь1 - Строка - Первая часть пути
//  Путь2 - Строка - Вторая часть пути
//  Путь3 - Строка, Неопределено - Третья часть пути
//  Путь4 - Строка, Неопределено - Четвертая часть пути
// 
// Возвращаемое значение:
//  Строка - Объединенный путь.
Функция ОбъединитьПути(Путь1, Путь2, Путь3 = Неопределено, Путь4 = Неопределено) Экспорт
	Разделитель = ПолучитьРазделительПути();
	
	МассивПути = СтрРазделить(Путь1, Разделитель, Ложь);
	
	Если СтрНачинаетсяС(Путь1, Разделитель) Тогда
		МассивПути.Вставить(0, "");
	КонецЕсли;
	
	ДопПути = Новый Массив;
	ДопПути.Добавить(Путь2);
	Если ЗначениеЗаполнено(Путь3) Тогда
		ДопПути.Добавить(Путь3);
	КонецЕсли;
	Если ЗначениеЗаполнено(Путь4) Тогда
		ДопПути.Добавить(Путь4);
	КонецЕсли;
	
	Для Каждого ТекПуть Из ДопПути Цикл
		МассивТекПути = СтрРазделить(ТекПуть, Разделитель, Ложь);
		
		Для Каждого ТекКусочекПути Из МассивТекПути Цикл
			МассивПути.Добавить(ТекКусочекПути);
		КонецЦикла;
	КонецЦикла;
	
	Возврат СтрСоединить(МассивПути, Разделитель);
	
КонецФункции

// Каталог вспомогательных библиотек инструментов.
// 
// Параметры:
//  РабочийКаталогПользователя - Строка - Каталог, в котором будет распологаться вспомогательные данные инструментов
// 
// Возвращаемое значение:
//  Строка - Каталог вспомогательных библиотек инструментов
Функция КаталогВспомогательныхБиблиотекИнструментов(РабочийКаталогПользователя) Экспорт
	Возврат ОбъединитьПути(РабочийКаталогПользователя, "tools_ui_1c", Формат(Версия(), "ЧГ=0;"))
КонецФункции

// Случайное имя файла.
// 
// Параметры:
//  Расширение - Строка - Расширение
//  Префикс - Строка - Префикс
// 
// Возвращаемое значение:
//  Строка - Случайное имя файла
Функция СлучайноеИмяФайла(Расширение = "tmp", Префикс = "") Экспорт
	Возврат Префикс
//			+ Формат(СлучайноеЧисло, "ЧГ=0;")
			+ Формат(ТекущаяУниверсальнаяДатаВМиллисекундах(), "ЧГ=0;")
			+ "."
			+ Расширение;
КонецФункции

// Получить индекс пиктограммы файла.
// 
// Параметры:
//  РасширениеФайла - Строка -Расширение файла
// 
// Возвращаемое значение:
//  Число -  Получить индекс пиктограммы файла
Функция ПолучитьИндексПиктограммыФайла(Знач РасширениеФайла) Экспорт

	Если ТипЗнч(РасширениеФайла) <> Тип("Строка") Или ПустаяСтрока(РасширениеФайла) Тогда

		Возврат 0;
	КонецЕсли;

	РасширениеФайла = РасширениеБезТочки(РасширениеФайла);

	Расширение = "." + НРег(РасширениеФайла) + ";";

	Если СтрНайти(".dt;.1cd;.cf;.cfu;", Расширение) <> 0 Тогда
		Возврат 6; // Файлы 1С.

	ИначеЕсли Расширение = ".mxl;" Тогда
		Возврат 8; // Табличный Файл.

	ИначеЕсли СтрНайти(".txt;.log;.ini;", Расширение) <> 0 Тогда
		Возврат 10; // Текстовый Файл.

	ИначеЕсли Расширение = ".epf;" Тогда
		Возврат 12; // Внешние обработки.

	ИначеЕсли СтрНайти(".ico;.wmf;.emf;", Расширение) <> 0 Тогда
		Возврат 14; // Картинки.

	ИначеЕсли СтрНайти(".htm;.html;.url;.mht;.mhtml;", Расширение) <> 0 Тогда
		Возврат 16; // HTML.

	ИначеЕсли СтрНайти(".doc;.dot;.rtf;", Расширение) <> 0 Тогда
		Возврат 18; // Файл Microsoft Word.

	ИначеЕсли СтрНайти(".xls;.xlw;", Расширение) <> 0 Тогда
		Возврат 20; // Файл Microsoft Excel.

	ИначеЕсли СтрНайти(".ppt;.pps;", Расширение) <> 0 Тогда
		Возврат 22; // Файл Microsoft PowerPoint.

	ИначеЕсли СтрНайти(".vsd;", Расширение) <> 0 Тогда
		Возврат 24; // Файл Microsoft Visio.

	ИначеЕсли СтрНайти(".mpp;", Расширение) <> 0 Тогда
		Возврат 26; // Файл Microsoft Visio.

	ИначеЕсли СтрНайти(".mdb;.adp;.mda;.mde;.ade;", Расширение) <> 0 Тогда
		Возврат 28; // База данных Microsoft Access.

	ИначеЕсли СтрНайти(".xml;", Расширение) <> 0 Тогда
		Возврат 30; // xml.

	ИначеЕсли СтрНайти(".msg;", Расширение) <> 0 Тогда
		Возврат 32; // Письмо электронной почты.

	ИначеЕсли СтрНайти(".zip;.rar;.arj;.cab;.lzh;.ace;", Расширение) <> 0 Тогда
		Возврат 34; // Архивы.

	ИначеЕсли СтрНайти(".exe;.com;.bat;.cmd;", Расширение) <> 0 Тогда
		Возврат 36; // Исполняемые файлы.

	ИначеЕсли СтрНайти(".grs;", Расширение) <> 0 Тогда
		Возврат 38; // Графическая схема.

	ИначеЕсли СтрНайти(".geo;", Расширение) <> 0 Тогда
		Возврат 40; // Географическая схема.

	ИначеЕсли СтрНайти(".jpg;.jpeg;.jp2;.jpe;", Расширение) <> 0 Тогда
		Возврат 42; // jpg.

	ИначеЕсли СтрНайти(".bmp;.dib;", Расширение) <> 0 Тогда
		Возврат 44; // bmp.

	ИначеЕсли СтрНайти(".tif;.tiff;", Расширение) <> 0 Тогда
		Возврат 46; // tif.

	ИначеЕсли СтрНайти(".gif;", Расширение) <> 0 Тогда
		Возврат 48; // gif.

	ИначеЕсли СтрНайти(".png;", Расширение) <> 0 Тогда
		Возврат 50; // png.

	ИначеЕсли СтрНайти(".pdf;", Расширение) <> 0 Тогда
		Возврат 52; // pdf.

	ИначеЕсли СтрНайти(".odt;", Расширение) <> 0 Тогда
		Возврат 54; // Open Office writer.

	ИначеЕсли СтрНайти(".odf;", Расширение) <> 0 Тогда
		Возврат 56; // Open Office math.

	ИначеЕсли СтрНайти(".odp;", Расширение) <> 0 Тогда
		Возврат 58; // Open Office Impress.

	ИначеЕсли СтрНайти(".odg;", Расширение) <> 0 Тогда
		Возврат 60; // Open Office draw.

	ИначеЕсли СтрНайти(".ods;", Расширение) <> 0 Тогда
		Возврат 62; // Open Office calc.

	ИначеЕсли СтрНайти(".mp3;", Расширение) <> 0 Тогда
		Возврат 64;

	ИначеЕсли СтрНайти(".erf;", Расширение) <> 0 Тогда
		Возврат 66; // Внешние отчеты.

	ИначеЕсли СтрНайти(".docx;", Расширение) <> 0 Тогда
		Возврат 68; // Файл Microsoft Word docx.

	ИначеЕсли СтрНайти(".xlsx;", Расширение) <> 0 Тогда
		Возврат 70; // Файл Microsoft Excel xlsx.

	ИначеЕсли СтрНайти(".pptx;", Расширение) <> 0 Тогда
		Возврат 72; // Файл Microsoft PowerPoint pptx.

	ИначеЕсли СтрНайти(".p7s;", Расширение) <> 0 Тогда
		Возврат 74; // Файл подписи.

	ИначеЕсли СтрНайти(".p7m;", Расширение) <> 0 Тогда
		Возврат 76; // зашифрованное сообщение.
	Иначе
		Возврат 4;
	КонецЕсли;

КонецФункции

// Преобразует расширение файла в нижний регистр без точки.
//
// Параметры:
//  Расширение - Строка - Расширение для преобразования.
//
// Возвращаемое значение:
//  Строка.
//
Функция РасширениеБезТочки(Знач Расширение) Экспорт

	Расширение = НРег(СокрЛП(Расширение));

	Если Сред(Расширение, 1, 1) = "." Тогда
		Расширение = Сред(Расширение, 2);
	КонецЕсли;

	Возврат Расширение;

КонецФункции

#КонецОбласти

#Область НастройкиИнструментов

Функция ИмяСохраненнойНастройкиКаталогаИнструментовНаСервере() Экспорт
	Возврат "УИ_КаталогДанныхИнструментовНаСервере";
КонецФункции

Функция КлючДанныхНастроекВХранилищеНастроек() Экспорт
	Возврат "УИ_УниверсальныеИнструменты_Настройки";
КонецФункции

Функция КлючНастроекПараметровСеанса() Экспорт
	Возврат "ПараметрыСеанса";
КонецФункции

#КонецОбласти

#Область ПараметрыПоставки

Функция ИмяФайлаСкачивания() Экспорт
	Возврат "UI.cfe";
КонецФункции

Функция ВариантПоставки() Экспорт
	Возврат "Расширение";
КонецФункции

Функция ВариантПоставкиПортативный() Экспорт
	Возврат "Портативный";
КонецФункции

Функция Версия() Экспорт
	Возврат "1.4.6";	
КонецФункции

Функция ЭтоПортативнаяПоставка() Экспорт
	Возврат ВариантПоставки() = ВариантПоставкиПортативный();	
КонецФункции

#КонецОбласти

#Область РаботаСТипами

// Представление типов.
// 
// Параметры:
//  ОжидаемыеТипы - ОписаниеТипов, Тип, Массив из Тип-  Ожидаемые типы
// 
// Возвращаемое значение:
//  Строка -  Представление типов
Функция ПредставлениеТипов(ОжидаемыеТипы) Экспорт
	Если ТипЗнч(ОжидаемыеТипы) = Тип("Массив") Тогда
		Результат = "";
		Индекс = 0;
		Для Каждого Тип Из ОжидаемыеТипы Цикл
			Если Не ПустаяСтрока(Результат) Тогда
				Результат = Результат + ", ";
			КонецЕсли;
			Результат = Результат + ПредставлениеТипа(Тип);
			Индекс = Индекс + 1;
			Если Индекс > 10 Тогда
				Результат = Результат + ",... " + СтрШаблон(НСтр("ru='(всего %1 типов)'"), ОжидаемыеТипы.Количество());
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Возврат Результат;
	Иначе
		Возврат ПредставлениеТипа(ОжидаемыеТипы);
	КонецЕсли;
КонецФункции

// Представление типа.
// 
// Параметры:
//  Тип - Произвольный, Тип, ОписаниеТипов -  Тип
// 
// Возвращаемое значение:
//  Строка -  Представление типа
Функция ПредставлениеТипа(Тип) Экспорт
	Если Тип = Неопределено Тогда
		Возврат "Неопределено";
	ИначеЕсли ТипЗнч(Тип) = Тип("ОписаниеТипов") Тогда
		ТипСтрокой = Строка(Тип);
		Возврат ?(СтрДлина(ТипСтрокой) > 150, Лев(ТипСтрокой, 150) + "..." + СтрШаблон(НСтр("ru='(всего %1 типов)'"),
			Тип.Типы().Количество()), ТипСтрокой);
	Иначе
		ТипСтрокой = Строка(Тип);
		Возврат ?(СтрДлина(ТипСтрокой) > 150, Лев(ТипСтрокой, 150) + "...", ТипСтрокой);
	КонецЕсли;
КонецФункции

// Значение ожидаемого типа.
// 
// Параметры:
//  Значение - Неопределено, Произвольный -  Значение
//  ОжидаемыеТипы - ОписаниеТипов, Тип, Массив из Тип, Произвольный -  Ожидаемые типы
// 
// Возвращаемое значение:
//  Булево, Неопределено -  Значение ожидаемого типа
Функция ЗначениеОжидаемогоТипа(Значение, ОжидаемыеТипы) Экспорт
	ТипЗначения = ТипЗнч(Значение);
	Если ТипЗнч(ОжидаемыеТипы) = Тип("ОписаниеТипов") Тогда
		Возврат ОжидаемыеТипы.Типы().Найти(ТипЗначения) <> Неопределено;
	ИначеЕсли ТипЗнч(ОжидаемыеТипы) = Тип("Тип") Тогда
		Возврат ТипЗначения = ОжидаемыеТипы;
	ИначеЕсли ТипЗнч(ОжидаемыеТипы) = Тип("Массив") Или ТипЗнч(ОжидаемыеТипы) = Тип("ФиксированныйМассив") Тогда
		Возврат ОжидаемыеТипы.Найти(ТипЗначения) <> Неопределено;
	ИначеЕсли ТипЗнч(ОжидаемыеТипы) = Тип("Соответствие") Или ТипЗнч(ОжидаемыеТипы) = Тип("ФиксированноеСоответствие") Тогда
		Возврат ОжидаемыеТипы.Получить(ТипЗначения) <> Неопределено;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

// Создает объект ОписаниеТипов, содержащий тип Строка.
//
// Параметры:
//  ДлинаСтроки - Число - длина строки.
//
// Возвращаемое значение:
//  ОписаниеТипов - описание типа Строка.
//
Функция ОписаниеТипаСтрока(ДлинаСтроки) Экспорт

	Массив = Новый Массив;
	Массив.Добавить(Тип("Строка"));

	КвалификаторСтроки = Новый КвалификаторыСтроки(ДлинаСтроки, ДопустимаяДлина.Переменная);

	Возврат Новый ОписаниеТипов(Массив, , КвалификаторСтроки);

КонецФункции

// Создает объект ОписаниеТипов, содержащий тип Число.
//
// Параметры:
//  Разрядность - Число - общее количество разрядов числа (количество разрядов
//                        целой части плюс количество разрядов дробной части).
//  РазрядностьДробнойЧасти - Число - число разрядов дробной части.
//  ЗнакЧисла - ДопустимыйЗнак - допустимый знак числа.
//
// Возвращаемое значение:
//  ОписаниеТипов - описание типа Число.
Функция ОписаниеТипаЧисло(Разрядность, РазрядностьДробнойЧасти = 0, ЗнакЧисла = Неопределено) Экспорт

	Если ЗнакЧисла = Неопределено Тогда
		КвалификаторЧисла = Новый КвалификаторыЧисла(Разрядность, РазрядностьДробнойЧасти);
	Иначе
		КвалификаторЧисла = Новый КвалификаторыЧисла(Разрядность, РазрядностьДробнойЧасти, ЗнакЧисла);
	КонецЕсли;

	Возврат Новый ОписаниеТипов("Число", КвалификаторЧисла);

КонецФункции

// Создает объект ОписаниеТипов, содержащий тип Дата.
//
// Параметры:
//  ЧастиДаты - ЧастиДаты - набор вариантов использования значений типа Дата.
//
// Возвращаемое значение:
//  ОписаниеТипов - описание типа Дата.
Функция ОписаниеТипаДата(ЧастиДаты) Экспорт

	Массив = Новый Массив;
	Массив.Добавить(Тип("Дата"));

	КвалификаторДаты = Новый КвалификаторыДаты(ЧастиДаты);

	Возврат Новый ОписаниеТипов(Массив, , , КвалификаторДаты);

КонецФункции

// Тип таблица значений.
// 
// Возвращаемое значение:
//  Тип -  Тип таблица значений
Функция ТипТаблицаЗначений() Экспорт
	ОписаниеТиповТЗ = Новый ОписаниеТипов("ТаблицаЗначений");
	Возврат ОписаниеТиповТЗ.Типы()[0];
	
КонецФункции

#КонецОбласти

#Область РедакторТипов

// Доступные наборы типов для редактирования.
// 
// Возвращаемое значение:
//  Структура -  Доступные наборы типов для редактирования:
// * Ссылки - Строка - 
// * СоставныеСсылки - Строка - 
// * Примитивные - Строка - 
// * Null - Строка - 
// * ХранилищеЗначения - Строка - 
// * КоллекцииЗначений - Строка - 
// * МоментВремени - Строка - 
// * Тип - Строка - 
// * Граница - Строка - 
// * УникальныйИдентификатор - Строка - 
// * СтандартныйПериод - Строка - 
// * СистемныеПеречисления - Строка - 
Функция ДоступныеНаборыТиповДляРедактирования() Экспорт
	ДоступныеНаборы = Новый Структура;
	ДоступныеНаборы.Вставить("Ссылки","ССЫЛКИ");
	ДоступныеНаборы.Вставить("СоставныеСсылки","СОСТАВНЫЕССЫЛКИ");
	ДоступныеНаборы.Вставить("Примитивные","ПРИМИТИВНЫЕ");
	ДоступныеНаборы.Вставить("Null","NULL");
	ДоступныеНаборы.Вставить("ХранилищеЗначения","ХРАНИЛИЩЕЗНАЧЕНИЙ");
	ДоступныеНаборы.Вставить("КоллекцииЗначений","КОЛЛЕКЦИИЗНАЧЕНИЙ");
	ДоступныеНаборы.Вставить("МоментВремени","МОМЕНТВРЕМЕНИ");
	ДоступныеНаборы.Вставить("Тип","ТИП");
	ДоступныеНаборы.Вставить("Граница","ГРАНИЦА");
	ДоступныеНаборы.Вставить("УникальныйИдентификатор","УНИКАЛЬНЫЙИДЕНТИФИКАТОР");
	ДоступныеНаборы.Вставить("СтандартныйПериод","СТАНДАРТНЫЙПЕРИОД");
	ДоступныеНаборы.Вставить("СистемныеПеречисления","СИСТЕМНЫЕПЕРЕЧИСЛЕНИЯ");
	
	Возврат ДоступныеНаборы;
КонецФункции

// Наборы типов для редактирования по умолчанию для поля формы.
// 
// Возвращаемое значение:
// Массив из  Строка -  Наборы типов для редактирования по умолчанию для поля формы
Функция НаборыТиповДляРедактированияПоУмолчаниюДляПоляФормы() Экспорт
	ДоступныеНаборы = ДоступныеНаборыТиповДляРедактирования();
	
	Наборы = Новый Массив;
	Наборы.Добавить(ДоступныеНаборы.Ссылки);
	Наборы.Добавить(ДоступныеНаборы.Примитивные);
	Наборы.Добавить(ДоступныеНаборы.УникальныйИдентификатор);
	Возврат Наборы;
КонецФункции

// Все наборы типов для редактирования.
// 
// Возвращаемое значение:
// Массив из Строка -  Все наборы типов для редактирования
Функция ВсеНаборыТиповДляРедактирования() Экспорт
	ДоступныеНаборы = ДоступныеНаборыТиповДляРедактирования();
	
	Наборы = Новый Массив;
	
	Для Каждого КлючЗначение Из ДоступныеНаборы Цикл
		Наборы.Добавить(КлючЗначение.Значение);
	КонецЦикла;
	
	Возврат Наборы;
	
КонецФункции

// Хранимые наборы типов для редактирования.
// 
// Возвращаемое значение:
//  Массив из Строка-  Хранимые наборы типов для редактирования
Функция ХранимыеНаборыТиповДляРедактирования() Экспорт
	ДоступныеНаборы = ДоступныеНаборыТиповДляРедактирования();
	
	Наборы = Новый Массив;
	Наборы.Добавить(ДоступныеНаборы.Ссылки);
	Наборы.Добавить(ДоступныеНаборы.СоставныеСсылки);
	Наборы.Добавить(ДоступныеНаборы.Примитивные);
	Наборы.Добавить(ДоступныеНаборы.ХранилищеЗначения);
	Наборы.Добавить(ДоступныеНаборы.УникальныйИдентификатор);
	
	Возврат Наборы;
	
КонецФункции

// Наборы типов для поля схемы компоновки данных.
// 
// Возвращаемое значение:
//  Массив из Строка -  Наборы типов для поля схемы компоновки данных
Функция НаборыТиповДляПоляСхемыКомпоновкиДанных() Экспорт
	ДоступныеНаборы = ДоступныеНаборыТиповДляРедактирования();
	
	Наборы = Новый Массив;
	Наборы.Добавить(ДоступныеНаборы.Ссылки);
	Наборы.Добавить(ДоступныеНаборы.СоставныеСсылки);
	Наборы.Добавить(ДоступныеНаборы.Примитивные);
	Наборы.Добавить(ДоступныеНаборы.ХранилищеЗначения);
	Наборы.Добавить(ДоступныеНаборы.УникальныйИдентификатор);
	Наборы.Добавить(ДоступныеНаборы.Null);
	
	Возврат Наборы;
		
КонецФункции

// Пустой уникальный идентификатор.
// 
// Возвращаемое значение:
//  УникальныйИдентификатор
Функция ПустойУникальныйИдентификатор() Экспорт
	Возврат Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000");
КонецФункции

#КонецОбласти

#Область КонтейнерЗначенияДляХраненияНаФорме

// Типы значений хранимые в конрейнерах.
// 
// Возвращаемое значение:
//  Массив из Тип-  Типы значений хранимые в конрейнерах
Функция ТипыЗначенийХранимыеВКонрейнерах() Экспорт
	Типы = Новый Массив;
	Типы.Добавить(Тип("МоментВремени"));
#Если НаКлиенте Тогда
	Типы.Добавить(ТипТаблицаЗначений());
#Иначе
	Типы.Добавить(Тип("ТаблицаЗначений"));
#КонецЕсли
	Типы.Добавить(Тип("Тип"));
	Типы.Добавить(Тип("ХранилищеЗначения"));
	Типы.Добавить(Тип("Массив"));
	Типы.Добавить(Тип("СписокЗначений"));
	Типы.Добавить(Тип("Граница"));
	Типы.Добавить(Тип("ТабличныйДокумент"));
	
	Возврат Типы;	
КонецФункции

// Типы контейнеров значения.
// 
// Возвращаемое значение:
//  Структура -  Типы контейнеров значения:
// * ТаблицаЗначений - Строка - 
// * МоментВремени - Строка - 
// * Тип - Строка - 
// * ОписаниеТипов - Строка - 
// * ХранилищеЗначения - Строка - 
// * Массив - Строка - 
// * СписокЗначений - Строка - 
// * Граница - Строка - 
// * ТабличныйДокумент - Строка - 
Функция ТипыКонтейнеровЗначения() Экспорт
	ТипыКонтейнеров = Новый Структура;
	ТипыКонтейнеров.Вставить("ТаблицаЗначений", "ТАБЛИЦАЗНАЧЕНИЙ");
	ТипыКонтейнеров.Вставить("МоментВремени", "МОМЕНТВРЕМЕНИ");
	ТипыКонтейнеров.Вставить("Тип", "ТИП");
	ТипыКонтейнеров.Вставить("ОписаниеТипов", "ОПИСАНИЕТИПОВ");
	ТипыКонтейнеров.Вставить("ХранилищеЗначения", "ХРАНИЛИЩЕЗНАЧЕНИЯ");
	ТипыКонтейнеров.Вставить("Массив", "МАССИВ");
	ТипыКонтейнеров.Вставить("СписокЗначений", "СПИСОКЗНАЧЕНИЙ");
	ТипыКонтейнеров.Вставить("Граница", "ГРАНИЦА");
	ТипыКонтейнеров.Вставить("ТабличныйДокумент", "ТАБЛИЧНЫЙДОКУМЕНТ");
	
	Возврат ТипыКонтейнеров;
КонецФункции

// Новый контейнер значения.
// 
// Возвращаемое значение:
//  Структура -  Новый контейнер значения:
// * Тип - Строка - доступные типы см. ТипыКонтейнеровЗначения
// * Хранилище - Произвольный, Неопределено - Само значение или хранилище, которое можно помещать в данные формы. 
// * Представление - Строка - Как показывать значение пользователю
Функция НовыйКонтейнерЗначения() Экспорт
	Контейнер = Новый Структура;
	Контейнер.Вставить("Тип", "");
	Контейнер.Вставить("Хранилище", Неопределено);
	Контейнер.Вставить("Представление", "");
	
	Возврат Контейнер;
КонецФункции

// Новый хранилище значения типа граница.
// 
// Возвращаемое значение:
//  Структура -  Новый хранилище значения типа граница:
// * ВидГраницы - Строка - Включая или Исключая
// * Дата - Дата - 
Функция НовыйХранилищеЗначенияТипаГраница() Экспорт
	Хранилище = Новый Структура;
	Хранилище.Вставить("ВидГраницы","Включая");
	Хранилище.Вставить("Дата",'00010101');
	
	Возврат Хранилище;	
КонецФункции

// Новый хранилище значения типа момент времени.
// 
// Возвращаемое значение:
//  Структура -  Новый хранилище значения типа момент времени:
// * Дата - Дата - 
// * Ссылка - Неопределено, ЛюбаяСсылка, ОбсуждениеСистемыВзаимодействия - 
Функция НовыйХранилищеЗначенияТипаМоментВремени() Экспорт
	Хранилище = Новый Структура;
	Хранилище.Вставить("Дата", '00010101');
	Хранилище.Вставить("Ссылка", Неопределено);

	Возврат Хранилище;
КонецФункции

// Суффикс имени поля хранения типа значения для поля с контейнером.
// 
// Возвращаемое значение:
//  Строка -  Суффикс имени поля хранения типа значения для поля с контейнером
Функция СуффиксИмениПоляХраненияТипаЗначенияДляПоляСКонтейнером() Экспорт
	Возврат "ТипЗначения";
КонецФункции

// Суффикс имени поля хранения представления типа значения для поля с контейнером.
// 
// Возвращаемое значение:
//  Строка -  Суффикс имени поля хранения представления типа значения для поля с контейнером
Функция СуффиксИмениПоляХраненияПредставленияТипаЗначенияДляПоляСКонтейнером() Экспорт
	Возврат "ТипЗначенияПредставление";
КонецФункции

// Суффикс имени поля хранения контейнера для поля с контейнером.
// 
// Возвращаемое значение:
//  Строка -  Суффикс имени поля хранения контейнера для поля с контейнером
Функция СуффиксИмениПоляХраненияКонтейнераДляПоляСКонтейнером() Экспорт
	Возврат "Контейнер";
КонецФункции

// Новый контейнер значения по типу.
// 
// Параметры:
//  Тип - Тип
// 
// Возвращаемое значение:
//  Структура -  см. НовыйКонтейнерЗначения
Функция НовыйКонтейнерЗначенияПоТипу(Тип) Экспорт
	ТипыКонтейнеров = ТипыКонтейнеровЗначения();
	
	Контейнер = НовыйКонтейнерЗначения();
	
	Если Тип = Тип("Граница") Тогда
		Контейнер.Тип = ТипыКонтейнеров.Граница;
	ИначеЕсли Тип = Тип("МоментВремени") Тогда 
		Контейнер.Тип = ТипыКонтейнеров.МоментВремени;
	КонецЕсли;
	
	Возврат Контейнер;
КонецФункции

// Значение хранилища контейнера момента времени по дате и ссылке.
// 
// Параметры:
//  Дата - Дата -  Дата
//  Ссылка - СсылкаНаОбъектИнформационнойБазыИлиОбсуждениеСистемыВзаимодействия -  Ссылка
// 
// Возвращаемое значение:
//  см. УИ_ОбщегоНазначенияКлиентСервер.НовыйХранилищеЗначенияТипаМоментВремени
Функция ЗначениеХранилищаКонтейнераМоментаВремениПоДатеИСсылке(Дата, Ссылка) Экспорт
	Хранилище = НовыйХранилищеЗначенияТипаМоментВремени();
	Хранилище.Дата = Дата;
	Хранилище.Ссылка = Ссылка;
	Возврат Хранилище;
КонецФункции

// Установить представление контейнера.
// 
// Параметры:
//  КонтейнерЗначения - см. НовыйКонтейнерЗначения
Процедура УстановитьПредставлениеКонтейнера(КонтейнерЗначения) Экспорт
	ТипыКонтейнеров = ТипыКонтейнеровЗначения();
	Если КонтейнерЗначения.Тип = ТипыКонтейнеров.МоментВремени Тогда
		Если КонтейнерЗначения.Хранилище <> Неопределено Тогда
			КонтейнерЗначения.Представление = Строка(КонтейнерЗначения.Хранилище.Дата)
											  + "; "
											  + КонтейнерЗначения.Хранилище.Ссылка;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

// Описание ОСДля техподдержки.
// 
// Возвращаемое значение:
//  Структура -  Описание ОСДля техподдержки:
// * ВерсияОС - Строка - 
// * Разрядность - Строка - 
// * Процессор - Строка - 
// * Память - Строка - 
Функция ОписаниеОСДляТехподдержки() Экспорт
	СистемнаяИнформация = Новый СистемнаяИнформация();
	
	Описание = Новый Структура;
	Описание.Вставить("ВерсияОС", СистемнаяИнформация.ВерсияОС);
	Описание.Вставить("Разрядность", Строка(СистемнаяИнформация.ТипПлатформы));
	Описание.Вставить("Процессор", СистемнаяИнформация.Процессор);
	Описание.Вставить("Память", Строка(СистемнаяИнформация.ОперативнаяПамять));
	
	Возврат Описание;	
КонецФункции

// Текущая версия платформы1 с предприятие.
// 
// Возвращаемое значение:
//  Строка -  Текущая версия платформы 1С предприятие
Функция ТекущаяВерсияПлатформы1СПредприятие() Экспорт

	СистИнфо = Новый СистемнаяИнформация;
	Возврат СистИнфо.ВерсияПриложения;

КонецФункции

// Тип управляемой формы.
// 
// Возвращаемое значение:
//  Тип -  Тип управляемой формы
Функция ТипУправляемойФормы() Экспорт
	Если ВерсияПлатформыНеМладше_8_3_14() Тогда
		//@skip-check wrong-string-literal-content
		Возврат Тип("ФормаКлиентскогоПриложения")
	Иначе
		Возврат Тип("УправляемаяФорма");
	КонецЕсли;
КонецФункции

// Таблица элемента формы.
// 
// Параметры:
//  Элемент - ПолеФормы, ГруппаФормы, ТаблицаФормы -
// 
// Возвращаемое значение:
//  ТаблицаФормы
// Возвращаемое значение:
//  Неопределено - Элемент не принадлежит таблице формы
Функция ТаблицаЭлементаФормы(Элемент) Экспорт
	ТекЭлемент = Элемент;
	ТипФормы = ТипУправляемойФормы();

	Пока ТипЗнч(ТекЭлемент) <> Тип("ТаблицаФормы") Цикл
		Если ТекЭлемент.Родитель = Неопределено Или ТипЗнч(ТекЭлемент.Родитель) = ТипФормы Тогда

			Возврат Неопределено;
		КонецЕсли;

		ТекЭлемент =  ТекЭлемент.Родитель;
	КонецЦикла;

	Возврат ТекЭлемент;
КонецФункции

// Возвращает код основного языка конфигурации, например "ru".
//
// Возвращаемое значение:
//  Строка - код языка.
//
Функция КодОсновногоЯзыка() Экспорт
#Если Не ТонкийКлиент И Не ВебКлиент И Не МобильныйКлиент Тогда
	Возврат Метаданные.ОсновнойЯзык.КодЯзыка;
#Иначе
		Возврат УИ_ОбщегоНазначенияПовтИсп.КодОсновногоЯзыка();
#КонецЕсли
КонецФункции

// Формирует и выводит сообщение, которое может быть связано с элементом 
// управления формы.
//
// Параметры:
//  ТекстСообщенияПользователю - Строка - текст сообщения.
//  КлючДанных                 - ЛюбаяСсылка - объект или ключ записи информационной базы, к которому это сообщение относится.
//  Поле                       - Строка - наименование реквизита формы.
//  ПутьКДанным                - Строка - путь к данным (путь к реквизиту формы).
//  Отказ                      - Булево - выходной параметр, всегда устанавливается в значение Истина.
//
// Пример:
//
//  1. Для вывода сообщения у поля управляемой формы, связанного с реквизитом объекта:
//  ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
//   НСтр("ru = 'Сообщение об ошибке.'"), ,
//   "ПолеВРеквизитеФормыОбъект",
//   "Объект");
//
//  Альтернативный вариант использования в форме объекта:
//  ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
//   НСтр("ru = 'Сообщение об ошибке.'"), ,
//   "Объект.ПолеВРеквизитеФормыОбъект");
//
//  2. Для вывода сообщения рядом с полем управляемой формы, связанным с реквизитом формы:
//  ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
//   НСтр("ru = 'Сообщение об ошибке.'"), ,
//   "ИмяРеквизитаФормы");
//
//  3. Для вывода сообщения связанного с объектом информационной базы:
//  ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
//   НСтр("ru = 'Сообщение об ошибке.'"), ОбъектИнформационнойБазы, "Ответственный",,Отказ);
//
//  4. Для вывода сообщения по ссылке на объект информационной базы:
//  ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
//   НСтр("ru = 'Сообщение об ошибке.'"), Ссылка, , , Отказ);
//
//  Случаи некорректного использования:
//   1. Передача одновременно параметров КлючДанных и ПутьКДанным.
//   2. Передача в параметре КлючДанных значения типа отличного от допустимых.
//   3. Установка ссылки без установки поля (и/или пути к данным).
//
//@skip-check method-too-many-params
Процедура СообщитьПользователю(Знач ТекстСообщенияПользователю, Знач КлючДанных = Неопределено, Знач Поле = "",
	Знач ПутьКДанным = "", Отказ = Ложь) Экспорт

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщенияПользователю;
	Сообщение.Поле = Поле;

	ЭтоОбъект = Ложь;

#Если Не ТонкийКлиент И Не ВебКлиент Тогда
	Если КлючДанных <> Неопределено И XMLТипЗнч(КлючДанных) <> Неопределено Тогда
		ТипЗначенияСтрокой = XMLТипЗнч(КлючДанных).ИмяТипа;
		ЭтоОбъект = СтрНайти(ТипЗначенияСтрокой, "Object.") > 0;
	КонецЕсли;
#КонецЕсли

	Если ЭтоОбъект Тогда
		Сообщение.УстановитьДанные(КлючДанных);
	Иначе
		Сообщение.КлючДанных = КлючДанных;
	КонецЕсли;

	Если Не ПустаяСтрока(ПутьКДанным) Тогда
		Сообщение.ПутьКДанным = ПутьКДанным;
	КонецЕсли;

	Сообщение.Сообщить();

	Отказ = Истина;

КонецПроцедуры

// Добавить массив объектов к сравнению.
// 
// Параметры:
//  Объекты - Массив из ЛюбаяСсылка, СписокЗначений из ЛюбаяСсылка ,ЛюбаяСсылка-Объекты
Процедура ДобавитьМассивОбъектовКСравнению(Объекты) Экспорт
	УИ_ОбщегоНазначенияВызовСервера.ДобавитьМассивОбъектовКСравнению(Объекты);
КонецПроцедуры

// Имя параметра отмененных длительных операций.
// 
// Параметры:
//  Параметры Параметры
// 
// Возвращаемое значение:
//  Строка -  Имя параметра отмененных длительных операций
Функция ИмяПараметраОтмененныхДлительныхОпераций(Параметры) Экспорт

	Возврат "УИ_ОтмененныеДлительныеОперации";
КонецФункции

// Вызывает исключение, если тип значения параметра ИмяПараметра процедуры или функции ИмяПроцедурыИлиФункции
// отличается от ожидаемого.
// Для диагностики типов параметров, передаваемых в процедуры и функции программного интерфейса.
//
// Параметры:
//   ИмяПроцедурыИлиФункции - Строка             - имя процедуры или функции, параметр которой проверяется.
//   ИмяПараметра           - Строка             - имя проверяемого параметра процедуры или функции.
//   ЗначениеПараметра      - Произвольный       - фактическое значение параметра.
//   ОжидаемыеТипы  - ОписаниеТипов, Тип, Массив - тип(ы) параметра процедуры или функции.
//   ОжидаемыеТипыСвойств   - Структура          - если ожидаемый тип - структура, то 
//                                                 в этом параметре можно указать типы ее свойств.
//
Процедура ПроверитьПараметр(Знач ИмяПроцедурыИлиФункции, Знач ИмяПараметра, Знач ЗначениеПараметра, Знач ОжидаемыеТипы,
	Знач ОжидаемыеТипыСвойств = Неопределено) Экспорт

	Контекст = "ОбщегоНазначенияКлиентСервер.ПроверитьПараметр";
	Проверить(ТипЗнч(ИмяПроцедурыИлиФункции) = Тип("Строка"), НСтр(
		"ru = 'Недопустимо значение параметра ИмяПроцедурыИлиФункции'"), Контекст);
	Проверить(ТипЗнч(ИмяПараметра) = Тип("Строка"), НСтр("ru = 'Недопустимо значение параметра ИмяПараметра'"),
		Контекст);

	ЭтоКорректныйТип = ЗначениеОжидаемогоТипа(ЗначениеПараметра, ОжидаемыеТипы);
	Проверить(ЭтоКорректныйТип <> Неопределено, НСтр("ru = 'Недопустимо значение параметра ОжидаемыеТипы'"), Контекст);

	НедопустимыйПараметр = НСтр("ru = 'Недопустимое значение параметра %1 в %2. 
								|Ожидалось: %3; передано значение: %4 (тип %5).'");
	Проверить(ЭтоКорректныйТип, СтрШаблон(НедопустимыйПараметр, ИмяПараметра, ИмяПроцедурыИлиФункции,
		ПредставлениеТипов(ОжидаемыеТипы), ?(ЗначениеПараметра <> Неопределено, ЗначениеПараметра, НСтр(
		"ru = 'Неопределено'")), ТипЗнч(ЗначениеПараметра)));

	Если ТипЗнч(ЗначениеПараметра) = Тип("Структура") И ОжидаемыеТипыСвойств <> Неопределено Тогда

		Проверить(ТипЗнч(ОжидаемыеТипыСвойств) = Тип("Структура"), НСтр(
			"ru = 'Недопустимо значение параметра ИмяПроцедурыИлиФункции'"), Контекст);

		НетСвойства = НСтр("ru = 'Недопустимое значение параметра %1 (Структура) в %2. 
						   |В структуре ожидалось свойство %3 (тип %4).'");
		НедопустимоеСвойство = НСтр("ru = 'Недопустимое значение свойства %1 в параметре %2 (Структура) в %3. 
									|Ожидалось: %4; передано значение: %5 (тип %6).'");
		Для Каждого Свойство Из ОжидаемыеТипыСвойств Цикл

			ОжидаемоеИмяСвойства = Свойство.Ключ;
			ОжидаемыйТипСвойства = Свойство.Значение;
			ЗначениеСвойства = Неопределено;

			Проверить(ЗначениеПараметра.Свойство(ОжидаемоеИмяСвойства, ЗначениеСвойства), СтрШаблон(НетСвойства,
				ИмяПараметра, ИмяПроцедурыИлиФункции, ОжидаемоеИмяСвойства, ОжидаемыйТипСвойства));

			ЭтоКорректныйТип = ЗначениеОжидаемогоТипа(ЗначениеСвойства, ОжидаемыйТипСвойства);
			Проверить(ЭтоКорректныйТип, СтрШаблон(НедопустимоеСвойство, ОжидаемоеИмяСвойства, ИмяПараметра,
				ИмяПроцедурыИлиФункции, ПредставлениеТипов(ОжидаемыеТипы), ?(ЗначениеСвойства <> Неопределено,
				ЗначениеСвойства, НСтр("ru = 'Неопределено'")), ТипЗнч(ЗначениеСвойства)));
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

// Вызывает исключение с текстом Сообщение, если Условие не равно Истина.
// Применяется для самодиагностики кода.
//
// Параметры:
//   Условие                - Булево - если не равно Истина, то вызывается исключение.
//   КонтекстПроверки       - Строка - например, имя процедуры или функции, в которой выполняется проверка.
//   Сообщение              - Строка - текст сообщения. Если не задан, то исключение вызывается с сообщением по
//                                     умолчанию.
//
Процедура Проверить(Знач Условие, Знач Сообщение = "", Знач КонтекстПроверки = "") Экспорт

	Если Условие <> Истина Тогда
		Если ПустаяСтрока(Сообщение) Тогда
			ТекстИсключения = НСтр("ru = 'Недопустимая операция'"); // Assertion failed
		Иначе
			ТекстИсключения = Сообщение;
		КонецЕсли;
		Если Не ПустаяСтрока(КонтекстПроверки) Тогда
			ТекстИсключения = ТекстИсключения + " " + СтрШаблон(НСтр("ru = 'в %1'"), КонтекстПроверки);
		КонецЕсли;
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;

КонецПроцедуры

// Получает ссылку предопределенного элемента по его полному имени.
// Предопределенные элементы могут содержаться только в следующих объектах:
//   - Справочники;
//   - Планы видов характеристик;
//   - Планы счетов;
//   - Планы видов расчета.
//
// Параметры:
//   ПолноеИмяПредопределенного - Строка - Полный путь к предопределенному элементу, включая его имя.
//     Формат аналогичен функции глобального контекста ПредопределенноеЗначение().
//     Например:
//       "Справочник.ВидыКонтактнойИнформации.EmailПользователя"
//       "ПланСчетов.Хозрасчетный.Материалы"
//       "ПланВидовРасчета.Начисления.ОплатаПоОкладу".
//
// Возвращаемое значение: 
//   ЛюбаяСсылка - ссылка на предопределенный элемент.
//   Неопределено - если предопределенный есть в метаданных, но не создан в ИБ.
//
Функция ПредопределенныйЭлемент(ПолноеИмяПредопределенного) Экспорт

// Используется стандартная функция платформы для получения:
	//  - пустых ссылок; 
	//  - значений перечислений;
	//  - точек маршрута бизнес-процессов.
	Если ".ПУСТАЯССЫЛКА" = ВРег(Прав(ПолноеИмяПредопределенного, 13)) Или "ПЕРЕЧИСЛЕНИЕ." = ВРег(Лев(
		ПолноеИмяПредопределенного, 13)) Или "БИЗНЕСПРОЦЕСС." = ВРег(Лев(ПолноеИмяПредопределенного, 14)) Тогда

		Возврат ПредопределенноеЗначение(ПолноеИмяПредопределенного);
	КонецЕсли;

	// Разбор полного имени предопределенного.
	ЧастиПолногоИмени = СтрРазделить(ПолноеИмяПредопределенного, ".");
	Если ЧастиПолногоИмени.Количество() <> 3 Тогда
		ВызватьИсключение ТекстОшибкиПредопределенноеЗначениеНеНайдено(ПолноеИмяПредопределенного);
	КонецЕсли;

	ПолноеИмяОбъектаМетаданных = ВРег(ЧастиПолногоИмени[0] + "." + ЧастиПолногоИмени[1]);
	ИмяПредопределенного = ЧастиПолногоИмени[2];

	// В зависимости от контекста выполняется обращение к разному кэшу.

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	ПредопределенныеЗначения = УИ_ОбщегоНазначенияПовтИсп.СсылкиПоИменамПредопределенных(ПолноеИмяОбъектаМетаданных);
#Иначе
		ПредопределенныеЗначения = УИ_ОбщегоНазначенияКлиентПовтИсп.СсылкиПоИменамПредопределенных(
			ПолноеИмяОбъектаМетаданных);
#КонецЕсли

	// Если ошибка в имени метаданных.
	Если ПредопределенныеЗначения = Неопределено Тогда
		ВызватьИсключение ТекстОшибкиПредопределенноеЗначениеНеНайдено(ПолноеИмяПредопределенного);
	КонецЕсли;

	// Получение результата из кэша.
	Результат = ПредопределенныеЗначения.Получить(ИмяПредопределенного);

	// Если предопределенного нет в метаданных.
	Если Результат = Неопределено Тогда
		ВызватьИсключение ТекстОшибкиПредопределенноеЗначениеНеНайдено(ПолноеИмяПредопределенного);
	КонецЕсли;

	// Если предопределенный есть в метаданных, но не создан в ИБ.
	Если Результат = Null Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ТекстОшибкиПредопределенноеЗначениеНеНайдено(ПолноеИмяПредопределенного)

	Возврат СтрШаблон(НСтр("ru = 'Предопределенное значение ""%1"" не найдено.'"), ПолноеИмяПредопределенного);

КонецФункции



#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// предназначен для модулей, которые являются частью некоторой функциональной подсистемы. В нем должны быть размещены экспортные процедуры и функции, которые допустимо вызывать только из других функциональных подсистем этой же библиотеки.
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// содержит процедуры и функции, составляющие внутреннюю реализацию общего модуля. В тех случаях, когда общий модуль является частью некоторой функциональной подсистемы, включающей в себя несколько объектов метаданных, в этом разделе также могут быть размещены служебные экспортные процедуры и функции, предназначенные только для вызова из других объектов данной подсистемы.
#КонецОбласти


